#Garden Home Hotel - Entire complex
#Copyright (c) 2023-Present - Mn10

#Originally started on: Not later than 23 May 2023.
#Updated 7 November 2024:
#1. Separated the South Wing building into its own file.
#2. Added east parking elevators.

#Scripts to be included
	#1. My new scripts
	<Include buildings/Mn10_Scripts/Deco2023_R1.txt>
	<Include buildings/Mn10_Scripts/Shafts_and_Elevators.txt>
	<Include buildings/Mn10_Scripts/Windows.txt>

	#2. ObjectPlus and SROC (credit to EG881)
	<Include ObjectPlus/Scripts/OP_Script2.txt> #EG881
	<Include SROC/Scripts/r1_script.txt> #EG881

	#3. My previous scripts
	<Include Deco2011/Scripts/r4_script.txt>
	<Include CustomObjects2018/Scripts/r1_script.txt>
	<Include Shaft Wall Creator/Scripts/r1_script.txt>
	<Include data/ACME Floor Indicator/Scripts/Indicators.txt>

#Add buildings
<Buildings>
	#Disable landscape cutting
	CutLandscapes = false

	#Add all builings
	Load GHH - North Wing.bld, -75, 0, 245, 0, -105 - 20, 0, -50 - 9, 105 + 5, 0, 50 + 10
	Load GHH - South Wing.bld, 0, 0, 0, 0, -200, 0, -125, 140, 0, 140
<EndBuildings>

##--------------------------------------------------------------------------------##

#Globals
<Globals>
	Name = Garden Home Hotel
	Designer = Mn10
	Location = 520 3rd Street, Tranquility City
	Version = 1
	CameraFloor = 0
	CameraPosition = 150, 10
	CameraRotation = 0, 90, 0
<EndGlobals>

##--------------------------------------------------------------------------------##

#Textures
<Textures>
	#Landscapes
	Load data/downtown.jpg, Downtown, 1, 1
	Load data/walkway.jpg, Sidewalk, 0.03125, 0.03125
	Load data/Melvin/ABH_Tex/walkway-darker.jpg, SidewalkDarker, 0.03125, 0.03125

		#Plants
		Load data/ogrebullet/textures/grass.jpg, Grass, 0.005, 0.005
		Load data/EG881/Trees_Summer/BradfordPear1.png, Tree1, 1, 1, false #EG881
		Load data/EG881/tree1.gif, Tree2, 1, 1, false #EG881

		#Roads
		Load data/grndroad1mid.jpg, Road, 0.04, 0.04
		Load data/EG881/2lanex_doubleline.jpg, ParkingRampRoad, 0.05, 0.041667 #EG881

		#Road markings
		Load data/EG881/parkingspacex.png, ParkingSpaceX, 1, 1, false #EG881
		Load data/EG881/parkingspacez.png, ParkingSpaceZ, 1, 1, false #EG881
		Load data/EG881Mod/NoParkingX.png, NoParkingX, 1, 1, false #EG881 #Yellow islands
		Load data/EG881Mod/NoParkingZ.png, NoParkingZ, 1, 1, false #EG881
		Load data/EG881Mod/WhiteIslandX.png, WhiteIslandX, 1, 1, false #EG881
		Load data/EG881Mod/WhiteIslandZ.png, WhiteIslandZ, 1, 1, false #EG881

	#Windows
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky2.jpg, MainWindows, true, 0.05, 1 #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsInt, 0.05, 1 #EG881
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky2.jpg, MainWindowsB, true, 1, 1, false #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsIntB, 1, 1, false #EG881

	#External facade
	Load data/cutston.jpg, ExtFacade, 0.075, 0.075

	#Interior stuff
		#East parking elevator lobby (Levels P4-P1)
		Load data/Carpet0013b_S.jpg, EastPK_PLFloor, 0.2, 0.2
		Load data/EG881/granite_0007e_black.jpg, EastPK_PLWall, 0.075, 0.075 #EG881 #Lower walls
		Load data/stucco5.jpg, EastPK_PLWall_Upper, 0.2, 0.2 #Upper walls (P4-P1)
		Load data/EG881/ceilingtile3x3.jpg, EastPK_PLCeiling, 0.125, 0.125 #EG881

			#Shaft doors
			Load data/EG881/elevdoorleft_black.jpg, EastPK_PLShaftDoors, 1, 1, false #EG881
			Load data/EG881/elevdoorright_black.jpg, EastPK_PLShaftDoors2, 1, 1, false #EG881
			Load data/EG881/elevdoortrim_black.jpg, EastPK_PLShaftDoors_Trim, 1, 1, false #EG881

		#Stairs from P4 to L
		Load data/floor065.jpg, EastPK_PLStairs, 0.25, 0.25
		Load data/stucco5.jpg, EastPK_MainStairsWall, 0.2, 0.2
		Load data/black_granite.jpg, EastPK_PLStairsWall, 0.1, 0.1
		Load data/EG881/granite_0007e_black.jpg, EastPK_PLStairsWall2, 0.075, 0.075 #EG881
		Load data/st_wall2.jpg, EastPK_GLStairsWall, 0.13635, 0.09765
		Load data/st_wall.jpg, EastPK_GLStairsWall2, 0.1875, 0.1344

		#East parking elevator lobby (Lobby level)
		Load data/floor065.jpg, EastPK_GLFloor, 0.25, 0.25
		Load data/Carpet0013_S.jpg, EastPK_GLCarpet, 0.2, 0.2
		Load data/st_wall.jpg, EastPK_GLWall, 0.1875, 0.1344 #Whole walls (Lobby level)
		Load data/EG881/ceilingtile2b.jpg, EastPK_GLCeiling, 0.125, 0.125 #EG881

			#Shaft doors
			Load data/Melvin/SRH/st_doors_blackedge.jpg, EastPK_GLShaftDoors, 1, 1, false
			Load data/Melvin/SRH/st_doors_blackedge.jpg, EastPK_GLShaftDoors2, -1, 1, false
			Load data/Melvin/st_panel.jpg, EastPK_GLShaftDoors_Trim, 1, 1, false

	#Doors
		#Door trims
		Load data/EG881/blackdoortrim.jpg, BlackTrim, 1, 1, false #EG881

		#Main doors
		#Load data/miscdoor88.gif, Door88, 1, 1, false
		#LoadCropped data/miscdoor88.gif, Door88Side, 236, 0, 20, 512, 1, 1, false
		#Load data/miscdoor9b.jpg, Door9b, 1, 1, false
		#Load data/EG881/Doors/miscdoor9bside.jpg, Door9bSide, 1, 1, false #EG881

		#Service doors
		Load data/EG881/Doors/servicedoor2_graynovents.jpg, ServiceDoor2, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2back_graynovents.jpg, ServiceDoor2B, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2_graynovents_side.jpg, ServiceDoor2Side, 1, 1, false #EG881

		#Black entrance doors
		Load data/EG881/miscdoor8_blacktrans.png, BLKEntDoorInt, 1, 1, false #EG881
		LoadAlphaBlend data/EG881/miscdoor8_blacktranstinted.jpg, data/EG881/miscdoor8_blacktranspec.png, data/sky2.jpg, BLKEntDoorExt, true, 1, 1, false #EG881
		LoadCropped data/EG881/miscdoor8_blacktrans.png, BLKEntDoorSide, 236, 0, 20, 512, 1, 1, false #EG881

	#Stairwells
	Load data/EG881/cem_tr2b.jpg, Stairs, 0.075225, 0.0625 #EG881
	Load data/EG881/cem_tr2b.jpg, StairsFloor, 0.075225, 0.0625 #EG881
	Load data/stucco1.jpg, StairsWall, 0.25, 0.25
	Load data/stucco3.jpg, StairsWall2, 0.25, 0.25
	Load data/EG881/cem_tr2b.jpg, StairsCeiling, 0.075225, 0.0625 #EG881

	#Shafts
	Load data/EG881/cem_tr2b.jpg, ShaftIntWall, 0.075225, 0.0625 #EG881

	#Elevators
		#Main components
		Load data/metal1.jpg, ElevExt, 0.25, 0.25, true
		Load data/counterweight.png, CWeight, 1, 1, false
		Load data/counterweight_frame.png, CWeight_Frame, 1, 1, false
		Load data/ropes.png, Ropes, 1, 1, false
		Load data/metal080.jpg, ElevRail, 0.5, 0.5
		Load data/metal1.jpg, ElevRailSide, 0.25, 0.25
		Load data/EG881/elevatordoortrackx.jpg, ElevDoorTrackX, 1, 1, false #EG881
		Load data/EG881/elevatordoortrackz.jpg, ElevDoorTrackZ, 1, 1, false #EG881
		Load data/panel-back3.jpg, ElevPanel, 1, 1, false
		Load data/panel-back3.jpg, ElevExtPanels, 1, 1, false

		#East Parking Elevators
			#Floor and Ceiling
			Load data/Carpet0021b_S.jpg, ParkingElevFloor, 0.2, 0.2
			Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, ParkingElevCeiling, 1, 1, false
			Rotate ParkingElevCeiling, 270
			Rotate ParkingElevCeiling, 90

			#Ceiling Lights
			Load data/EG881/Lights/squarelight1_white.jpg, ParkingElevCeilingLight, 1, 1, false #EG881 #Elevators 1-6
			Load data/panel-back3.jpg, ParkingElevCeilingTrim, 1, 1, false
			Load data/panel-back3.jpg, ParkingElevCeilingTrim2, 1, 1, false
			Rotate ParkingElevCeilingTrim2, 90

			#Walls and panels
			Load data/EG881/blackdoortrim.jpg, ParkingElevWall, 1, 1, false #EG881
			Load data/marb148.jpg, ParkingElevWallPanel, 0.25, 0.25
			Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, ParkingElevPanelMain, 1, 1, false
			Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, ParkingElevPanelTop, 1, 0.0667, false
			
			#Car doors
			Load data/Melvin/OTIS67_Darkaluminium_door.jpg, ParkingElevDoors, 1, 1, false #EG881
			Load data/Melvin/OTIS67_Darkaluminium_door.jpg, ParkingElevDoors2, -1, 1, false #EG881

	#Elevator fixtures
		#Standard elevators (Hall buttons)
		Load data/Vector's Fixtures/Westinghouse RT/wrtupunlit.png, ButtonUp, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtuplit.png, ButtonUpLit, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtudnunlit.png, ButtonDown, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtdnlit.png, ButtonDownLit, 1, 1, false

		#Standard elevators
		Load data/Vector's Fixtures/Westinghouse RT/wrtblankunlit.png, Button, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtblanklit.png, ButtonLit, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtdopen.png, ButtonOpen, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtdclose.png, ButtonClose, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtstop.png, ButtonStop, 1, 1, false
		Load data/Vector's Fixtures/Westinghouse RT/wrtalarm.png, ButtonAlarm, 1, 1, false

		AddTextRange 1, 4, Button, ButtonB%number%, nimbus_sans.ttf, 40, B%number%, 5, 0, 128, 128, center, center, 255, 255, 255
		AddTextRange 1, 4, ButtonLit, ButtonLitB%number%, nimbus_sans.ttf, 40, B%number%, 5, 0, 128, 128, center, center, 255, 255, 255
		AddText Button, Button1, nimbus_sans.ttf, 40, 1, 5, 0, 128, 128, center, center, 255, 255, 255
		AddText ButtonLit, ButtonLit1, nimbus_sans.ttf, 40, 1, 5, 0, 128, 128, center, center, 255, 255, 255

		#Directional and floor indicators
			#Lanterns
			Load data/Vector's Fixtures/SchindlerRT/rtuplanternunlit.png, LanternUp, 1, 1, false
			Load data/Vector's Fixtures/SchindlerRT/rtuplanternlit.png, LanternUpLit, 1, 1, false
			Load data/Vector's Fixtures/SchindlerRT/rtdnlanternunlit.png, LanternDown, 1, 1, false
			Load data/Vector's Fixtures/SchindlerRT/rtdnlanternlit.png, LanternDownLit, 1, 1, false

			#Floor indicators (Both outside and inside)
			Load data/ACME Floor Indicator/Resized/ACME Floor Indicator 2.jpg, IndBack, 1, 1, false
			AddTextRange 1, 4, IndBack, Ind_ButtonB%number%, acme_led.ttf, 240, B%number%, -1, -1, -1, -1, center, center, 255, 0, 0
			AddText IndBack, Ind_Button1, acme_led.ttf, 240, _1, -1, -1, -1, -1, center, center, 255, 0, 0

	#Elevator number/name signs
		#Elevator number signs (Inside)
		Load data/Melvin/transparent128.png, IN_ElevNoBack, 1, 1, false
		AddTextRange 1, 2, IN_ElevNoBack, IN_ElevNo%number%, nimbus_sans_bold.ttf, 50, P%number%, -1, -1, -1, -1, center, center, 0, 0, 0, false

	#Floor signs
		#Floor signs (Elevator lobby)
		Load data/Melvin/ABH_Tex/NewFS256/FSBack_SapphireService.jpg, FlrSignBack, 1, 1, false #Passenger
		AddTextRange 1, 4, FlrSignBack, FlrSignB%number%, nimbus_sans.ttf, 120, B%number%, 0, 0, 256, 198, center, center, 0, 0, 0, false
		AddText FlrSignBack, FlrSign1, nimbus_sans.ttf, 120, 1, 0, 0, 256, 198, center, center, 0, 0, 0, false

	#Other signs and objects
		#Exit signs
		Load data/panel-back3.jpg, StainlessSteel, 1, 1, false
		Load data/Melvin/ExitSignDown.png, ExitSign, 1, 1, false
		Load data/Melvin/ExitSignUp.png, ExitSignUp, 1, 1, false
		Load data/Melvin/ExitSignLeft.png, ExitSignLeft, 1, 1, false
		Load data/Melvin/ExitSignRight.png, ExitSignRight, 1, 1, false
		Load data/Melvin/ExitSignBoth.png, ExitSignBoth, 1, 1, false

		#Miscellaneous
		Load data/signs_mod/elevhallsign1.png, FireSign, 1, 1, false
		Load data/Melvin/Aceheart/MainFloorStar.png, MainFloorStar, 1, 1, false
<EndTextures>

##--------------------------------------------------------------------------------##

#Floor assignments
<Floors -1 to -4>
	ID = B(0 - %floor%)
	Name = Basement Level (0 - %floor%)
	Type = Basement
	Height = 8.5
	InterfloorHeight = 1.5
	if[%floor% = -1] Altitude = -11.5
<EndFloors>

<Floor -5>
	ID = %floor%
	Name = Shaft Pit
	Type = Basement
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 0>
	ID = 1
	Name = Level 1
	Type = Lobby
	Height = 15
	InterfloorHeight = 1.5
	Altitude = -1.5
<EndFloor>

##--------------------------------------------------------------------------------##

#Landscape
	#Coordinate Set
	Set MainX = -1096 + 35
	Set MainZ = 1644 + 110
	Set InGardenHomeHotel = true

	#Tranquility City Landscape
	<Include buildings/KW_Maps/Tranquility City.txt>

	#Landscape dimension for building(s)
	Set 1 = 135 #South Wing Dimension X
	Set 2 = 75 #South Wing Dimension Z
	
	#****************************************#

	#Cut landscape for the hotel buildings
	Cut Landscape, -200, -1, -125, 140, 1, 140, false, true #South Wing
	Cut Landscape, -75 - 125, -1, 245 - 59, -75 + 110, 1, 245 + 60, false, true #North Wing

	#****************************************#

	#Sidewalk east of South Wing
	DrawWalls = false, true, false, false, false, false
	Cut Landscape, 140, Floor(0).Base - 1, -20, 270, Floor(0).Base + 1, 45, false, true #Cut for "EastEntSidewalk"

	AddFloor landscape, EastEntSidewalk1, Sidewalk, 0, 140, -20, 270, -20 + 10, Floor(0).Base, Floor(0).Base, false, true, 0, 0
	AddFloor landscape, EastEntSidewalk2, SidewalkDarker, 0, 140, -20 + 10, 270, 45 - 10, Floor(0).Base, Floor(0).Base, false, true, 0, 0
	AddFloor landscape, EastEntSidewalk3, Sidewalk, 0, 140, 45 - 10, 270, 45, Floor(0).Base, Floor(0).Base, false, true, 0, 0

	#****************************************#

	#Additional pathways after North Wing's creation
	DrawWalls = false, true, false, false, false, false
	#Cut Landscape, -105, Floor(0).Base - 1, 75, -85, Floor(0).Base + 1, 95, false, true #South-North Wings (1)
	Cut Landscape, -120, Floor(0).Base - 1, 140, -90, Floor(0).Base + 1, 186, false, true #South-North Wings (2)
	Cut Landscape, -90, Floor(0).Base - 1, 305, -60, Floor(0).Base + 1, 345, false, true #North Wing-4th Street

	#AddFloor landscape, Pathway_SNWings1, Sidewalk, 0, -105, 75, -85, 95, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, Pathway_SNWings2, Sidewalk, 0, -120, 140, -90, 186, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, Pathway_NWing_4thStreet, Sidewalk, 0, -90, 305, -60, 345, Floor(0).Base, Floor(0).Base, 0, 0

	#****************************************#

	#Parking Entrance Cuts
	Cut Landscape, -215, -0.5 - 1, 130.75 - 24, -200, 1, 130.75, false, true
	Cut Landscape, -215 - 1, -0.5 - 1, 130.75 - 24, -200 + 1, 1, 130.75, true, false

		#Parking Entrance
		ReverseAxis = true
		DrawWalls = true, true, false, false, false, false
		AddFloor landscape, PGEntrance, Road, 0, -215, 130.75 - 24, -200, 130.75, -0.5, 0, 0, 0
		ReverseAxis = false

		#Sidewalk fillers
		DrawWalls = false, true, false, false, false, false
		AddWall landscape, PGEntrance_SF1, Sidewalk, 0, -215, 130.75 - 24, -200, 130.75 - 24, 0.5, 0.5, -0.5, -0.5, 0, 0
		DrawWalls = true, false, false, false, false, false
		AddWall landscape, PGEntrance_SF2, Sidewalk, 0, -215, 130.75, -200, 130.75, 0.5, 0.5, -0.5, -0.5, 0, 0

	#****************************************#

	#Trees
	#1. Trees between South & North Wings
		AddTree(Tree1, -120 - 10, 147.5, 13.5, 12, 0)
		AddTree(Tree1, -120 - 10, 147.5 + 15, 13.5, 12, 0)
		AddTree(Tree1, -120 - 10, 147.5 + 30, 13.5, 12, 0)

		AddTree(Tree1, -90 + 10, 147.5, 13.5, 12, 0)
		AddTree(Tree1, -90 + 10, 147.5 + 15, 13.5, 12, 0)
		AddTree(Tree1, -90 + 10, 147.5 + 30, 13.5, 12, 0)

	#2. Trees between North Wing & 4th Street
		AddTree(Tree1, -90 - 10, 317.5, 13.5, 12, 0)
		AddTree(Tree1, -90 - 10, 317.5 + 15, 13.5, 12, 0)

		AddTree(Tree1, -60 + 10, 317.5, 13.5, 12, 0)
		AddTree(Tree1, -60 + 10, 317.5 + 15, 13.5, 12, 0)

	#3. Trees at South Wing's east entrances
		#North
		AddTree(Tree1, 165 - 15, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 15, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 30, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 45, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 60, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 75, 45 + 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 90, 45 + 10, 13.5, 12, 0)

		#South
		AddTree(Tree1, 165 - 15, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 15, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 30, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 45, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 60, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 75, -20 - 10, 13.5, 12, 0)
		AddTree(Tree1, 165 + 90, -20 - 10, 13.5, 12, 0)

##--------------------------------------------------------------------------------##

#Set texture prefixes/suffixes
<Function TexPrefix>
	#Main prefixes
	Set 5 = EastPK_PL
	if[%floor% = 0] Set 5 = EastPK_GL
<EndFunction>

#East parking elevator lobby (Added on November 7, 2024)
Cut Landscape, 145 + 20, -1, 0, 175 + 20, 1, 25, false, true #Landscape cut for East Parking Elevators
Cut Landscape, 175 + 20, -1, 0, 175 + 20 + 10, 1, 20, false, true #Landscape cut for East Parking Elevators
Cut Landscape, 175 + 20, -1, 0, 175.5 + 20, 1, 25, false, true #Landscape cut for East Parking Elevators

<Floors -4 to 0>
	Set 1 = 135 #South Wing Dimension X
	Set 2 = 75 #South Wing Dimension Z
	TexPrefix() #Set texture prefixes

	#East Elevator Lobby
		#1. Floor
		Set WestEnd = 135 + 5
		if[%floor% = 0] Set WestEnd = 135 + 20 + 10

		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
		if[%floor% < 0] AddFloor EastLobbyFloor, %5%Floor, 0.5, %WestEnd%, 0, 175 + 20 + 0.66, 25, %interfloorheight%, %interfloorheight%, 0, 0, true
		if[%floor% = 0] AddFloor EastLobbyFloor1, %5%Floor, 0.5, %WestEnd%, 0, 175 + 20 + 0.66, 20, %interfloorheight%, %interfloorheight%, 0, 0, true
		if[%floor% = 0] AddFloor EastLobbyFloor2, %5%Floor, 0.5, %WestEnd%, 20, 175.5 + 20, 25, %interfloorheight%, %interfloorheight%, 0, 0, true

		#2. Ceiling
		Set ELH = %fullheight%
		if[%floor% = 0] Set ELH = %interfloorheight% + 9.5

		FloorOrientation = bottom
		DrawWalls = true, false, false, false, false, false
		AddFloor EastLobbyCeiling, %5%Ceiling, 0.5, %WestEnd%, 0, 175 + 20, 25, %ELH%, %ELH%, 0, 0, true

		#3a. Walls (P4-P1)
		WallOrientation = left
		DrawWalls = false, true, false, false, false, false
		if[%floor% ! 0] AddWall_CustomTex(EastLobbyWall_Front_%floorid%, 2, %5%Wall, %5%Wall_Upper, Blank, 0.5, %WestEnd%, 0, 175 + 20, 0, 3.5, %height% - 3.5, 0, %interfloorheight%, true)
		WallOrientation = left
		DrawWalls = true, false, false, false, false, false
		if[%floor% ! 0] AddWall_CustomTex(EastLobbyWall_Right_%floorid%, 2, %5%Wall, %5%Wall_Upper, Blank, 0.5, 175 + 20, 0, 175 + 20, 25, 3.5, %height% - 3.5, 0, %interfloorheight%, true)
		WallOrientation = right
		if[%floor% = -4] AddWall_CustomTex(EastLobbyWall_Back_%floorid%, 2, %5%Wall, %5%Wall_Upper, Blank, 0.5, %WestEnd%, 25, 175 + 20, 25, 3.5, %height% - 3.5, 0, %interfloorheight%, true)
		if[%floor% ! -4 & %floor% ! 0] AddWall_CustomTex(EastLobbyWall_Back1_%floorid%, 2, %5%Wall, %5%Wall_Upper, Blank, 0.5, %WestEnd%, 25, 165.5, 25, 3.5, %height% - 3.5, 0, %interfloorheight%, true)
		if[%floor% ! -4 & %floor% ! 0] AddWall_CustomTex(EastLobbyWall_Back2_%floorid%, 2, %5%Wall, %5%Wall_Upper, Blank, 0.5, 185.5, 25, 175 + 20, 25, 3.5, %height% - 3.5, 0, %interfloorheight%, true)

		#3b. Walls (Lobby level)
		WallOrientation = left
		DrawWalls = true, true, false, false, false, false
		TextureOverride ExtFacade, %5%Wall, Blank, Blank, Blank, Blank
		if[%floor% = 0] AddWall EastLobbyWall_Front_%floorid%, ExtFacade, 0.5, %WestEnd%, 0, 175 + 20, 0, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0, true
		TextureOverride ExtFacade, %5%Wall, Blank, Blank, Blank, Blank
		if[%floor% = 0] AddWall EastLobbyWall_Left1_%floorid%, ExtFacade, 0.5, %WestEnd%, 0, %WestEnd%, 25, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0, true
		if[%floor% = 0] AddWall EastLobbyWall_Left2L, EastPK_MainStairsWall, 0.5, %WestEnd%, 20, %WestEnd%, 25, %interfloorheight% / 2, %interfloorheight% / 2, 0, 0, 0, 0, true
		if[%floor% = 0] AddWall EastLobbyWall_Left2U, %5%Wall, 0.5, %WestEnd%, 20, %WestEnd%, 25, %interfloorheight% / 2, %interfloorheight% / 2, %interfloorheight% / 2, %interfloorheight% / 2, 0, 0, true
	
		WallOrientation = right
		TextureOverride %5%Wall, ExtFacade, Blank, Blank, Blank, Blank
		if[%floor% = 0] AddWall EastLobbyWall_Back_%floorid%, ExtFacade, 0.5, %WestEnd%, 25, 175.5 + 20, 25, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0, true

		WallOrientation = left
		DrawWalls = true, false, false, false, false, false
		if[%floor% = 0] AddWall EastLobbyWall_RightInt_%floorid%, %5%Wall, 0.5, 175 + 20, 0, 175 + 20, 25, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0, true

		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		if[%floor% = 0] AddWall EastLobbyWall_RightExt_%floorid%, ExtFacade, 0.5, 175.5 + 20, 20, 175.5 + 20, 25, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0, true
<EndFloors>

<Floor 0>
	TexPrefix() #Set texture prefixes

	#East Elevator Lobby (Continued)
		#1. Carpeting on Floor
		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
		AddFloor EastLobbyCarpet, %5%Carpet, 0.5, 170 - 2, 5 - 2, 190 + 2, 15 + 2, %interfloorheight% + 0.001, %interfloorheight% + 0.001, 0, 0, true

		#2. Ceiling (Exterior)
		Set ELH = %fullheight%

		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
		AddFloor EastLobbyCeiling1_Ext, ExtFacade, 0.5, %WestEnd%, 0, 175 + 20, 20, %ELH%, %ELH%, 0, 0, true
		AddFloor EastLobbyCeiling2_Ext, ExtFacade, 0.5, %WestEnd%, 20, 175.5 + 20, 25, %ELH%, %ELH%, 0, 0, true

		#4. Windows on walls
		Set WinHeight = 9.5 - 0.5
	
		Add3DWindows(EastLobbyWin_Front, 180.25, 0, front, 29, %WinHeight%, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 7/4, 1)
		Add3DWindows(EastLobbyWin_Back, 180.25, 25, back, 29, %WinHeight%, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 7/4, 1)
		Add3DWindows(EastLobbyWin_Left1, 145 + 20, 10 - 6.25, left, 6, %WinHeight%, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 2/4, 1)
		Add3DWindows(EastLobbyWin_Left2, 145 + 20, 10 + 6.25, left, 6, %WinHeight%, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 2/4, 1)
		Add3DWindows(EastLobbyWin_Left3, 145 + 20, 10, left, 6, %WinHeight% - 7, 7.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		#5. Access doors
		Set DTex = BLKEntDoorInt
		Set DTex_Ext = BLKEntDoorExt
		#Set DSTex = %DTex%Side
		Set DSTex = BLKEntDoorSide
	
		Set Door_MainX = 145 + 20
		Set Door_MainZ = 10
		
		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , true, %DTex%, 0.2, 1, 75, %Door_MainX%, %Door_MainZ% - 1.5, 3, 7, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , true, %DTex%, 0.2, 4, 75, %Door_MainX%, %Door_MainZ% + 1.5, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, false, 0, 0, true

		#Exit sign
		AddBoxSign(LobbyLevel_ExitSign1, StainlessSteel, ExitSign, StainlessSteel, 0.1, false, true, true, %Door_MainX% + 0.55, %Door_MainZ%, 1.8, 0.9, %interfloorheight% + 7.2, true)

	#5. Emergency Exit Door
	Set DTex = ServiceDoor2
	Set DTex_Ext = ServiceDoor2B
	#Set DSTex = %DTex%Side
	Set DSTex = ServiceDoor2Side
	
	Set Door_MainX = 175.5 + 20
	Set Door_MainZ = 22
		
		SetLock 2, 0
		WallOrientation = right
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 2, 75, %Door_MainX%, %Door_MainZ%, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 3, 7, %interfloorheight%, false, 0, 0, true
		SetLock 0, 0
		
		#Exit sign
		AddBoxSign(LobbyLevel_ExitSign2, ExitSign, StainlessSteel, StainlessSteel, 0.1, false, true, true, %Door_MainX% - 0.55, %Door_MainZ%, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloor>

##--------------------------------------------------------------------------------##

#STAIRWELLS
<Function StairwellSets>
	#Set Values
	Set NumSteps = 10 #Total: 20 steps
<EndFunction>

#Elevator lobby stairs
CreateStairwell 1, 156 - 0.5 + 20, 21.75 + 0.5, -4, 0
CutStairwell 1, -10, -2.25, 10, 2.25, 2, 5
ShowFullStairs 1 = always

<Floors -4 to 0>
	StairwellSets() #Determine stairs' size

	Set 21 = 156 - 0.5 + 20 #Center X
	Set 22 = 21.75 + 0.5 #Center Z
	Set 23 = 10 #Cut X
	Set 24 = 2.25 #Cut Z
	Set 25 = 1 #Stairwell Number
	Set 26 = 0.5 #Riser Size
	
	#Set texture names
	Set Infix = PL
	if[%floor% = 0] Set Infix = GL

	#Railings
	Set RailHeight = 3.5 + %interfloorheight%
	Set RailHeight2 = %interfloorheight% - 0.5
	Set RailOffset = -%interfloorheight%
	Set RailOffsetU1 = (%26% * 10) - %interfloorheight%
	Set RailOffsetU2 = (%26% * 20) - %interfloorheight%

	#Railings around stairs
	WallOrientation = right
	DrawWalls = true, true, true, true, true, false
	if[%floor% > -4] AddStairsWall %25%, Stair%25%Front, EastPK_%Infix%StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %RailHeight%, %RailHeight%, %RailOffset%, %RailOffset%, 0, 0
	if[%floor% > -4 & %floor% < 0] AddStairsWall %25%, Stair%25%LeftIFloor, EastPK_%Infix%StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %RailHeight2%, %RailHeight2%, %RailOffset%, %RailOffset%, 0, 0
	#if[%floor% = 0] AddStairsWall %25%, Stair%25%Left, EastPK_%Infix%StairsWall, 0.5, -%23%, -%24% - 0.5, -%23%, %24% + 0.4, %RailHeight%, %RailHeight%, %RailOffset%, %RailOffset%, 0, 0
	WallOrientation = left
	if[%floor% > -4] AddStairsWall %25%, Stair%25%RightIFloor, EastPK_%Infix%StairsWall, 0.5, %23%, -%24%, %23%, %24%, %RailHeight2%, %RailHeight2%, %RailOffset%, %RailOffset%, 0, 0
	#if[%floor% > -4] AddStairsWall %25%, Stair%25%Back, EastPK_%Infix%StairsWall, 0.5, -%23%, %24%, %23%, %24%, %RailHeight%, %RailHeight%, %RailOffset%, %RailOffset%, 0, 0

	Set EH = 0 #Extra height
	if[%floor% = -1] Set EH = Floor(0).InterfloorHeight / 2 #Extra height
	#if[%floor% = -1] Set EH = Floor(0).InterfloorHeight #Extra height
	DrawWalls = true, false, false, false, false, false
	if[%floor% > -4 & %floor% < 0] AddStairsWall %25%, Stair%25%Back, EastPK_MainStairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight% + %EH%, %fullheight% + %EH%, -%interfloorheight%, -%interfloorheight%, 0, 0
	if[%floor% = 0] AddStairsWall %25%, Stair%25%Back, EastPK_%Infix%StairsWall2, 0.5, -%23%, %24%, %23%, %24%, %interfloorheight% / 2, %interfloorheight% / 2, -1 * (%interfloorheight% / 2), -1 * (%interfloorheight% / 2), 0, 0

	#Railings next to stairs
	Set RailHeight = 3.5 + 0.5
	Set RailOffset = -0.5
	Set RailOffsetU1 = (%26% * 10) - 0.5
	Set RailOffsetU2 = (%26% * 20) - 0.5

	WallOrientation = left
	DrawWalls = true, true, true, true, true, true
	if[%floor% < 0] AddStairsWall %25%, Stair%25%RailFront, EastPK_PLStairsWall2, 0.25, -%23%, -%24%, %23%, -%24%, %RailHeight%, %RailHeight%, %RailOffset%, %RailOffsetU2%, 0, 0
	WallOrientation = right
	if[%floor% < 0] AddStairsWall %25%, Stair%25%RailBack, EastPK_PLStairsWall2, 0.25, -%23%, %24%, %23%, %24%, %RailHeight%, %RailHeight%, %RailOffset%, %RailOffsetU2%, 0, 0

	#Stairs
	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 0] OP_OrthoStair(%25%, ST%25%_MainStairs, EastPK_PLStairs, -%24% + 0.25, %24% - 0.25, left, -%23%, %NumSteps% * 2, 1, %26%, 0, 0.5)
<EndFloors>

##--------------------------------------------------------------------------------##

#SHAFTWORK
#East Parking Elevators
AddStdShaft(1, 2, 180 + 20, 10, 4.67, 9.67, -5, 0, ShaftIntWall, 0.5, 0.33, 0, 5, %fullheight%)
ShowFullShaft 1 = true

#External shaft walls
<Floor 0>
	#Shaft 1
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, 180 + 20, 10, 5, 10, Shaft1, %height%, %interfloorheight%, true, false, true, true, true, false, true)
<EndFloor>

##--------------------------------------------------------------------------------##

#ELEVATORS

#East Parking Elevators
#Set motors' position + Idle Sound
<Elevators 1 to 2>
	MotorPosition = 0, Floor(0).Altitude + Floor(0).FullHeight - 0.5, 0
	CarIdleSound = EG881Sounds/MTelevidle.wav #EG881
<EndElevators>

<Elevator 1>
	Name = East Parking Elevator 1
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = -4 - 0
	AssignedShaft = 1
	CreateElevator true, 0, 5, 0
<EndElevator>

<Elevator 2>
	Name = East Parking Elevator 2
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = -4 - 0
	AssignedShaft = 1
	CreateElevator true, 0, -5, 0
<EndElevator>

<Function BtnPanelE1-2>
	Set PanelNumber = %param1%
	Set PanelNumber2 = %PanelNumber% + 1

	AddButton %PanelNumber2%, , MainFloorStar, MainFloorStar, 1, 1, off, 0.4, 0.8, -0.76, 0
	AddButton %PanelNumber2%, , Button1, ButtonLit1, 1, 1, 0, 1, 1
	AddButton %PanelNumber%, , ButtonB1, ButtonLitB1, 2, 2, -1, 1, 1
	AddButton %PanelNumber%, , ButtonB2, ButtonLitB2, 2, 1, -2, 1, 1
	AddButton %PanelNumber%, , ButtonB3, ButtonLitB3, 3, 2, -3, 1, 1
	AddButton %PanelNumber%, , ButtonB4, ButtonLitB4, 3, 1, -4, 1, 1

	AddButton %PanelNumber%, , ButtonOpen, ButtonOpen, 5, 1, open, 1, 1
	AddButton %PanelNumber%, , ButtonClose, ButtonClose, 5, 2, close, 1, 1
	if[%PanelNumber% = 1] AddControl %PanelNumber%, , 6, 1, 1, 1, 0, 0, 1, run, stop, ButtonStop, ButtonStop
	if[%PanelNumber% = 1] AddButton %PanelNumber%, , ButtonAlarm, ButtonAlarm, 6, 2, alarm, 1, 1
<EndFunction>

<Elevators 1 to 2>
	Set Prefix = Parking

	#Set textures and dimensions	
	M10_ElevCabTex(ElevExt, %Prefix%ElevFloor, %Prefix%ElevCeiling, %Prefix%ElevWall, %Prefix%ElevWall)
	M10_ElevDimensions(7.68, 6.5, 7.5, 0.2, 0.2, true, false, true, true)
	M10_ElevPanels(%Prefix%ElevPanelMain, %Prefix%ElevPanelTop, 1.5, 1.5, 0.5, 0.2, false, true, false, false)

	#Finish elevator assembly
	M10_ElevInterior()
	#--------------------
	#DETAILED INTERIORS
	#1. Ceiling lights
	Set LightWidth = 3.6
	Set LightLength = 2.4
	Set TrimWidth = 0.2

	CreateWallBox2 elevator, CL_Main, %Prefix%ElevCeilingLight, 0, 0, %LightWidth%, %LightLength%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true

		#2a. Trims around lights
		CreateWallBox2 elevator, CL_Trim_Front, %Prefix%ElevCeilingTrim2, 0, 0 - (%LightLength% / 2) - (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Back, %Prefix%ElevCeilingTrim2, 0, 0 + (%LightLength% / 2) + (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Left, %Prefix%ElevCeilingTrim, 0 - (%LightWidth% / 2) - (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Right, %Prefix%ElevCeilingTrim, 0 + (%LightWidth% / 2) + (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 7.5 - 0.05, 0, 0, false, true, true, true

	#2. Panels on walls
	Set WPTex = %Prefix%ElevWallPanel

		#Front panels
		CreateWallBox2 elevator, Panel_F1, %WPTex%, 0 - (7.28 * 1/4), -3.05 + 0.01, (7.28 * 1/2) - 0.3, 0.02, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_F2, %WPTex%, 0 + (7.28 * 1/4), -3.05 + 0.01, (7.28 * 1/2) - 0.3, 0.02, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true

		#Right panels
		CreateWallBox2 elevator, Panel_R1, %WPTex%, 3.64 - 0.01, 0 - (6.1 * 1/4), 0.02, (6.1 * 1/2) - 0.3, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_R2, %WPTex%, 3.64 - 0.01, 0 + (6.1 * 1/4), 0.02, (6.1 * 1/2) - 0.3, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true

		#Back panels
		CreateWallBox2 elevator, Panel_B1, %WPTex%, 0 - (7.28 * 1/4), 3.05 - 0.01, (7.28 * 1/2) - 0.3, 0.02, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_B2, %WPTex%, 0 + (7.28 * 1/4), 3.05 - 0.01, (7.28 * 1/2) - 0.3, 0.02, 7.5 - 0.5, 0.25, 0, 0, false, true, true, true
	#--------------------
	#Car Doors
	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -3.84

	WallOrientation = right
	AddDoorComponent 1, Left, %ElevDoorTex%, %ElevDoorTex%, 0.2, left, 0.8, 0.6, %IDP%, -1.75, %IDP%, 0, 7, 0, 0, 0, 0, 0
	AddDoorComponent 1, Right, %ElevDoorTex%2, %ElevDoorTex%2, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 1.75, 7, 0, 0, 0, 0, 0
	FinishDoors 1, false, false

		#Door track
		FloorOrientation = top
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevDoorTrackX, ElevExt, ElevExt, ElevExt, ElevExt
		AddFloor DoorTrack, ElevDoorTrackX, 0.05, %IDP% - 0.2, -1.75 - (1.75 * 1), %IDP%, 1.75 + (1.75 * 1), 0, 0, 0, 0
	#--------------------
	#Right Panels
	Set P1_PositionX = -3.64 + 0.001
	Set P1_PositionZ = 2.4

		#Floor buttons
		CreateWallBox2 elevator, RightPB, ElevPanel, %P1_PositionX% - 0.001 + 0.025, %P1_PositionZ%, 0.05, 0.8, 4.75, 2.25, 0, 0, false, true, true, true
		CreatePanel , 6, 2, right, %P1_PositionX% + 0.05, %P1_PositionZ%, 0.26, 0.13, 0.15, 0.2, 3.75, 0, 0
		CreatePanel , 6, 1, right, %P1_PositionX% + 0.05, %P1_PositionZ%, 0.26, 0.13, 0.15, 0.2, 3.75, 0, 0 #1st floor button only
		BtnPanelE1-2(1)
	#--------------------
	#Left Panel
	Set P2_PositionX = -3.64 + 0.001
	Set P2_PositionZ = -2.4

		#Floor buttons
		CreateWallBox2 elevator, LeftPB, ElevPanel, %P2_PositionX% - 0.001 + 0.025, %P2_PositionZ%, 0.05, 0.8, 4.75, 2.25, 0, 0, false, true, true, true
		CreatePanel , 6, 2, right, %P2_PositionX% + 0.05, %P2_PositionZ%, 0.26, 0.13, 0.15, 0.2, 3.75, 0, 0
		CreatePanel , 6, 1, right, %P2_PositionX% + 0.05, %P2_PositionZ%, 0.26, 0.13, 0.15, 0.2, 3.75, 0, 0 #1st floor button only
		BtnPanelE1-2(3)
	#--------------------

	#Floor indicators
	AddFloorIndicator Ind_Button, right, %P1_PositionX% + 0.05, %P1_PositionZ%, 0.5, 0.286, 5.75 + 0.25 - 0.143
	AddFloorIndicator Ind_Button, right, %P2_PositionX% + 0.05, %P2_PositionZ%, 0.5, 0.286, 5.75 + 0.25 - 0.143

	#Elevator number sign
	DrawWalls = false, true, false, false, false, false
	AddWall ElevNo%elevator%_Inside, IN_ElevNo%elevator%, 0, %P1_PositionX% + 0.05, %P1_PositionZ% - 0.25, %P1_PositionX% + 0.05, %P1_PositionZ% + 0.25, 0.5, 0.5, 6.5, 6.5, 0, 0

	#--------------------
	#Shaft components
	DrawWalls = true, true, true, true, true, true
		#Ropes
		RopePosition = 0, 7.7, 0
		RopeTexture = Ropes
	
		#Counterweight
		Set Position = -3.25 - 0.65
		if[%elevator% = 2] Set Position = 3.25 + 0.65

		CreateCounterweight CWeight_Frame, CWeight, 0, %Position%, 2, 6, 0.5, 0.5

		#Rails
		AddRails ElevRail, ElevRailSide, 0, 0, true, 3.25 + 0.06, 0.1
		AddRails ElevRail, ElevRailSide, 0, %Position%, false, 1 + 0.06, 0.1
<EndElevators>

##--------------------------------------------------------------------------------##

#HALL STATIONS
#Set textures for Elevators 1-2
<Function ElevHallTex_E1-2>
	#Shaft doors
	TexPrefix() #Set prefixes for shaft doors
	Set DoorTexture = %5%ShaftDoors

	#Use automatic filler walls or not
	Set AutoFiller = false

	#Door trims
		#Sides
		Set DoorTrimTexture = %DoorTexture%_Trim

		#Top
		Set DoorTrimTexture_Top = %DoorTrimTexture%

	#Deco dimensions
		Set SideDecoH = 7.2
		Set TopDecoH = 0.2

	#Call button panels
	Set BPanelTexture = ElevExtPanels
	Set ButtonPrefix = Button

	#Lantern panels
	Set LPanelTexture = %BPanelTexture%
	Set LanternPrefix = Lantern

		#Lateral offset for lantern panels
		Set ExtraLateralOffset = 0
<EndFunction>

<Floors -4 to 0>
	#Shaft Doors
	ElevHallTex_E1-2() #Set shaft door and panel textures
	Set StartNo = 1 #Number of the first elevator

	Set IDP = -3.84 - 0.3

	WallOrientation = right
	AddShaftDoorComponent %StartNo%, 1, Left, %DoorTexture%, %DoorTexture%, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 1.75, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo%, 1, Right, %DoorTexture%2, %DoorTexture%2, 0.2, left, 0.8, 0.6, %IDP%, -1.75, %IDP%, 0, 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo%, 1, %AutoFiller%, false

	WallOrientation = right
	AddShaftDoorComponent %StartNo% + 1, 1, Left, %DoorTexture%, %DoorTexture%, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 1.75, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo% + 1, 1, Right, %DoorTexture%2, %DoorTexture%2, 0.2, left, 0.8, 0.6, %IDP%, -1.75, %IDP%, 0, 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo% + 1, 1, %AutoFiller%, false

	#Door tracks
	AddShaftTrack(1, 1, -4.34, -4.14, 5 - 3.5, 5 + 3.5, ElevDoorTrackX, ElevExt, 0.05, 1, 7)
	AddShaftTrack(2, 1, -4.34, -4.14, -5 - 3.5, -5 + 3.5, ElevDoorTrackX, ElevExt, 0.05, 1, 7)

	#****************************************#

	#Door trims/fillers
	WallTex() #Set filler textures to match with shaft walls
	Set Shaft1CX = 180 + 20 #1st shaft for elevator bank
	Set Shaft1CZ = 10

		#Levels P4-P1
		if[%floor% < 0] AddDecoObject(Elev1_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, left, true, false, 0.05, 0.2, %Shaft1CX% - 5, %Shaft1CZ% + 5, 3.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, external)
		if[%floor% < 0] AddDecoObject(Elev2_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, left, true, false, 0.05, 0.2, %Shaft1CX% - 5, %Shaft1CZ% - 5, 3.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, external)

		#Lobby level
		WallOrientation = left
		if[%floor% = 0] AddFillerWalls %5%Wall, 0.66, %Shaft1CX% - 5, %Shaft1CZ% + 5, 3.5, 7, %interfloorheight%, false, 0, 0, true
		if[%floor% = 0] AddFillerWalls %5%Wall, 0.66, %Shaft1CX% - 5, %Shaft1CZ% - 5, 3.5, 7, %interfloorheight%, false, 0, 0, true

	#****************************************#

	#Call buttons
	CallButtonElevators = 1, 2
	CreateCallButtons %BPanelTexture%, %ButtonPrefix%Up, %ButtonPrefix%UpLit, %ButtonPrefix%Down, %ButtonPrefix%DownLit, %Shaft1CX% - 5.001, %Shaft1CZ%, 3.6, left, 0.4, 0.8, true, 0, 0

		#Signs
		Set Suffix = %floorid%

		DrawWalls = true, false, false, false, false, false
		AddWall E1-2FloorSign, FlrSign%Suffix%, 0, %Shaft1CX% - 5.001, %Shaft1CZ% - 0.5, %Shaft1CX% - 5.001, %Shaft1CZ% + 0.5, 1, 1, 5.5, 5.5, 0, 0, false
		#AddWall E1-2HallSign, FireSign, 0, %Shaft1CX% - 5.001, %Shaft1CZ% - 0.25, %Shaft1CX% - 5.001, %Shaft1CZ% + 0.25, 0.75, 0.75, 2.6, 2.6, 0, 0, false

	#****************************************#

	#Indicator sets
	Set Offset = 7.5
	if[%floor% = 0] Set Offset = 7.75

		#Set textures
		ACMEInd_LanternTex(%LanternPrefix%Up, %LanternPrefix%UpLit, %LanternPrefix%Down, %LanternPrefix%DownLit, %LPanelTexture%, Ind_Button)

		#Indicator creation
		ACMELobbyInds(left, 1, %Shaft1CX% - 5, %Shaft1CZ% + 5, %Offset%, -4, 0, true)
		ACMELobbyInds(left, 2, %Shaft1CX% - 5, %Shaft1CZ% - 5, %Offset%, -4, 0, true)
<EndFloors>

#Global settings
<Elevators 1 to 2>
	#Door settings
	OpenSound 1 = EG881Sounds/elevopenquick2.wav #EG881
	CloseSound 1 = EG881Sounds/elevclosequick2.wav #EG881
	DoorTimer 1 = 9000
	QuickClose 1 = 6000

	#Car sounds
	CarStartSound = EG881Sounds/slowelevstart.wav #EG881
	CarMoveSound = EG881Sounds/slowelevmove.wav #EG881
	CarStopSound = EG881Sounds/slowelevstop.wav #EG881

	#Motor sounds
	MotorStartSound = EG881Sounds/motor_start3.wav #EG881
	MotorRunSound = EG881Sounds/motor_running3.wav #EG881
	MotorStopSound = EG881Sounds/motor_stop3.wav #EG881

	#Notifications
	NotifyEarly = 1
	UpChimeSound 1 = EG881Sounds/MTupchime2.wav #EG881
	DownChimeSound 1 = EG881Sounds/MTdownchime2.wav #EG881
	BeepSound = EG881Sounds/MTbeep2.wav #EG881

	#Other settings
	RecallFloor = 0
	AlternateRecallFloor = -4
	QueueResets = true
<EndElevators>