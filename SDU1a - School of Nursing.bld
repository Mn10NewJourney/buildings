#Skydream University - School of Nursing
#Copyright (c) 2024-Present - Mn10
#Entire campus copyright (c) 2018-Present - Mn10

#Start date: 29 September 2024
#Major detail finish date: 1 October 2024

#SCRIPTS TO BE INCLUDED
	#1. My new scripts
	<Include buildings/Mn10_Scripts/Deco2023_R1.txt>
	<Include buildings/Mn10_Scripts/Shafts_and_Elevators.txt>
	<Include buildings/Mn10_Scripts/Windows.txt>
	
	#2. ObjectPlus and SROC (credit to EG881)
	<Include ObjectPlus/Scripts/OP_Script2.txt> #EG881
	<Include SROC/Scripts/r1_script.txt> #EG881

	#3. My previous scripts
	<Include CustomObjects2018/Scripts/r1_script.txt>
	<Include Shaft Wall Creator/Scripts/r1_script.txt>
	<Include data/ACME Floor Indicator/Scripts/Indicators.txt>

############################################################

#Globals
<Globals>
	Name = Daylight School of Nursing
	Designer = Mn10
	Location = 999 Daylight Street, Tranquility City (East of Downtown Area)
	Version = 1a
	Description = Along with School of Medicine, this faculty's professors must work hard to produce good specialists for various hospitals in this city.
	CameraFloor = 0
	CameraPosition = -80, 0
	CameraRotation = 0, 0, 0
<EndGlobals>

#Textures
<Textures>
	#Commons
	Load data/black.jpg, Black, 1, 1, false
	Load data/Melvin/transparent128.png, Blank, 1, 1, false
	Load data/EG881/blackdoortrim.jpg, BlackTrim, 1, 1, false #EG881
	Load data/signs_mod/elevhallsign1a.png, FireSign, 1, 1, false #EG881

	#Landscapes
	Load data/downtown.jpg, Downtown, 1, 1
	#Load data/grndroad1mid.jpg, Road, 0.04, 0.04
	Load data/walkway.jpg, Sidewalk, 0.03125, 0.03125

		#Wooden floors
		#Load data/herringbone04.jpg, WoodenFloor, 0.2, 0.2
		Load data/checker10.jpg, WoodenFloor2, 0.25, 0.25

		#Plants
		Load data/EG881/grass2.jpg, Grass_Dark, 0.01, 0.01 #EG881
		Load data/EG881/Trees_Summer/BradfordPear1.png, Tree1, 1, 1, false #EG881
		Load data/EG881/tree1.gif, Tree2, 1, 1, false #EG881

	########################################

	#Windows
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3j.jpg, MainWindows, true, 0.05, 1 #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsInt, 0.05, 1

	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3j.jpg, MainWindowsB, true, 1, 1, false #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsIntB, 1, 1, false

	########################################

	#External facades
	Load data/granite_0007e.jpg, ExtFacade, 0.0625, 0.0625, true
	#Load data/granite_0007c.jpg, ExtFacade2, 0.05, 0.05, true
	Load data/EG881/cem_tr2b.jpg, RoofFloor, 0.075225, 0.0625 #EG881
	#Load data/EG881/Concrete/smoothconcrete1.jpg, RoofFloor, 0.0625, 0.0625 #EG881

	#Lobby
	Load data/floor065.jpg, LobbyFloor, 0.25, 0.25
	Load data/Carpet0006_S.jpg, LobbyCarpet, 0.25, 0.25
	Load data/Carpet0013b_S.jpg, LobbyCarpet2, 0.25, 0.25
	Load data/st_wall2.jpg, LobbyWall, 0.13635, 0.09765
	Load data/EG881/ceilingtile3x3.jpg, LobbyCeiling, 0.125, 0.125 #EG881

		#Shaft doors
		Load data/EG881/stainlesssteeldoorschindler.jpg, LobbyShaftDoors, -1, 1, false #EG881
		Load data/EG881/stainlesssteeldoor.jpg, LobbyShaftDoors2, -1, 1, false #EG881
		Load data/EG881/stainlesssteel.jpg, LobbyShaftDoors_Trim, 1, 1, false #EG881
		Load data/EG881/stainlesssteel2.jpg, LobbyShaftDoors_TrimTop, 1, 1, false #EG881

	#Upper levels
	Load data/Carpet0013b_S.jpg, ClassroomFloor, 0.25, 0.25 #Hallway floors
	Load data/EG881/Carpet/eg881_Carpet4.jpg, ClassroomFloor2, 0.2, 0.2 #EG881 #Flooring inside classrooms
	Load data/EG881/Carpet/eg881_Carpet2.jpg, ClassroomFloor3, 0.2, 0.2 #EG881 #Flooring inside classrooms
	Load data/EG881/granite_0007e_black.jpg, ClassroomWallLower, 0.075, 0.075 #EG881 #Hallway walls
	Load data/stucco5.jpg, ClassroomWall, 0.25, 0.25, true
	Load data/EG881/stucco22b_white.jpg, ClassroomWall2, 0.1, 0.1 #EG881 #Classroom walls
	Load data/EG881/ceilingtile3x3.jpg, ClassroomCeiling, 0.125, 0.125 #EG881
	Load data/EG881/Ceilings/ceilingtile4single.jpg, DropCeiling, 0.5, 0.5 #EG881

		#Shaft doors
		Load data/EG881/elevdoorright_black.jpg, ClassroomShaftDoors, -1, 1, false #EG881
		Load data/EG881/elevdoorleft_black.jpg, ClassroomShaftDoors2, -1, 1, false #EG881
		Load data/EG881/elevdoortrim_black.jpg, ClassroomShaftDoors_Trim, 1, 1, false #EG881

	#Restrooms
	Load data/granite_0007e.jpg, RestroomFloor, 0.2, 0.2
	Load data/marb148.jpg, RestroomWall, 0.15, 0.15

	#Drop ceilings
	Load data/EG881/Ceilings/ceilingtile4single.jpg, DropCeiling, 0.5, 0.5 #EG881 #Restrooms

	########################################

	#Doors
		#Main doors
		Load data/miscdoor6.jpg, Door6, 1, 1, false
		LoadCropped data/miscdoor6.jpg, Door6Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor8.jpg, Door8, 1, 1, false
		LoadCropped data/miscdoor8.jpg, Door8Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor88.gif, Door88, 1, 1, false
		LoadCropped data/miscdoor88.gif, Door88Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor9b.jpg, Door9b, 1, 1, false
		Load data/EG881/Doors/miscdoor9bside.jpg, Door9bSide, 1, 1, false #EG881

		Load data/EG881/Doors/miscdoor9b_white.jpg, Door9bWhite, 1, 1, false #EG881
		LoadCropped data/EG881/Doors/miscdoor9b_white.jpg, Door9bWhiteSide, 236, 0, 20, 512, 1, 1, false #EG881

		#Service doors
		#Load data/servicedoor1.jpg, ServiceDoor1, -1, 1, false
		#LoadCropped data/servicedoor1.jpg, ServiceDoor1Side, 0, 0, 10, 256, 1, 1, false
		Load data/EG881/Doors/servicedoor2_graynovents.jpg, ServiceDoor2, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2back_graynovents.jpg, ServiceDoor2B, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2_graynovents_side.jpg, ServiceDoor2Side, 1, 1, false #EG881
		Load data/servicedoor3.jpg, ServiceDoor3, 1, 1, false
		LoadCropped data/servicedoor3.jpg, ServiceDoor3Side, 236, 0, 20, 512, 1, 1, false

		#Black entrance doors
		Load data/EG881/miscdoor8_blacktrans.png, BLKEntDoorInt, 1, 1, false #EG881
		LoadAlphaBlend data/EG881/miscdoor8_blacktranstinted.jpg, data/EG881/miscdoor8_blacktranspec.png, data/sky3j.jpg, BLKEntDoorExt, true, 1, 1, false #EG881
		LoadCropped data/EG881/miscdoor8_blacktrans.png, BLKEntDoorSide, 236, 0, 20, 512, 1, 1, false #EG881

	########################################

	#Stairwells
	Load data/EG881/cem_tr2b.jpg, Stairs, 0.075225, 0.0625 #EG881
	Load data/EG881/cem_tr2b.jpg, StairsFloor, 0.075225, 0.0625 #EG881
	Load data/stucco1.jpg, StairsWall, 0.25, 0.25
	Load data/stucco3.jpg, StairsWall2, 0.25, 0.25
	Load data/EG881/cem_tr2b.jpg, StairsCeiling, 0.075225, 0.0625 #EG881

	#Escalators
	Load data/panel-back3.jpg, SilverBalustrade, 1, 1, false
	#Load data/EG881_Otis/Walls_and_Panels/otisstainlesssteel1.jpg, SilverBalustrade2, 1, 1, false #EG881
	Load data/MT/blank.gif, GlassBalustrade, 1, 1, false

	########################################

	#Shafts
	Load data/EG881/cem_tr3.jpg, ShaftIntWall, 0.075225, 0.0625 #EG881

	#Elevators
	#Main components
	Load data/metal1.jpg, ElevExt, 0.25, 0.25
	Load data/panel-back3.jpg, ElevExt2, 1, 1, false
	Load data/counterweight.png, CWeight, 1, 1, false
	Load data/counterweight_frame.png, CWeight_Frame, 1, 1, false
	Load data/ropes.png, Ropes, 1, 1, false
	Load data/metal080.jpg, ElevRail, 0.5, 0.5
	Load data/metal1.jpg, ElevRailSide, 0.25, 0.25
	#Load data/EG881/elevatordoortrackx.jpg, ElevDoorTrackX, 1, 1, false #EG881
	Load data/EG881/elevatordoortrackz.jpg, ElevDoorTrackZ, 1, 1, false #EG881
	Load data/panel-back3.jpg, ElevPanel, 1, 1, false
	Load data/panel-back3.jpg, ElevExtPanels, 1, 1, false

		#West elevators
		Load data/Carpet0013b_S.jpg, WestElevFloor, 0.2, 0.2
		Load data/panel-back3.jpg, WestElevCeiling, 1, -1, false
		Load data/EG881/Lights/squarelight1_white.jpg, WestElevCeilingLight, 1, 1, false #EG881
		Load data/panel-back3.jpg, WestElevCeilingTrim, 1, 1, false #EG881
		Load data/panel-back3.jpg, WestElevCeilingTrim2, 1, 1, false #EG881
		Rotate WestElevCeilingTrim2, 90
		Load data/EG881/blackdoortrim.jpg, WestElevWall, 1, 1, false #EG881
		Load data/checker24.jpg, WestElevWallPanel, 0.5, 0.5
		Load data/panel-back3.jpg, WestElevPanelMain, 1, 1, false
		Load data/panel-back3.jpg, WestElevPanelTop, 1, 0.0667, false
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, WestElevDoors, -1, 1, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, WestElevDoors2, 1, 1, false #EG881

		#East elevators
		Load data/Carpet0021b_S.jpg, EastElevFloor, 0.2, 0.2
		Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, EastElevCeiling, 1, 1, false
		Load data/EG881/light1z.jpg, EastElevCeilingLight, 1, 2, false #EG881
		Load data/otis67/OTIS67_EU_Button_Lighted.gif, EastElevCeilingDL, 1, 1, false #DL = Downlight
		Load data/panel-back3.jpg, EastElevCeilingTrim, 1, 1, false
		Load data/EG881/elevdoortrim_white.jpg, EastElevWall, 0.5, 0.5 #EG881
		Load data/graniteTexture_0001b.jpg, EastElevWallPanel, 0.3, 0.3
		Load data/panel-back3.jpg, EastElevPanelMain, 1, 1, false
		Load data/panel-back3.jpg, EastElevPanelTop, 1, 0.0667, false
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, EastElevDoors, -1, 1, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, EastElevDoors2, 1, 1, false #EG881

	####################

	#Schindler MT fixtures
		#Call buttons
		Load data/MT/upbutton.gif, MTButtonUp, 1, 1, false
		Load data/MT/upbutton_lit.gif, MTButtonUpLit, 1, 1, false
		Load data/MT/downbutton.gif, MTButtonDown, 1, 1, false
		Load data/MT/downbutton_lit.gif, MTButtonDownLit, 1, 1, false

		#Car buttons
		Load data/MT/MT.gif, MTButton, 1, 1, false
		Load data/MT/MTlit.gif, MTButtonLit, 1, 1, false
		Load data/MT/Open.gif, MTButtonOpen, 1, 1, false
		Load data/MT/Close.gif, MTButtonClose, 1, 1, false
		Load data/MT/Stop.gif, MTButtonStop, 1, 1, false
		Load data/MT/Alarm.gif, MTButtonAlarm, 1, 1, false

		AddTextRange 1, 6, MTButton, MTButton%number%Temp, nimbus_sans_bold_condensed.ttf, 63, %number%, 16, 55, 135, 148, center, center, 255, 255, 255
		AddTextRange 1, 6, MTButtonLit, MTButtonLit%number%Temp, nimbus_sans_bold_condensed.ttf, 63, %number%, 16, 55, 135, 148, center, center, 255, 255, 255

		AddTextRange 1, 6, MTButton%number%Temp, MTButton%number%, braille.ttf, 25, $%number%, 16, 148, 135, 194, center, center, 255, 255, 255
		AddTextRange 1, 6, MTButtonLit%number%Temp, MTButtonLit%number%, braille.ttf, 25, $%number%, 16, 148, 135, 194, center, center, 255, 255, 255

		#Indicators (Lanterns made by epco_rules, modified by ElevatorFace.)
		Load data/Melvin/MT Lanterns/LanternUnlit.gif, LanternUp, 1, 1, false
		Load data/Melvin/MT Lanterns/LanternBlue.gif, LanternUpLit, 1, 1, false
		Load data/Melvin/MT Lanterns/LanternUnlit.gif, LanternDown, 1, -1, false
		Load data/Melvin/MT Lanterns/LanternRed.gif, LanternDownLit, 1, -1, false

		Load data/black.jpg, InsideIndBack, 1, 1, false
		Load data/ACME Floor Indicator/Resized/ACME Floor Indicator.jpg, OutsideIndBack, 1, 1, false

		AddTextRange 1, 6, InsideIndBack, Ind_Button%number%, acme_led.ttf, 75, _%number%, -1, -1, -1, -1, center, center, 255, 0, 0
		AddTextRange 1, 6, OutsideIndBack, OutsideInd_Button%number%, acme_led.ttf, 240, _%number%, -1, -1, -1, -1, center, center, 255, 0, 0

	########################################

	#Signs
		#1. Floor signs (Blank)
		AddTextRange 1, 6, Blank, BlankSign%number%, nimbus_sans_bold.ttf, 72, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddText Blank, BlankSignR, nimbus_sans_bold.ttf, 72, R, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#2. Restrooms
		Load data/text16.jpg, WoodSign2, 1, 1, false
		Load data/signs_mod/Restroom1A.png, Sign_Restroom_Brown, 1, 1, false
		#Load data/signs_mod/Restroom1.png, Sign_Restroom_Gray, 1, 1, false
		Load data/signs_mod/Sign_Restroom_1A.png, Sign_RestroomW_Brown, 1, 1, false
		Load data/signs_mod/Sign_Restroom_3A.png, Sign_RestroomM_Brown, 1, 1, false
		#Load data/signs_mod/Sign_Restroom_1.png, Sign_RestroomW_Gray, 1, 1, false
		#Load data/signs_mod/Sign_Restroom_3.png, Sign_RestroomM_Gray, 1, 1, false

		#3. Exit signs
		Load data/panel-back3.jpg, StainlessSteel, 1, 1, false
		Load data/Melvin/ExitSignDown.png, ExitSign, 1, 1, false
		Load data/Melvin/ExitSignUp.png, ExitSignUp, 1, 1, false
		Load data/Melvin/ExitSignLeft.png, ExitSignLeft, 1, 1, false
		Load data/Melvin/ExitSignRight.png, ExitSignRight, 1, 1, false
		#Load data/Melvin/ExitSignBoth.png, ExitSignBoth, 1, 1, false

		#4. Elevator number signs (Inside)
		Load data/Melvin/transparent128.png, IN_ElevNoBack, 1, 1, false
		AddTextRange 1, 4, IN_ElevNoBack, IN_ElevNo%number%, nimbus_sans_bold.ttf, 55, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#5. Classroom signs
		Load data/Melvin/Sign-LightBlue2.png, Sign_LightBlue, 1, 1, false
		AddTextRange 201, 210, Sign_LightBlue, SignRoom%number%, nimbus_sans_bold_condensed.ttf, 92, NS %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddTextRange 301, 310, Sign_LightBlue, SignRoom%number%, nimbus_sans_bold_condensed.ttf, 92, NS %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddTextRange 401, 410, Sign_LightBlue, SignRoom%number%, nimbus_sans_bold_condensed.ttf, 92, NS %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddTextRange 501, 510, Sign_LightBlue, SignRoom%number%, nimbus_sans_bold_condensed.ttf, 92, NS %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddTextRange 601, 610, Sign_LightBlue, SignRoom%number%, nimbus_sans_bold_condensed.ttf, 92, NS %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#6. Building sign
		Load data/Melvin/SDU_Signs/SchoolOfNursing.png, BldgSign, 1, 1, false
		Load data/marb148.jpg, BldgSignBack, 0.25, 0.25
<EndTextures>

############################################################

#Floor Assignments
<Floor -1>
	ID = -1
	Name = Shaft Pit
	Type = Basement
	Height = 8.5
	InterfloorHeight = 1.5
	Altitude = -11.5
<EndFloor>

<Floor 0>
	ID = 1
	Name = Lobby
	Type = Lobby
	Height = 9
	InterfloorHeight = 1.5
	Altitude = -1.5
<EndFloor>

<Floors 1 to 5>
	ID = %floor% + 1
	Name = Classroom Level %floorid%
	Type = Office
	Height = 9
	InterfloorHeight = 2
<EndFloors>

<Floor 6>
	ID = R
	Name = Roof
	Type = Roof
	Height = 9.5
	InterfloorHeight = 2
<EndFloor>

############################################################

#Landscape
#Ground
AddGround Ground, Downtown, -158400, -158400, 158400, 158400, 0, 7920, 7920
Cut Landscape, -100 - 68 - 12, -1, -40 - 10, 100 + 12, 1, 40 + 5 + 50 + 15, false, true

#Sidewalks
FloorOrientation = top
DrawWalls = true, true, true, true, true, true
AddFloor landscape, SidewalkFront, Sidewalk, 0.5, -100 - 80, -40 - 10, 100, -40, 0, 0, 0, 0
AddFloor landscape, SidewalkLeft, Sidewalk, 0.5, -100 - 8, -40, -100, 40 + 5, 0, 0, 0, 0
AddFloor landscape, SidewalkRight, Sidewalk, 0, 100, -40 - 10, 100 + 12, 40 + 5 + 50 + 15, 0, 0, 0, 0
AddFloor landscape, SidewalkBack, Sidewalk, 0.5, -100, 40, 100, 40 + 5, 0, 0, 0, 0

#Grass
DrawWalls = true, true, false, false, false, false
AddFloor landscape, WestGrass1A, Grass_Dark, 0, -100 - 68 - 12, -40, -150, 87.5, 0, 0, 0, 0
AddFloor landscape, WestGrass1B, Grass_Dark, 0, -130, -40, -100 - 8, 62.5, 0, 0, 0, 0
AddFloor landscape, WestGrass2, Grass_Dark, 0, -100 - 68 - 12, 87.5, -100 - 8, 45 + 50 + 15, 0, 0, 0, 0

AddFloor landscape, NorthGrass1, Grass_Dark, 0, -100 - 8, 45, 100, 62.5, 0, 0, 0, 0
AddFloor landscape, NorthGrass2, Grass_Dark, 0, -100 - 8, 87.5, 100, 45 + 50 + 15, 0, 0, 0, 0

#Wooden floors
AddFloor landscape, WoodenFloor_North, WoodenFloor2, 0, -130, 62.5, 100, 87.5, 0, 0, 0, 0
AddFloor landscape, WoodenFloor_West, WoodenFloor2, 0, -150, -40, -130, 87.5, 0, 0, 0, 0

#North Trees
AddTree(Tree1, 90, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 15, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 30, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 45, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 60, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 75, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 90, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 105, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 120, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 135, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 150, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 165, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 180, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 195, 55, 13.5, 12, 0)
AddTree(Tree1, 90 - 210, 55, 13.5, 12, 0)

AddTree(Tree2, 90, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 15, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 30, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 45, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 60, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 75, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 90, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 105, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 120, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 135, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 150, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 165, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 180, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 195, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 210, 55 + 42.5, 12, 16, 0)
AddTree(Tree2, 90 - 225, 55 + 42.5, 12, 16, 0)

#West Trees
AddTree(Tree1, -120, -30, 13.5, 12, 0)
AddTree(Tree1, -120, -30 + 15, 13.5, 12, 0)
AddTree(Tree1, -120, -30 + 30, 13.5, 12, 0)
AddTree(Tree1, -120, -30 + 45, 13.5, 12, 0)
AddTree(Tree1, -120, -30 + 60, 13.5, 12, 0)

AddTree(Tree2, -120 - 40, -30, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 15, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 30, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 45, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 60, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 75, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 90, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 105, 12, 16, 0)
AddTree(Tree2, -120 - 40, -30 + 120, 12, 16, 0)

##--------------------------------------------------------------------------------##

#Set texture prefixes/suffixes
<Function TexPrefix>
	#Main prefixes
	Set 5 = Lobby
	if[%floor% > 0] Set 5 = Classroom
<EndFunction>

##--------------------------------------------------------------------------------##

#1st-6th floors
<Floors 0 to 5>
	Set 1 = 100 #Building Dimension X
	Set 2 = 40 #Building Dimension Z
	TexPrefix() #Set texture prefix/suffix

	#Floors
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Floor, %5%Floor, 0.5, -%1%, -%2%, %1%, %2%, 0, 0, 0, 0, false

	#Ceilings
	FloorOrientation = bottom
	DrawWalls = true, false, false, false, false, false
	AddFloor Ceiling, %5%Ceiling, 0.5, -%1%, -%2%, %1%, %2%, %height%, %height%, 0, 0, false

	#Windows
	Set MyIFloorType = 1
	#if[%floor% = 1 | %floor% = 3 | %floor% = 5] Set MyIFloorType = 2
	if[%floor% = 0] Set MyIFloorType = 3
	Set WinPrefix = Main

	SetAutoSize = true, false
	if[%floor% > 0] AddCustomWindows(FrontWin, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, -%2%, %1%, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(FrontWin1, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, -%2%, -20, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(FrontWin2, %WinPrefix%Windows, %WinPrefix%WindowsInt, 20, -%2%, %1%, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)

	if[%floor% > 0] AddCustomWindows(LeftWin, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, 0, -%1%, 10, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% > 0] AddCustomWindows(RightWin, %WinPrefix%WindowsInt, %WinPrefix%Windows, %1%, 0, %1%, 10, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)

	AddCustomWindows(BackWin1, %WinPrefix%WindowsInt, %WinPrefix%Windows, -%1% + 30, %2%, -20, %2%, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)
	AddCustomWindows(BackWin2, %WinPrefix%WindowsInt, %WinPrefix%Windows, 10, %2%, %1% - 30, %2%, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)
	SetAutoSize = true, true
	
	#Perimeter walls
	TexPrefix() #Set texture prefix/suffix
	Set WallHeight = %fullheight%
	Set WallOffset = 0
	if[%floor% = 0] Set WallHeight = %height%
	if[%floor% = 0] Set WallOffset = %interfloorheight%
	
	Set IntTex = %5%Wall
	if[%floor% > 0] Set IntTex = %5%Wall2

	Set ShowSidePos = true
	if[%floor% > 0] Set ShowSidePos = false

		#Front wall
		WallOrientation = left
		DrawWalls = true, true, true, true, false, false
		TextureOverride ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		if[%floor% = 0] AddWall FrontWall, ExtFacade, 0.5, -20, -%2%, 20, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		#Left walls
		Set Extra = 0
		if[%floor% = 0] Set Extra = 10

		WallOrientation = left
		DrawWalls = true, false, false, false, false, false
		AddWall LeftWall_A, ExtFacade, 0.5, -%1%, %2% - 25, -%1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, true, false, %ShowSidePos%, false, false
		TextureOverride ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		AddWall LeftWall_B, ExtFacade, 0.5, -%1%, -%2%, -%1%, 0 + %Extra%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, true, true, false, false, false
		if[%floor% > 0] DrawWalls = true, false, false, false, false, false
		TextureOverride ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		AddWall LeftWall_C, ExtFacade, 0.5, -%1%, 10, -%1%, 15, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

			#Interior walls on upper floors
			DrawWalls = false, true, true, false, false, false
			if[%floor% > 0] AddWall_CustomTex(LeftWall_CInt, 2, %5%WallLower, %5%Wall, Blank, 0.5, -%1%, 10, -%1%, 15, 3.75, %height% - 3.75, 0, 0, false)

		#Right walls
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall RightWall_A, ExtFacade, 0.5, %1%, %2% - 25, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, true, false, %ShowSidePos%, false, false
		TextureOverride %IntTex%, ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		AddWall RightWall_B, ExtFacade, 0.5, %1%, -%2%, %1%, 0 + %Extra%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, true, true, false, false, false
		if[%floor% > 0] DrawWalls = false, true, false, false, false, false
		TextureOverride %IntTex%, ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		AddWall RightWall_C, ExtFacade, 0.5, %1%, 10, %1%, 15, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

			#Interior walls on upper floors
			DrawWalls = true, false, true, false, false, false
			if[%floor% > 0] AddWall_CustomTex(RightWall_CInt, 2, %5%WallLower, %5%Wall, Blank, 0.5, %1%, 10, %1%, 15, 3.75, %height% - 3.75, 0, 0, false)

		#Back walls
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall BackWall_A, ExtFacade, 0.5, -%1%, %2%, -%1% + 30, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, true, false, false, false, false
		TextureOverride RestroomWall, ExtFacade, %IntTex%, %IntTex%, %IntTex%, %IntTex%
		AddWall BackWall_B, ExtFacade, 0.5, -20, %2%, 10, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = false, true, false, false, false, false
		AddWall BackWall_C, ExtFacade, 0.5, %1% - 30, %2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

	#3D Windows for restrooms
	Set RRCeiHeight = 8.5
	if[%floor% > 0] Add3DWindows(Restroom1_Windows, -5 - 7.5 + 6 - 4, %2%, back, 8, 2, %RRCeiHeight% - 2.5, MainWindowsIntB, MainWindowsB, RestroomWall, 0.5, 2/4, 1)
	if[%floor% > 0] Add3DWindows(Restroom2_Windows, -5 + 7.5 - 6 + 4, %2%, back, 8, 2, %RRCeiHeight% - 2.5, MainWindowsIntB, MainWindowsB, RestroomWall, 0.5, 2/4, 1)
<EndFloors>

<Floor 0>
	TexPrefix() #Set texture prefixes

	#Building sign
	WallOrientation = right
	DrawWalls = true, true, true, true, true, false
	TextureOverride BldgSign, BldgSignBack, BldgSignBack, BldgSignBack, BldgSignBack, BldgSignBack
	AddWall BldgSign, BldgSign, 0.25, -30 - 7.875, -%2% - 5, -30 + 7.875, -%2% - 5, 4.5, 4.5, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Elevator lobby floors
		#Floor cuts
		CutAll -%1%, %interfloorheight% - 0.1, -%2%, -60, %interfloorheight% + 0.1, %2%, false, true #West lobby
		CutAll 60, %interfloorheight% - 0.1, -%2%, %1%, %interfloorheight% + 0.1, %2%, false, true #East lobby

		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
	
		#West lobby
		AddFloor WestLobby_TR1, BlackTrim, 0.5, -%1%, 0, -60.2, 0.2, 0, 0, 0, 0, false
		AddFloor WestLobby_TR2, BlackTrim, 0.5, -60.2, -%2%, -60, %2%, 0, 0, 0, 0, false
		AddFloor WestLobby1, %5%Carpet, 0.5, -%1%, 0.2, -60.2, %2%, 0, 0, 0, 0, false
		AddFloor WestLobby2, %5%Carpet2, 0.5, -%1%, -%2%, -60.2, 0, 0, 0, 0, 0, false

		#East lobby
		AddFloor EastLobby_TR1, BlackTrim, 0.5, 60.2, 0, %1%, 0.2, 0, 0, 0, 0, false
		AddFloor WestLobby_TR2, BlackTrim, 0.5, 60, -%2%, 60.2, %2%, 0, 0, 0, 0, false
		AddFloor EastLobby, %5%Carpet, 0.5, 60.2, 0.2, %1%, %2%, 0, 0, 0, 0, false
		AddFloor EastLobby2, %5%Carpet2, 0.5, 60.2, -%2%, %1%, 0, 0, 0, 0, 0, false

	#Entrance doors
	Set EntHeight = %height%
	Set DTex = BLKEntDoorInt
	Set DTex_Ext = BLKEntDoorExt
	#Set DSTex = %DTex%Side
	Set DSTex = BLKEntDoorSide

		#1. East doors
		Set Door_MainX = %1%
		Set Door_MainZ = 5.5

		WallOrientation = right
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 2, 75, %Door_MainX%, %Door_MainZ% - 1.5, 3, 7, %interfloorheight%, 0, 0
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 3, 75, %Door_MainX%, %Door_MainZ% + 1.5, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, false, 0, 0, true

		Add3DWindows(E1_WinTop, %Door_MainX%, %Door_MainZ%, right, 6, %EntHeight% - 7.5, 7.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(E1_WinSide1, %Door_MainX%, %Door_MainZ% - 3 - 0.875, right, 1.25, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(E1_WinSide2, %Door_MainX%, %Door_MainZ% + 3 + 0.875, right, 1.25, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)

		#2. West doors
		Set Door_MainX = -%1%
		Set Door_MainZ = 5.5

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 1, 75, %Door_MainX%, %Door_MainZ% - 1.5, 3, 7, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 4, 75, %Door_MainX%, %Door_MainZ% + 1.5, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, false, 0, 0, true

		Add3DWindows(E1_WinTop, %Door_MainX%, %Door_MainZ%, left, 6, %EntHeight% - 7.5, 7.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(E1_WinSide1, %Door_MainX%, %Door_MainZ% - 3 - 0.875, left, 1.25, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(E1_WinSide2, %Door_MainX%, %Door_MainZ% + 3 + 0.875, left, 1.25, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		#3. South doors
		#Access point S1
		Set Door_MainX = -11
		Set Door_MainZ = -%2%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, true, 0, 0, true

		Add3DWindows(S1A_WinTop, %Door_MainX%, %Door_MainZ%, front, 6, %EntHeight% - 7.5, 7.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(S1A_WinSide1, %Door_MainX% - 3 - 2.5, %Door_MainZ%, front, 4.5, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(S1A_WinSide2, %Door_MainX% + 3 + 2.5, %Door_MainZ%, front, 4.5, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		#Access point S2
		Set Door_MainX = 11
		Set Door_MainZ = -%2%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, true, 0, 0, true

		Add3DWindows(S1A_WinTop, %Door_MainX%, %Door_MainZ%, front, 6, %EntHeight% - 7.5, 7.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(S1A_WinSide1, %Door_MainX% - 3 - 2.5, %Door_MainZ%, front, 4.5, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(S1A_WinSide2, %Door_MainX% + 3 + 2.5, %Door_MainZ%, front, 4.5, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		Set Door_MainX = 0
		Add3DWindows(S1-S2_WinMiddle, %Door_MainX%, %Door_MainZ%, front, 16 - 0.5, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 3/4, 1)
<EndFloor>

#Roof
<Floor 6>
	Set 1 = 100 #Building Dimension X
	Set 2 = 40 #Building Dimension Z

	#Roof floor
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Roof, RoofFloor, 0.5, -%1%, -%2%, %1%, %2%, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Roof railings
	Set WallHeight = %interfloorheight% + 4.5
	Set WallOffset = 0

	WallOrientation = Left
	DrawWalls = true, true, true, true, true, false
	AddWall FrontRail, ExtFacade, 0.5, -%1% + 0.5, -%2%, %1% - 0.5, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall LeftRail, ExtFacade, 0.5, -%1%, -%2%, -%1%, %2% - 25, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	WallOrientation = Right
	AddWall RightRail, ExtFacade, 0.5, %1%, -%2%, %1%, %2% - 25, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall BackRail, ExtFacade, 0.5, -%1% + 30, %2%, %1% - 30, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloor>

################################################################################

#CLASSROOM DOORS FUNCTION
<Function RoomDoors_NS_South> #South rooms
	#Classroom doors (South hallway)
	Set RoomNo = %param1%
	Set X1 = %param2%
	Set X2 = %param3%
	
	Set DTex = Door88
	Set DSTex = %DTex%Side

		#Front doors
		Set Door_MainX = %X1%
		Set Door_MainZ = -0.5

		WallOrientation = left
		TextureOverride %DTex%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, 0, 0, 0
		TextureOverride %DTex%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, 0, 0, 0
		AddFillerWalls %5%Wall2, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, 0, true, 0, 0, false

		Add3DWindows(Room%RoomNo%_WinTop, %Door_MainX%, %Door_MainZ% + 0.5, back, 6, %height% - 7.5, 7.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		Add3DWindows(Room%RoomNo%_WinSide1, %Door_MainX% - 3 - 1.5, %Door_MainZ% + 0.5, back, 2.5, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		Add3DWindows(Room%RoomNo%_WinSide2, %Door_MainX% + 3 + 1.5, %Door_MainZ% + 0.5, back, 2.5, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)

		AddBoxSign(Room%RoomNo%_Sign, Sign_LightBlue, SignRoom%RoomNo%, Sign_LightBlue, 0.1, true, true, true, %X1%, 0 + 0.05, 1, 0.5, 7, false)

		#Rear doors
		Set Door_MainX = %X2%
		Set Door_MainZ = -0.5

		WallOrientation = left
		TextureOverride %DTex%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, 0, 0, 0
		TextureOverride %DTex%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, 0, 0, 0
		AddFillerWalls %5%Wall2, 0.5, %Door_MainX%, %Door_MainZ%, 6, 7, 0, true, 0, 0, false

		Add3DWindows(Room%RoomNo%_WinTop, %Door_MainX%, %Door_MainZ% + 0.5, back, 6, %height% - 7.5, 7.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		Add3DWindows(Room%RoomNo%_WinSide1, %Door_MainX% - 3 - 1.5, %Door_MainZ% + 0.5, back, 2.5, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		Add3DWindows(Room%RoomNo%_WinSide2, %Door_MainX% + 3 + 1.5, %Door_MainZ% + 0.5, back, 2.5, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
<EndFunction>

<Function RoomDoors_NS_North> #North rooms
	#Classroom doors (North hallway)
	Set RoomNo = %param1%
	Set X1 = %param2%
	Set Open = %param3%
	Set CreateSign = %param4%
	Set DoorTexture = %param5%
	Set AddWin = %param6%
	Set DoorFillerTex = %param7%

	Set SignOffset = 7
	if[%AddWin% = false | %AddWin% = False] Set SignOffset = 7.2

	Set DTex = %DoorTexture% #Receive door texture from 6th parameter
	Set DSTex = %DTex%Side

		#Room door
		Set Door_MainX = %X1%
		Set Door_MainZ = 10.5

		WallOrientation = right
		TextureOverride %DTex%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddDoor , , false, %DTex%, 0.2, %Open%, 75, %Door_MainX%, %Door_MainZ%, 3, 7, 0, 0, 0
		AddFillerWalls %DoorFillerTex%, 0.5, %Door_MainX%, %Door_MainZ%, 3, 7, 0, true, 0, 0, false

		if[%AddWin% = true | %AddWin% = True] Add3DWindows(Room%RoomNo%_WinTop, %Door_MainX%, %Door_MainZ% - 0.5, front, 3, %height% - 7.5, 7.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		if[%AddWin% = true | %AddWin% = True] Add3DWindows(Room%RoomNo%_WinSide1, %Door_MainX% - 1.5 - 1.25, %Door_MainZ% - 0.5, front, 2, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)
		if[%AddWin% = true | %AddWin% = True] Add3DWindows(Room%RoomNo%_WinSide2, %Door_MainX% + 1.5 + 1.25, %Door_MainZ% - 0.5, front, 2, %height% - 0.5, 0.25, MainWindowsIntB, MainWindowsIntB, %5%Wall2, 0.5, 1/4, 1)

		if[%CreateSign% = true | %CreateSign% = True] AddBoxSign(Room%RoomNo%_Sign, SignRoom%RoomNo%, Sign_LightBlue, Sign_LightBlue, 0.1, true, true, true, %X1%, 10 - 0.05, 1, 0.5, %SignOffset%, false)
<EndFunction>

<Floors 1 to 5>
	TexPrefix() #Set texture prefixes
	
	CutAll -70, %interfloorheight% - 0.1, 10 + 0.3, 70, %interfloorheight% + 0.1, %2%, false, true #North classrooms
	CutAll -%1%, %interfloorheight% - 0.1, -%2%, %1%, %interfloorheight% + 0.1, 0 - 0.3, false, true #South classrooms

	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false

	#South classrooms
	AddFloor SouthFloor_TR, BlackTrim, 0.5, -%1%, 0 - 0.5, %1%, 0 - 0.3, 0, 0, 0, 0, false
	AddFloor SouthFloor, %5%Floor2, 0.5, -%1%, -%2%, %1%, 0 - 0.5, 0, 0, 0, 0, false

	#North classrooms
	AddFloor NorthFloor_TR, BlackTrim, 0.5, -70, 10 + 0.3, 70, 10 + 0.5, 0, 0, 0, 0, false
	AddFloor NorthFloor, %5%Floor3, 0.5, -70, 10 + 0.5, 70, %2%, 0, 0, 0, 0, false

	#========================================#

	#Classroom hallway walls
		#North
		WallOrientation = left
		DrawWalls = true, false, false, false, false, false
		AddWall_CustomTex(NorthHallA_Ext, 2, %5%WallLower, %5%Wall, Blank, 0.5, -70.5, 10, 70.5, 10, 3.75, %height% - 3.75, 0, 0, false)
		AddWall_CustomTex(NorthHallB_Ext, 2, %5%WallLower, %5%Wall, Blank, 0.5, -70.5, 10, -70.5, 28, 3.75, %height% - 3.75, 0, 0, false)

		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall_CustomTex(NorthHallC_Ext, 2, %5%WallLower, %5%Wall, Blank, 0.5, 70.5, 10, 70.5, 28, 3.75, %height% - 3.75, 0, 0, false)

		#South
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall_CustomTex(SouthHall_Ext, 2, %5%WallLower, %5%Wall, Blank, 0.5, -%1%, 0, %1%, 0, 3.75, %height% - 3.75, 0, 0, false)

	#========================================#

	#Classroom interior walls
		#North
		WallOrientation = left
		DrawWalls = false, true, false, false, false, false
		AddWall NorthHallA_Int, %5%Wall2, 0.5, -70.5, 10, -20, 10, %height%, %height%, 0, 0, 0, 0, false
		AddWall NorthHallA_Int2, %5%Wall2, 0.5, 10, 10, 70.5, 10, %height%, %height%, 0, 0, 0, 0, false
		AddWall NorthHallB_Int, %5%Wall2, 0.5, -70.5, 10, -70.5, 28, %height%, %height%, 0, 0, 0, 0, false

		WallOrientation = right
		DrawWalls = true, false, false, false, false, false
		AddWall NorthHallC_Int, %5%Wall2, 0.5, 70.5, 10, 70.5, 28, %height%, %height%, 0, 0, 0, 0, false

		#South
		WallOrientation = right
		DrawWalls = true, false, false, false, false, false
		AddWall SouthHall_Int, %5%Wall2, 0.5, -%1%, 0, %1%, 0, %height%, %height%, 0, 0, 0, 0, false

	#========================================#

	#South divider walls
		WallOrientation = center
		DrawWalls = true, true, false, false, false, false
		AddWall SouthDivider1, %5%Wall2, 1, -60, -%2%, -60, 0, %height%, %height%, 0, 0, 0, 0, false
		AddWall SouthDivider2, %5%Wall2, 1, -20, -%2%, -20, 0, %height%, %height%, 0, 0, 0, 0, false
		AddWall SouthDivider3, %5%Wall2, 1, 20, -%2%, 20, 0, %height%, %height%, 0, 0, 0, 0, false
		AddWall SouthDivider4, %5%Wall2, 1, 60, -%2%, 60, 0, %height%, %height%, 0, 0, 0, 0, false

	#North divider walls
		WallOrientation = center
		DrawWalls = true, true, false, false, false, false
		AddWall NorthDivider1, %5%Wall2, 1, 40, 10, 40, %2%, %height%, %height%, 0, 0, 0, 0, false
		AddWall NorthDivider2A, %5%Wall2, 1, -35, 10, -35, %2%, %height%, %height%, 0, 0, 0, 0, false
		AddWall NorthDivider2B, %5%Wall2, 1, -50, 10, -50, %2%, %height%, %height%, 0, 0, 0, 0, false
		
	#========================================#
	
	#Classroom doors
		#South
		RoomDoors_NS_South(%floorid%01, -90 - 1.75, -70 + 1.75)
		RoomDoors_NS_South(%floorid%02, -50 - 1.75, -30 + 1.75)
		RoomDoors_NS_South(%floorid%03, -10 - 1.75, 10 + 1.75)
		RoomDoors_NS_South(%floorid%04, 30 - 1.75, 50 + 1.75)
		RoomDoors_NS_South(%floorid%05, 70 - 1.75, 90 + 1.75)

		#South
		RoomDoors_NS_North(%floorid%06, 65.5, 7, true, Door88, true, %5%Wall2) #Front door
		RoomDoors_NS_North(%floorid%06, 44.5, 6, false, Door88, true, %5%Wall2) #Rear door
		RoomDoors_NS_North(%floorid%07, 35.5, 7, true, Door88, true, %5%Wall2) #Front door
		RoomDoors_NS_North(%floorid%07, 14.5, 6, false, Door88, true, %5%Wall2) #Rear door
		#RoomDoors_NS_North(%floorid%08, -24.5, 7, true, Door88, true, %5%Wall2) #Front door
		#RoomDoors_NS_North(%floorid%08, -45.5, 6, false, Door88, true, %5%Wall2) #Rear door

		SetAutoSize = false, false
		RoomDoors_NS_North(%floorid%08, -32.5, 7, true, Door9bWhite, false, Door9bWhiteSide) #Rear door
		RoomDoors_NS_North(%floorid%09, -37.5, 6, true, Door9bWhite, false, Door9bWhiteSide) #Rear door
		RoomDoors_NS_North(%floorid%10, -52.5, 6, true, Door9bWhite, false, Door9bWhiteSide) #Rear door
		SetAutoSize = true, true
<EndFloor>

################################################################################

#Set door textures for restrooms
<Function RR_DTex>
	Set DTex = Door8
	if[%floor% > 0] Set DTex = Door6
	Set DSTex = %DTex%Side
<EndFunction>

#Restrooms
<Floors 0 to 5>
	TexPrefix() #Set texture prefixes
	RR_DTex() #Set door textures
	Set RRHeight = %height%
	Set RRCeiHeight = 8.5
	
	Set ExtTex = %5%Wall
	if[%floor% > 0] Set ExtTex = %5%Wall2

		#Restrooms' floors
		Set Extra = 0
		if[%floor% = 0] Set Extra = 0.2
		CutAll -20, %interfloorheight% - 0.1, 10.5 - %Extra%, 10, %interfloorheight% + 1, 40, false, true

		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
		AddFloor RR_Floor, RestroomFloor, 0.5, -20, 10.5, 10, 40, 0, 0, 0, 0, false
		if[%floor% = 0] AddFloor RR_FloorTrim, BlackTrim, 0.5, -20, 10.5 - 0.2, 10, 10.5, 0, 0, 0, 0, false

		#Restrooms' dropped ceiling
		FloorOrientation = bottom
		DrawWalls = true, false, false, false, false, false
		AddFloor RR_DropCeiling, DropCeiling, 0.5, -20, 10, 10, 40, %RRCeiHeight%, %RRCeiHeight%, 0, 0, false
	
		#Restroom walls
		WallOrientation = left
		DrawWalls = true, true, false, false, false, false
		TextureOverride %ExtTex%, RestroomWall, Blank, Blank, Blank, Blank
		AddWall RR_Left, RestroomWall, 0.5, -20, 10, -20, 40, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		if[%floor% > 0] DrawWalls = false, true, false, false, false, false
		TextureOverride %ExtTex%, RestroomWall, Blank, Blank, Blank, Blank
		AddWall RR_Front, RestroomWall, 0.5, -20, 10, 10, 10, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

			#Extra walls to provide privacy
			DrawWalls = true, true, true, true, false, false
			if[%floor% = 0] AddWall RR_Front_Extra, %5%Wall, 0.75, -20 + 3, 2, 10 - 3, 2, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		WallOrientation = right
		DrawWalls = true, true, false, false, false, false
		TextureOverride RestroomWall, %ExtTex%, Blank, Blank, Blank, Blank
		AddWall RR_Right, RRWall, 0.5, 10, 10, 10, 40, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		WallOrientation = center
		DrawWalls = true, true, false, false, false, false
		AddWall RR_Middle, RestroomWall, 1, -5, 10, -5, 40, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		#Restroom doors
		WallOrientation = right
		AddStdDoor Floor %floor%, RR1Door_%floorid%, , , false, %DTex%, %DSTex%, 0.2, front, back, 75, 75, -5 - 2.5, 10.5, 3, 7, 0, 0, 0, 0, 0
		AddStdDoor Floor %floor%, RR2Door_%floorid%, , , false, %DTex%, %DSTex%, 0.2, back, back, 75, 75, -5 + 2.5, 10.5, 3, 7, 0, 0, 0, 0, 0
		AddFillerWalls ConnectionWall, 0.5, -5 - 2.5, 10.5, 3, 7, 0, true, 0, 0, false
		AddFillerWalls ConnectionWall, 0.5, -5 + 2.5, 10.5, 3, 7, 0, true, 0, 0, false
	
		#Restroom signs
		Set BoxTex = WoodSign2
		Set SignSuffix = _Brown

			#Signs above doors
			AddBoxSign(RR1_Sign, Sign_RestroomW%SignSuffix%, Sign_RestroomW%SignSuffix%, %BoxTex%, 0.05, true, true, true, -5 - 2.5, 10 - 0.025, 1.25, 1.25, 7.25, false)
			AddBoxSign(RR2_Sign, Sign_RestroomM%SignSuffix%, Sign_RestroomM%SignSuffix%, %BoxTex%, 0.05, true, true, true, -5 + 2.5, 10 - 0.025, 1.25, 1.25, 7.25, false)
<EndFloors>

#External shaft and stairwell walls
<Floor 0>
	TexPrefix()
	
	#Elevator shafts
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, -80, 34, 10, 6, Shaft1, %height%, 0, true, false, true, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, 80, 34, 10, 6, Shaft2, %height%, 0, true, true, false, false, false, false, false)

	#Stairwells
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, -95, 27.5, 5, 12.5, ST1, %height%, 0, true, false, true, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, 95, 27.5, 5, 12.5, ST1, %height%, 0, true, true, false, false, false, false, false)
<EndFloor>

<Floors 1 to 5>
	TexPrefix()
	
	#Elevator shafts
	CreateShaftWall(%5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, 0.33, -80, 34, 10, 6, Shaft1, 3.75, 0, true, false, false, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, -80, 34, 10, 6, Shaft1, %height% - 3.75, 3.75, true, false, false, false, false, false, false)

	CreateShaftWall(%5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, 0.33, 80, 34, 10, 6, Shaft2, 3.75, 0, true, false, false, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, 80, 34, 10, 6, Shaft2, %height% - 3.75, 3.75, true, false, false, false, false, false, false)

		#Walls inside the classrooms
		CreateShaftWall(%5%Wall2, %5%Wall2, %5%Wall2, %5%Wall2, %5%Wall, %5%Wall, 0.33, -80, 34, 10, 6, Shaft1_CL, %height%, 0, false, false, true, false, false, false, false)
		CreateShaftWall(%5%Wall2, %5%Wall2, %5%Wall2, %5%Wall2, %5%Wall, %5%Wall, 0.33, 80, 34, 10, 6, Shaft2_CL, %height%, 0, false, true, false, false, false, false, false)

	#Stairwells
	CreateShaftWall(%5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, 0.33, -95, 27.5, 5, 12.5, ST1, 3.75, 0, true, false, true, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, -95, 27.5, 5, 12.5, ST1, %height% - 3.75, 3.75, true, false, true, false, false, false, false)

	CreateShaftWall(%5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, %5%WallLower, 0.33, 95, 27.5, 5, 12.5, ST1, 3.75, 0, true, true, false, false, false, false, false)
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, 95, 27.5, 5, 12.5, ST1, %height% - 3.75, 3.75, true, true, false, false, false, false, false)
<EndFloors>

<Floor 6>
	#Elevator shafts
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, -80, 34, 10, 6, Shaft1, %fullheight%, 0, true, false, true, true, true, false, true)
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, 80, 34, 10, 6, Shaft2, %fullheight%, 0, true, true, false, true, true, false, true)

	#Stairwells
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, -95, 27.5, 5, 12.5, ST1, %fullheight%, 0, true, true, true, true, true, false, true)
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, 95, 27.5, 5, 12.5, ST1, %fullheight%, 0, true, true, true, true, true, false, true)
<EndFloor>

########################################

#Support columns
<Floors 0 to 5>
	#Set Values
	TexPrefix() #Set texture prefixes
	Set 1 = 100 #Building Dimension X
	Set 2 = 40 #Building Dimension Z
	Set SupportTex = %5%Wall
	if[%floor% > 0] Set SupportTex = %5%Wall2
	Set DistanceX = 40 #Distance between supports X
	Set DistanceZ = 40 #Distance between supports Z
	Set SupportHeight = %fullheight%
	Set SupportOffset = -%interfloorheight%

	#Corner Supports
	#CreateWallBox2 External, SupportNW, %SupportTex%, -%1% + 1.25, %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	#CreateWallBox2 External, SupportNE, %SupportTex%, %1% - 1.25, %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSW, %SupportTex%, -%1% + 1.25, -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSE, %SupportTex%, %1% - 1.25, -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#North Supports
	CreateWallBox2 External, SupportN1, %SupportTex%, -%1% + (%DistanceX% * 1), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN2, %SupportTex%, -%1% + (%DistanceX% * 2), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN3, %SupportTex%, -%1% + (%DistanceX% * 3), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN4, %SupportTex%, -%1% + (%DistanceX% * 4), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#South Supports
	CreateWallBox2 External, SupportS1, %SupportTex%, -%1% + (%DistanceX% * 1), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS2, %SupportTex%, -%1% + (%DistanceX% * 2), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS3, %SupportTex%, -%1% + (%DistanceX% * 3), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS4, %SupportTex%, -%1% + (%DistanceX% * 4), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#West Supports
	CreateWallBox2 External, SupportW1, %SupportTex%, -%1% + 1.25, -%2% + (%DistanceZ% * 1), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#East Supports
	CreateWallBox2 External, SupportE1, %SupportTex%, %1% - 1.25, -%2% + (%DistanceZ% * 1), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
<EndFloors>

##--------------------------------------------------------------------------------##

#STAIRWELLS
<Function StairwellSets>
	#Set Values
	Set NumSteps = 11 #Total: 22 steps
<EndFunction>

#West Stairwell
CreateStairwell 1, -95, 27.5, 0, 6
CutStairwell 1, -4.5 - 0.5, -12, 4.5, 12, 0, 5
ShowFullStairs 1 = always

<Floors 0 to 6>
	StairwellSets() #Determine stairs' size

	Set 21 = -95 #Center X
	Set 22 = 27.5 #Center Z
	Set 23 = 4.5 #Cut X
	Set 24 = 12 #Cut Z
	Set 25 = 1 #Stairwell Number
	Set 26 = 0.5 #Riser Size

	#Walls
	WallOrientation = right
	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, Stair%25%Front, StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Left, StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	WallOrientation = left
	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, Stair%25%Right, StairsWall, 0.5, %23%, -%24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Back, StairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	WallOrientation = center
	DrawWalls = true, true, true, true, false, true
	AddStairsWall %25%, Stair%25%Center, StairsWall2, 1, 0, %24% - 5 - %NumSteps%, 0, %24% - 5, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	#Railing
	WallOrientation = left
	DrawWalls = true, true, true, true, true, true
	if[%floor% = 6] AddStairsWall %25%, Stair%25%Rail, StairsWall2, 0.5, -%23%, %24% - 5 - %NumSteps%, -%23% + 4, %24% - 5 - %NumSteps%, 0.5 + 3.5, 0.5 + 3.5, -0.5, -0.5, 0, 0

	#Ceiling
	FloorOrientation = top
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 6] AddStairsFloor %25%, Stairs%25%Ceiling, StairsCeiling, 0.5, -%23%, -%24%, %23%, %24%, %height%, %height%, false, false, 0, 0

	#Stairs
	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 6] OP_OrthoStair(%25%, ST%25%_LowerStairs, Stairs, -%23%, -%23% + 4, front, %24% - 5 - %NumSteps%, %NumSteps%, 1, %26%, 0, 0.5)
	if[%floor% < 6] OP_OrthoStair(%25%, ST%25%_UpperStairs, Stairs, %23% - 4, %23%, back, %24% - 5, %NumSteps%, 1, %26%, %26% * %NumSteps%, 0.5)

	#Landings
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	if[%floor% = 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23% - 0.5, -%24%, %23%, %24%, 0, 0, false, false, 0, 0
	if[%floor% > 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23%, %24% - 5 - %NumSteps%, 0, 0, false, false, 0, 0
	if[%floor% < 6] AddStairsFloor %25%, Stairs%25%LandingUpper, StairsFloor, 0.5, -%23%, %24% - 5, %23%, %24%, %26% * %NumSteps%, %26% * %NumSteps%, false, false, 0, 0

	#Floor Signs
	Set Suffix = %floorid%
	Set SignCenterPoint = -1

	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, ST%25%FloorSign, BlankSign%Suffix%, 0, %SignCenterPoint% - 1, -%24% + 0.001, %SignCenterPoint% + 1, -%24% + 0.001, 2, 2, 4 - 0.5, 4 - 0.5, 0, 0

	#Doors
	WallOrientation = right
	TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 6, 75, %23% - 2, -%24%, 3, 7, 0, 0, 0

	#Emergency Exit Door
	SetLock 1, 0
	WallOrientation = left
	TextureOverride ServiceDoor2, ServiceDoor2B, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	if[%floor% = 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 1, 75, -%23% - 0.5, -%24% + 2, 3, 7, 0, 0, 0
	SetLock 0, 0

		#Filler walls
		WallOrientation = right
		if[%floor% > 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% + %23% - 2, %22% - %24%, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true
		WallOrientation = left
		if[%floor% = 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% - %23% - 0.5, %22% - %24% + 2, 3, 7 - 0.001, %interfloorheight%, false, 0, 0, true

	#Exit Signs
	Set SignTex = ExitSign
	AddBoxSign(Stair%25%_MainExits, %SignTex%, StainlessSteel, StainlessSteel, 0.1, true, true, true, %21% + %23% - 2, %22% - %24% - 0.55, 1.8, 0.9, %interfloorheight% + 7.2, true)
	if[%floor% > 0 & %floor% < 6] AddBoxSign(Stair%25%_Hallway, ExitSignLeft, ExitSignRight, StainlessSteel, 0.1, false, true, true, %21% + %23% - 2, 5, 1.8, 0.9, %height% - 0.9, false)
	if[%floor% = 0] AddBoxSign(Stair%25%_SLExit, StainlessSteel, ExitSign, StainlessSteel, 0.1, false, true, true, %21% - %23% + 0.05, %22% - %24% + 2, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloors>

#East Stairwell
CreateStairwell 2, 95, 27.5, 0, 6
CutStairwell 2, -4.5, -12, 4.5 + 0.5, 12, 0, 5
ShowFullStairs 2 = always

<Floors 0 to 6>
	StairwellSets() #Determine stairs' size

	Set 21 = 95 #Center X
	Set 22 = 27.5 #Center Z
	Set 23 = 4.5 #Cut X
	Set 24 = 12 #Cut Z
	Set 25 = 2 #Stairwell Number
	Set 26 = 0.5 #Riser Size

	#Walls
	WallOrientation = right
	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, Stair%25%Front, StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Left, StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	WallOrientation = left
	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, Stair%25%Right, StairsWall, 0.5, %23%, -%24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Back, StairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	WallOrientation = center
	DrawWalls = true, true, true, true, false, true
	AddStairsWall %25%, Stair%25%Center, StairsWall2, 1, 0, %24% - 5 - %NumSteps%, 0, %24% - 5, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	#Railing
	WallOrientation = left
	DrawWalls = true, true, true, true, true, true
	if[%floor% = 6] AddStairsWall %25%, Stair%25%Rail, StairsWall2, 0.5, %23% - 4, %24% - 5 - %NumSteps%, %23%, %24% - 5 - %NumSteps%, 0.5 + 3.5, 0.5 + 3.5, -0.5, -0.5, 0, 0

	#Ceiling
	FloorOrientation = top
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 6] AddStairsFloor %25%, Stairs%25%Ceiling, StairsCeiling, 0.5, -%23%, -%24%, %23%, %24%, %height%, %height%, false, false, 0, 0

	#Stairs
	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 6] OP_OrthoStair(%25%, ST%25%_LowerStairs, Stairs, %23% - 4, %23%, front, %24% - 5 - %NumSteps%, %NumSteps%, 1, %26%, 0, 0.5)
	if[%floor% < 6] OP_OrthoStair(%25%, ST%25%_UpperStairs, Stairs, -%23%, -%23% + 4, back, %24% - 5, %NumSteps%, 1, %26%, %26% * %NumSteps%, 0.5)

	#Landings
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	if[%floor% = 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23% + 0.5, %24%, 0, 0, false, false, 0, 0
	if[%floor% > 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23%, %24% - 5 - %NumSteps%, 0, 0, false, false, 0, 0
	if[%floor% < 6] AddStairsFloor %25%, Stairs%25%LandingUpper, StairsFloor, 0.5, -%23%, %24% - 5, %23%, %24%, %26% * %NumSteps%, %26% * %NumSteps%, false, false, 0, 0

	#Floor Signs
	Set Suffix = %floorid%
	Set SignCenterPoint = 1

	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, ST%25%FloorSign, BlankSign%Suffix%, 0, %SignCenterPoint% - 1, -%24% + 0.001, %SignCenterPoint% + 1, -%24% + 0.001, 2, 2, 4 - 0.5, 4 - 0.5, 0, 0

	#Doors
	WallOrientation = right
	TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 7, 75, -%23% + 2, -%24%, 3, 7, 0, 0, 0

	#Emergency Exit Door
	SetLock 2, 0
	WallOrientation = right
	TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	if[%floor% = 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 2, 75, %23% + 0.5, -%24% + 2, 3, 7, 0, 0, 0
	SetLock 0, 0

		#Filler walls
		WallOrientation = right
		if[%floor% > 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% - %23% + 2, %22% - %24%, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true
		if[%floor% = 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% + %23% + 0.5, %22% - %24% + 2, 3, 7 - 0.001, %interfloorheight%, false, 0, 0, true

	#Exit Signs
	Set SignTex = ExitSign
	AddBoxSign(Stair%25%_MainExits, %SignTex%, StainlessSteel, StainlessSteel, 0.1, true, true, true, %21% - %23% + 2, %22% - %24% - 0.55, 1.8, 0.9, %interfloorheight% + 7.2, true)
	if[%floor% > 0 & %floor% < 6] AddBoxSign(Stair%25%_Hallway, ExitSignLeft, ExitSignRight, StainlessSteel, 0.1, false, true, true, %21% - %23% + 2, 5, 1.8, 0.9, %height% - 0.9, false)
	if[%floor% = 0] AddBoxSign(Stair%25%_SLExit, ExitSign, StainlessSteel, StainlessSteel, 0.1, false, true, true, %21% + %23% - 0.05, %22% - %24% + 2, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloors>

##--------------------------------------------------------------------------------##

#SHAFTS
#West Elevators
AddStdShaft(1, 1, -80, 34, 9.67, 5.67, -1, 6, ShaftIntWall, 0.5, 0.33, 0, 5, %interfloorheight%)

#East Elevators
AddStdShaft(2, 1, 80, 34, 9.67, 5.67, -1, 6, ShaftIntWall, 0.5, 0.33, 0, 5, %interfloorheight%)

#Machine room doors
<Floor 6>
	Set DTex = ServiceDoor3
	Set DSTex = %DTex%Side

	#Shaft 1 (West)
	Set Door_MainX = -80
	Set Door_MainZ = 34 - 6

	WallOrientation = left
	AddStdDoor DoorManager, %floorid%_Shaft1MR_L, , , false, %DTex%, %DSTex%, 0.2, front, front, 75, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddStdDoor DoorManager, %floorid%_Shaft1MR_R, , , false, %DTex%, %DSTex%, 0.2, back, front, 75, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddFillerWalls ConnectionWall, 0.66, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, true, 0, 0, true

	#Shaft 2 (East)
	Set Door_MainX = 80
	Set Door_MainZ = 34 - 6

	WallOrientation = left
	AddStdDoor DoorManager, %floorid%_Shaft2MR_L, , , false, %DTex%, %DSTex%, 0.2, front, front, 75, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddStdDoor DoorManager, %floorid%_Shaft2MR_R, , , false, %DTex%, %DSTex%, 0.2, back, front, 75, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddFillerWalls ConnectionWall, 0.66, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, true, 0, 0, true
<EndFloor>

#Set idle sounds
<Elevators 1 to 4>
	CarIdleSound = EG881Sounds/MTelevidle.wav #EG881
<EndElevators>

#West elevators
<Elevator 1>
	Name = Elevator 1
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 5
	AssignedShaft = 1
	CreateElevator true, -5 + 0.5, 0, 0
<EndElevator>

<Elevator 2>
	Name = Elevator 2
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 5
	AssignedShaft = 1
	CreateElevator true, 5 - 0.5, 0, 0
<EndElevator>

#East elevators
<Elevator 3>
	Name = Elevator 3
	Type = Service
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 5
	AssignedShaft = 2
	CreateElevator true, -5 + 0.5, 0, 0
<EndElevator>

<Elevator 4>
	Name = Elevator 4
	Type = Service
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 5
	AssignedShaft = 2
	CreateElevator true, 5 - 0.5, 0, 0
<EndElevator>

<Function BtnPanelE1-4>
	Set PanelNumber = %param1%
	Set BtnPrefix = MTButton

	AddButton %PanelNumber%, , %BtnPrefix%6, %BtnPrefix%Lit6, 1, 2, 5, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%5, %BtnPrefix%Lit5, 1, 1, 4, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%4, %BtnPrefix%Lit4, 2, 2, 3, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%3, %BtnPrefix%Lit3, 2, 1, 2, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%2, %BtnPrefix%Lit2, 3, 2, 1, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%1, %BtnPrefix%Lit1, 3, 1, 0, 1, 1

	AddButton %PanelNumber%, , %BtnPrefix%Open, %BtnPrefix%Open, 5, 1, open, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%Close, %BtnPrefix%Close, 5, 2, close, 1, 1
	if[%PanelNumber% = 1] AddButton %PanelNumber%, , %BtnPrefix%Stop, %BtnPrefix%Stop, 6, 1, stop, 1, 1
	if[%PanelNumber% = 1] AddButton %PanelNumber%, , %BtnPrefix%Alarm, %BtnPrefix%Alarm, 6, 2, alarm, 1, 1
<EndFunction>

<Elevators 1 to 4>
	Set Prefix = West
	if[%elevator% > 2] Set Prefix = East

	#Set textures and dimensions
	if[%elevator% = 1 | %elevator% = 3] Set P1Width = 1.25
	if[%elevator% = 1 | %elevator% = 3] Set P2Width = 0.25
	if[%elevator% = 2 | %elevator% = 4] Set P1Width = 0.25
	if[%elevator% = 2 | %elevator% = 4] Set P2Width = 1.25

	M10_ElevCabTex(ElevExt, %Prefix%ElevFloor, %Prefix%ElevCeiling, %Prefix%ElevWall, %Prefix%ElevWall)
	M10_ElevDimensions(6, 9.28, 7.5, 0.2, 0.2, false, true, true, true)
	M10_ElevPanels(%Prefix%ElevPanelMain, %Prefix%ElevPanelTop, %P1Width%, %P2Width%, 0.5, 0.2, true, false, false, false)

	#Finish elevator assembly
	M10_ElevInterior()
	#--------------------
	#Car Doors
	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -4.64

	WallOrientation = right
	if[%elevator% = 1 | %elevator% = 3] AddDoorComponent 1, 2Speed_Fast, %ElevDoorTex%2, %ElevDoorTex%2, 0.15, left, 0.8, 0.6, 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 2.75, %IDP% - (0.15 * 1), 7, 0, 0, 0, 0, 0
	if[%elevator% = 1 | %elevator% = 3] AddDoorComponent 1, 2Speed_Slow, %ElevDoorTex%2, %ElevDoorTex%2, 0.15, left, 0.4, 0.3, 2.75 - (2.25 * 2), %IDP%, 2.75 - (2.25 * 1), %IDP%, 7, 0, 0, 0, 0, 0

	if[%elevator% = 2 | %elevator% = 4] AddDoorComponent 1, 2Speed_Fast, %ElevDoorTex%, %ElevDoorTex%, 0.15, right, 0.8, 0.6, -2.75, %IDP% - (0.15 * 1), -2.75 + (2.25 * 1), %IDP% - (0.15 * 1), 7, 0, 0, 0, 0, 0
	if[%elevator% = 2 | %elevator% = 4] AddDoorComponent 1, 2Speed_Slow, %ElevDoorTex%, %ElevDoorTex%, 0.15, right, 0.4, 0.3, -2.75 + (2.25 * 1), %IDP%, -2.75 + (2.25 * 2), %IDP%, 7, 0, 0, 0, 0, 0
	FinishDoors 1, false, false

		#Door track
		FloorOrientation = top
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevDoorTrackZ, ElevExt, ElevExt, ElevExt, ElevExt
		if[%elevator% = 1 | %elevator% = 3] AddFloor DoorTrack, ElevDoorTrackZ, 0.05, 2.75 - (2.25 * 3), %IDP% - (0.15 * 2), 2.75, %IDP%, 0, 0, 0, 0
		TextureOverride ElevExt, ElevDoorTrackZ, ElevExt, ElevExt, ElevExt, ElevExt
		if[%elevator% = 2 | %elevator% = 4] AddFloor DoorTrack, ElevDoorTrackZ, 0.05, -2.75, %IDP% - (0.15 * 2), -2.75 + (2.25 * 3), %IDP%, 0, 0, 0, 0

		#Top filler
		FloorOrientation = bottom
		DrawWalls = true, true, true, true, true, true
		TextureOverride %Prefix%ElevPanelTop, ElevExt, ElevExt, ElevExt, ElevExt, ElevExt
		if[%elevator% = 1 | %elevator% = 3] AddFloor TopFiller, %Prefix%ElevPanelTop, 0.05, 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 2.75, %IDP%, 7, 7, 0, 0
		TextureOverride %Prefix%ElevPanelTop, ElevExt, ElevExt, ElevExt, ElevExt, ElevExt
		if[%elevator% = 2 | %elevator% = 4] AddFloor TopFiller, %Prefix%ElevPanelTop, 0.05, -2.75, %IDP% - (0.15 * 1), -2.75 + (2.25 * 1), %IDP%, 7, 7, 0, 0

	#Gap filler
	WallOrientation = right
	DrawWalls = true, true, true, true, true, true
	TextureOverride ElevExt, ElevExt, %Prefix%ElevPanelMain, ElevExt, ElevExt, ElevExt
	if[%elevator% = 1 | %elevator% = 3] AddWall GapFiller, %Prefix%ElevPanelMain, 0.15 * 2, 2.75, %IDP%, 3, %IDP%, 7, 7, 0, 0, 0, 0
	TextureOverride ElevExt, ElevExt, ElevExt, %Prefix%ElevPanelMain, ElevExt, ElevExt
	if[%elevator% = 2 | %elevator% = 4] AddWall GapFiller, %Prefix%ElevPanelMain, 0.15 * 2, -3, %IDP%, -2.75, %IDP%, 7, 7, 0, 0, 0, 0
	#--------------------
	#Button Panels
	if[%elevator% = 1 | %elevator% = 3] Set Position = -2.275
	if[%elevator% = 2 | %elevator% = 4] Set Position = 2.275

		#Panel back
		CreateWallBox2 elevator, MainPB, ElevPanel, %Position%, -4.44 + 0.025, 0.7, 0.05, 4.75, 2.25, 0, 0, false, true, true, true

		#Panel
		CreatePanel , 6, 2, back, %Position%, -4.44 + 0.051, 0.2, 0.2, 0.3, -0.1, 3.55, 0, 0

			#Buttons
			BtnPanelE1-4(1)

	#Elevator number sign
	DrawWalls = false, true, false, false, false, false
	AddWall ElevNo%elevator%_Inside, IN_ElevNo%elevator%, 0, %Position% - 0.25, -4.44 + 0.051, %Position% + 0.25, -4.44 + 0.051, 0.5, 0.5, 6.25, 6.25, 0, 0
	
	#Floor Indicator
	AddFloorIndicator Ind_Button, back, %Position%, -4.44 + 0.051, 0.35, 0.35, 5.75
	#--------------------
	#Shaft components
	DrawWalls = true, true, true, true, true, true
		#Ropes
		RopePosition = 0, 7.5 + 0.2, 0
		RopeTexture = Ropes
	
		#Counterweight
		if[%elevator% = 1 | %elevator% = 3] Set Position = -3 - 0.8
		if[%elevator% = 2 | %elevator% = 4] Set Position = 3 + 0.8
		CreateCounterweight CWeight_Frame, CWeight, %Position%, 0, 0.5, 6, 2, 0.5
	
		#Rails
		AddRails ElevRail, ElevRailSide, 0, 0, false, 3 + 0.06, 0.1
		AddRails ElevRail, ElevRailSide, %Position%, 0, true, 1 + 0.06, 0.1
<EndElevators>

<Elevators 1 to 2>
	#MORE DETAILED INTERIORS
	Set Prefix = West

	#1. Panels on walls
	Set WPTex = %Prefix%ElevWallPanel
	Set WPanelHeight = 7.5 - 0.1 - 0.5

		#Back panels
		CreateWallBox2 elevator, Panel_B1, %WPTex%, 0 - (5.6 * 1/4), 4.44 - 0.01, (5.6 * 1/2) - 0.3, 0.02, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_B2, %WPTex%, 0 + (5.6 * 1/4), 4.44 - 0.01, (5.6 * 1/2) - 0.3, 0.02, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

		#Left panels
		CreateWallBox2 elevator, Panel_L1, %WPTex%, -2.8 + 0.01, 0 - (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_L2, %WPTex%, -2.8 + 0.01, 0, 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_L3, %WPTex%, -2.8 + 0.01, 0 + (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

		#Right panels
		CreateWallBox2 elevator, Panel_R1, %WPTex%, 2.8 - 0.01, 0 - (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_R2, %WPTex%, 2.8 - 0.01, 0, 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_R3, %WPTex%, 2.8 - 0.01, 0 + (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

	#2. Ceiling lights
	Set LightWidth = 3
	Set LightLength = 4.5
	Set TrimWidth = 0.2

	CreateWallBox2 elevator, CL_Main, %Prefix%ElevCeilingLight, 0, 0, %LightWidth%, %LightLength%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true

	#1a. Trims around lights
	CreateWallBox2 elevator, CL_Trim_Front, %Prefix%ElevCeilingTrim2, 0, 0 - (%LightLength% / 2) - (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Trim_Back, %Prefix%ElevCeilingTrim2, 0, 0 + (%LightLength% / 2) + (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Trim_Left, %Prefix%ElevCeilingTrim, 0 - (%LightWidth% / 2) - (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Trim_Right, %Prefix%ElevCeilingTrim, 0 + (%LightWidth% / 2) + (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
<EndElevators>

<Elevators 3 to 4>
	#MORE DETAILED INTERIORS
	Set Prefix = East

	#1. Panels on walls
	Set WPTex = %Prefix%ElevWallPanel
	Set WPanelHeight = 7.5

		#Back panels
		CreateWallBox2 elevator, Panel_B1, %WPTex%, 0, 4.44 - 0.01, 2.5, 0.02, %WPanelHeight%, 0, 0, 0, false, true, true, true

		#Left panels
		CreateWallBox2 elevator, Panel_L1, %WPTex%, -2.8 + 0.01, 0, 0.02, 4, %WPanelHeight%, 0, 0, 0, false, true, true, true

		#Right panels
		CreateWallBox2 elevator, Panel_R1, %WPTex%, 2.8 - 0.01, 0, 0.02, 4, %WPanelHeight%, 0, 0, 0, false, true, true, true

	#2. Ceiling lights
	Set LightWidth = 2.375
	Set LightLength = 9.28 - 0.4
	Set TrimWidth = 0.2

	CreateWallBox2 elevator, CL_Main, %Prefix%ElevCeilingLight, 0, 0, %LightWidth%, %LightLength%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true

	#1a. Trims around lights
	CreateWallBox2 elevator, CL_Trim_Left, %Prefix%ElevCeilingTrim, 0 - (%LightWidth% / 2) - (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Trim_Right, %Prefix%ElevCeilingTrim, 0 + (%LightWidth% / 2) + (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength%, 0.05, 7.5 - 0.05, 0, 0, false, true, true, true

	#3. Downlights
	DrawWalls = true, false, false, false, false, false
	Set DLOffset = 7.5 - 0.001 #Offsets for downlights
	AddFloor Downlight1/1, %Prefix%ElevCeilingDL, 0, -2.8 + 0.75 - 0.11, (-3 - 0.2) - 0.11, -2.8 + 0.75 + 0.11, (-3 - 0.2) + 0.11, %DLOffset%, %DLOffset%, 0, 0
	AddFloor Downlight1/2, %Prefix%ElevCeilingDL, 0, 2.8 - 0.75 - 0.11, (-3 - 0.2) - 0.11, 2.8 - 0.75 + 0.11, (-3 - 0.2) + 0.11, %DLOffset%, %DLOffset%, 0, 0
	AddFloor Downlight2/1, %Prefix%ElevCeilingDL, 0, -2.8 + 0.75 - 0.11, 0 - 0.11, -2.8 + 0.75 + 0.11, 0 + 0.11, %DLOffset%, %DLOffset%, 0, 0
	AddFloor Downlight2/2, %Prefix%ElevCeilingDL, 0, 2.8 - 0.75 - 0.11, 0 - 0.11, 2.8 - 0.75 + 0.11, 0 + 0.11, %DLOffset%, %DLOffset%, 0, 0
	AddFloor Downlight3/2, %Prefix%ElevCeilingDL, 0, -2.8 + 0.75 - 0.11, (3 + 0.2) - 0.11, -2.8 + 0.75 + 0.11, (3 + 0.2) + 0.11, %DLOffset%, %DLOffset%, 0, 0
	AddFloor Downlight3/2, %Prefix%ElevCeilingDL, 0, 2.8 - 0.75 - 0.11, (3 + 0.2) - 0.11, 2.8 - 0.75 + 0.11, (3 + 0.2) + 0.11, %DLOffset%, %DLOffset%, 0, 0
<EndElevators>

#HALL STATIONS
#Set textures (Passenger)
<Function ElevHallTex>
	TexPrefix() #Set prefixes for shaft doors

	#Shaft doors
	Set MDoorTexture = %5%ShaftDoors

	#Use automatic filler walls or not
	Set AutoFiller = false

	#Door trims
		#Sides
		Set DoorTrimTexture = %MDoorTexture%_Trim

		#Top
		Set DoorTrimTexture_Top = %DoorTrimTexture%
		if[%floor% = 0] Set DoorTrimTexture_Top = %MDoorTexture%_TrimTop

	#Deco dimensions
		Set SideDecoH = %height%
		Set TopDecoH = %height% - 7
		if[%floor% > 0] Set SideDecoH = 7.2
		if[%floor% > 0] Set TopDecoH = 0.2

	#Call button panels
	Set BPanelTexture = ElevExtPanels
	Set ButtonPrefix = MTButton

	#Lantern panels
	Set LPanelTexture = %BPanelTexture%
	Set LanternPrefix = Lantern

		#Lateral offset for lantern panels
		Set ExtraLateralOffset = 0.05
		if[%floor% > 0] Set ExtraLateralOffset = 0
<EndFunction>

#Elevator 1-2 (West)
<Floors 0 to 5>
	#Shaft Doors
	ElevHallTex() #Set shaft door and panel textures
	Set StartNo = 1 #Number of the first elevator

	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -4.64 - 0.4

	WallOrientation = right
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Fast, %MDoorTexture%, %MDoorTexture%, 0.15, left, 0.8, 0.6, 2.75 - (2.25 * 1), %IDP%, 2.75, %IDP%, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Slow, %MDoorTexture%2, %MDoorTexture%2, 0.15, left, 0.4, 0.3, 2.75 - (2.25 * 2), %IDP% - (0.15 * 1), 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 7, 0, -1, 0, -1, 0
	FinishShaftDoor %StartNo%, 1, %AutoFiller%, false

	WallOrientation = right
	AddShaftDoorComponent %StartNo% + 1, 1, 2Speed_Fast, %MDoorTexture%, %MDoorTexture%, 0.15, right, 0.8, 0.6, -2.75, %IDP%, -2.75 + (2.25 * 1), %IDP%, 7, 0, -1, 0, -1, 0
	AddShaftDoorComponent %StartNo% + 1, 1, 2Speed_Slow, %MDoorTexture%2, %MDoorTexture%2, 0.15, right, 0.4, 0.3, -2.75 + (2.25 * 1), %IDP% - (0.15 * 1), -2.75 + (2.25 * 2), %IDP% - (0.15 * 1), 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo% + 1, 1, %AutoFiller%, false

	#Door tracks
	AddShaftTrack(1, 1, -4 - 4.5, -4 + 2.25, -5.34, -5.34 + 0.3, ElevDoorTrackZ, ElevExt, 0.05, 1, 7)
	AddShaftTrack(2, 1, 4 - 2.25, 4 + 4.5, -5.34, -5.34 + 0.3, ElevDoorTrackZ, ElevExt, 0.05, 1, 7)

	#Top filler
	AddShaftTrack(1, 1, -4, -4 + 2.25, -5.34, -5.34 + 0.15, %DoorTrimTexture%, ElevExt, 0.05, 2, 7)
	AddShaftTrack(2, 1, 4 - 2.25, 4, -5.34, -5.34 + 0.15, %DoorTrimTexture%, ElevExt, 0.05, 2, 7)

	#Gap filler
		WallOrientation = left
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevExt, %DoorTrimTexture%, %DoorTrimTexture%, ElevExt, ElevExt
		AddShaftWall 1, GapFiller_Elev1, ElevExt, 0.15 * 2, -4 + 2.25, -5.34, -4 + 2.25 + 0.25, -5.34, 7, 7, %interfloorheight%, %interfloorheight%, 0, 0	
		TextureOverride ElevExt, ElevExt, %DoorTrimTexture%, %DoorTrimTexture%, ElevExt, ElevExt
		AddShaftWall 1, GapFiller_Elev2, ElevExt, 0.15 * 2, 4 - 2.25 - 0.25, -5.34, 4 - 2.25, -5.34, 7, 7, %interfloorheight%, %interfloorheight%, 0, 0	

	#****************************************#

	#Door trims/fillers
	WallTex() #Set filler textures to match with shaft walls
	Set Shaft1CX = -80 #1st shaft for elevator bank
	Set Shaft1CZ = 34

		#1st shaft
		WallOrientation = left
		AddDecoObject(Elev1_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, front, true, false, 0.05, 0.2, %Shaft1CX% - 4, %Shaft1CZ% - 6, 4.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)
		AddDecoObject(Elev2_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, front, true, false, 0.05, 0.2, %Shaft1CX% + 4, %Shaft1CZ% - 6, 4.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)

	#****************************************#

	#Call buttons
	CallButtonElevators = 1, 2
	CreateCallButtons %BPanelTexture%, %ButtonPrefix%Up, %ButtonPrefix%UpLit, %ButtonPrefix%Down, %ButtonPrefix%DownLit, %Shaft1CX%, %Shaft1CZ% - 6.001, 3.5, front, 0.5, 1, true, 0, 0

		#Signs
		DrawWalls = true, false, false, false, false, false
		AddWall E1-2HallSign, FireSign, 0, %Shaft1CX% - 0.25, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 0.25, %Shaft1CZ% - 6 - 0.001, 0.75, 0.75, 2.6, 2.6, 0, 0, false
		AddWall E1-2FloorSign, BlankSign%floorid%, 0, %Shaft1CX% - 0.75, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 0.75, %Shaft1CZ% - 6 - 0.001, 1.5, 1.5, 6 - 0.75, 6 - 0.75, 0, 0, false

	#****************************************#

	#Indicator sets
	Set Offset = 7.75

		#Set textures
		ACMEInd_LanternTex(%LanternPrefix%Up, %LanternPrefix%UpLit, %LanternPrefix%Down, %LanternPrefix%DownLit, %LPanelTexture%, OutsideInd_Button)

		#Indicator creation
		ACMELobbyInds(front, 1, %Shaft1CX% - 4, %Shaft1CZ% - 6 - %ExtraLateralOffset%, %Offset%, 0, 5)
		ACMELobbyInds(front, 2, %Shaft1CX% + 4, %Shaft1CZ% - 6 - %ExtraLateralOffset%, %Offset%, 0, 5)
<EndFloors>

#Elevator 3-4 (East)
<Floors 0 to 5>
	#Shaft Doors
	ElevHallTex() #Set shaft door and panel textures
	Set StartNo = 3 #Number of the first elevator

	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -4.64 - 0.4

	WallOrientation = right
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Fast, %MDoorTexture%, %MDoorTexture%, 0.15, left, 0.8, 0.6, 2.75 - (2.25 * 1), %IDP%, 2.75, %IDP%, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Slow, %MDoorTexture%2, %MDoorTexture%2, 0.15, left, 0.4, 0.3, 2.75 - (2.25 * 2), %IDP% - (0.15 * 1), 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 7, 0, -1, 0, -1, 0
	FinishShaftDoor %StartNo%, 1, %AutoFiller%, false

	WallOrientation = right
	AddShaftDoorComponent %StartNo% + 1, 1, 2Speed_Fast, %MDoorTexture%, %MDoorTexture%, 0.15, right, 0.8, 0.6, -2.75, %IDP%, -2.75 + (2.25 * 1), %IDP%, 7, 0, -1, 0, -1, 0
	AddShaftDoorComponent %StartNo% + 1, 1, 2Speed_Slow, %MDoorTexture%2, %MDoorTexture%2, 0.15, right, 0.4, 0.3, -2.75 + (2.25 * 1), %IDP% - (0.15 * 1), -2.75 + (2.25 * 2), %IDP% - (0.15 * 1), 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo% + 1, 1, %AutoFiller%, false

	#Door tracks
	AddShaftTrack(3, 2, -4 - 4.5, -4 + 2.25, -5.34, -5.34 + 0.3, ElevDoorTrackZ, ElevExt, 0.05, 1, 7)
	AddShaftTrack(4, 2, 4 - 2.25, 4 + 4.5, -5.34, -5.34 + 0.3, ElevDoorTrackZ, ElevExt, 0.05, 1, 7)

	#Top filler
	AddShaftTrack(3, 2, -4, -4 + 2.25, -5.34, -5.34 + 0.15, %DoorTrimTexture%, ElevExt, 0.05, 2, 7)
	AddShaftTrack(4, 2, 4 - 2.25, 4, -5.34, -5.34 + 0.15, %DoorTrimTexture%, ElevExt, 0.05, 2, 7)

	#Gap filler
		WallOrientation = left
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevExt, %DoorTrimTexture%, %DoorTrimTexture%, ElevExt, ElevExt
		AddShaftWall 2, GapFiller_Elev3, ElevExt, 0.15 * 2, -4 + 2.25, -5.34, -4 + 2.25 + 0.25, -5.34, 7, 7, %interfloorheight%, %interfloorheight%, 0, 0	
		TextureOverride ElevExt, ElevExt, %DoorTrimTexture%, %DoorTrimTexture%, ElevExt, ElevExt
		AddShaftWall 2, GapFiller_Elev4, ElevExt, 0.15 * 2, 4 - 2.25 - 0.25, -5.34, 4 - 2.25, -5.34, 7, 7, %interfloorheight%, %interfloorheight%, 0, 0	

	#****************************************#

	#Door trims/fillers
	WallTex() #Set filler textures to match with shaft walls
	Set Shaft1CX = 80 #1st shaft for elevator bank
	Set Shaft1CZ = 34

		#1st shaft
		WallOrientation = left
		AddDecoObject(Elev3_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, front, true, false, 0.05, 0.2, %Shaft1CX% - 4, %Shaft1CZ% - 6, 4.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)
		AddDecoObject(Elev4_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, front, true, false, 0.05, 0.2, %Shaft1CX% + 4, %Shaft1CZ% - 6, 4.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)

	#****************************************#

	#Call buttons
	CallButtonElevators = 3, 4
	CreateCallButtons %BPanelTexture%, %ButtonPrefix%Up, %ButtonPrefix%UpLit, %ButtonPrefix%Down, %ButtonPrefix%DownLit, %Shaft1CX%, %Shaft1CZ% - 6.001, 3.5, front, 0.5, 1, true, 0, 0

		#Signs
		DrawWalls = true, false, false, false, false, false
		AddWall E3-4HallSign, FireSign, 0, %Shaft1CX% - 0.25, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 0.25, %Shaft1CZ% - 6 - 0.001, 0.75, 0.75, 2.6, 2.6, 0, 0, false
		AddWall E3-4FloorSign, BlankSign%floorid%, 0, %Shaft1CX% - 0.75, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 0.75, %Shaft1CZ% - 6 - 0.001, 1.5, 1.5, 6 - 0.75, 6 - 0.75, 0, 0, false

	#****************************************#

	#Indicator sets
	Set Offset = 7.75

		#Set textures
		ACMEInd_LanternTex(%LanternPrefix%Up, %LanternPrefix%UpLit, %LanternPrefix%Down, %LanternPrefix%DownLit, %LPanelTexture%, OutsideInd_Button)

		#Indicator creation
		ACMELobbyInds(front, 3, %Shaft1CX% - 4, %Shaft1CZ% - 6 - %ExtraLateralOffset%, %Offset%, 0, 5)
		ACMELobbyInds(front, 4, %Shaft1CX% + 4, %Shaft1CZ% - 6 - %ExtraLateralOffset%, %Offset%, 0, 5)
<EndFloors>

#Elevator global settings
<Elevators 1 to 4>
	#Door settings
	DoorTimer 1 = 12000
	QuickClose 1 = 8000
	OpenSound 1 = EG881Sounds/freightelevatoropen1.wav #EG881
	CloseSound 1 = EG881Sounds/freightelevatorclose1.wav #EG881

	#Notifications
	NotifyEarly = 1
	QueueResets = true
	UpChimeSound 1 = EG881Sounds/MTupchime2.wav #EG881
	DownChimeSound 1 = EG881Sounds/MTdownchime2.wav #EG881
	BeepSound = EG881Sounds/MTbeep2.wav #EG881

	#Movement Sounds
	MotorStartSound = EG881Sounds/motor_start3.wav #EG881
	MotorRunSound = EG881Sounds/motor_running3.wav #EG881
	MotorStopSound = EG881Sounds/motor_stop3.wav #EG881

	CarStartSound = EG881Sounds/elevstart10.wav #EG881
	CarMoveSound = EG881Sounds/elevmove10.wav #EG881
	CarStopSound = EG881Sounds/elevstop10.wav #EG881
<EndElevators>