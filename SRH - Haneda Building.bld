#Serenity Hospital - Haneda Building
#Copyright (c) 2024-Present - Mn10
#Entire hospital copyright (c) 2016-Present - Mn10

#Start date: 6 November 2024
#Major detail finish date: ???

#SCRIPTS TO BE INCLUDED
	#1. My new scripts
	<Include buildings/Mn10_Scripts/Deco2023_R1.txt>
	<Include buildings/Mn10_Scripts/Shafts_and_Elevators.txt>
	<Include buildings/Mn10_Scripts/Windows.txt>
	
	#2. ObjectPlus and SROC (credit to EG881)
	<Include ObjectPlus/Scripts/OP_Script2.txt> #EG881
	<Include SROC/Scripts/r1_script.txt> #EG881

	#3. My previous scripts
	<Include CustomObjects2018/Scripts/r1_script.txt>
	<Include Shaft Wall Creator/Scripts/r1_script.txt>
	#<Include data/ACME Floor Indicator/Scripts/Indicators.txt>
	<Include data/New_Mitsubishi_Elevator/Scripts/Indicators.txt>

#########################################################################

#Globals
<Globals>
	Name = Serenity Hospital - Haneda Building
	Designer = Mn10 (name suggested by ningsama in August 2016)
	Location = 731 8th Street, Tranquility City
	CameraFloor = 0
	CameraPosition = 0, 0
	CameraRotation = 0, 0, 0
<EndGlobals>

#########################################################################


#Textures
<Textures>
	#Commons
	Load data/black.jpg, Black, 1, 1, false
	Load data/Melvin/transparent128.png, Blank, 1, 1, false
	Load data/EG881/blackdoortrim.jpg, BlackTrim, 1, 1, false #EG881
	Load data/text16.jpg, WoodSign2, 1, 1, false
	Load data/signs_mod/elevhallsign1a.png, FireSign, 1, 1, false #EG881

	#Landscapes
	#Load data/grndroad1mid.jpg, Road, 0.04, 0.04
	Load data/walkway.jpg, Sidewalk, 0.03125, 0.03125
	
		#Plants
		Load data/ogrebullet/textures/grass.jpg, Grass, 0.005, 0.005
		#Load data/ogrebullet/materials/textures/grass_1024.jpg, Grass_Light, 0.005, 0.005
		Load data/EG881/grass2.jpg, Grass_Dark, 0.01, 0.01 #EG881
		Load data/EG881/Trees_Summer/BradfordPear1.png, Tree1, 1, 1, false #EG881
		Load data/EG881/tree1.gif, Tree2, 1, 1, false #EG881

	########################################

	#Windows
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3c.jpg, MainWindows, true, 0.05, 1 #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsInt, 0.05, 1 #EG881
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3c.jpg, MainWindowsB, true, 1, 1, false #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsIntB, 1, 1, false #EG881

	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3c.jpg, LobbyWindows, true, 0.025, 1 #EG881
	Load data/EG881/windows11h_insideblack.png, LobbyWindowsInt, 0.025, 1 #EG881

	########################################

	#External facades
	Load data/granite_0008b.jpg, ExtFacade, 0.0625, 0.0625
	Load data/EG881/Concrete/smoothconcrete1_white.jpg, ExtFacade_Inner, 0.075, 0.075, true #EG881

	#Main textures
		#Lower hospital levels
		Load data/marb047.jpg, LHFloor, 0.2, 0.2
		Load data/marbl3.jpg, LHWall, 0.25, 0.25, true
		Load data/EG881/ceilingtile3x3.jpg, LHCeiling, 0.125, 0.125 #EG881

			Load data/Melvin/WestinTQC/text16door.jpg, LHShaftDoors, 1, 1, false #EG881
			Load data/Melvin/WestinTQC/text16door.jpg, LHShaftDoors2, -1, 1, false #EG881
			Load data/text16.jpg, LHShaftDoors_Trim, 0.5, 0.5, true

		#Upper hospital levels
		Load data/floor065.jpg, UHFloor, 0.25, 0.25
		Load data/marb148.jpg, UHWall, 0.15, 0.15, true
		Load data/EG881/ceilingtile2b.jpg, UHCeiling, 0.125, 0.125 #EG881

			Load data/EG881_Otis/Doors/otiswhitedoorleft.jpg, UHShaftDoors, 1, 1, false #EG881
			Load data/EG881_Otis/Doors/otiswhitedoorright.jpg, UHShaftDoors2, 1, 1, false #EG881
			Load data/EG881_Otis/Doors/otiswhitedoortrim1.jpg, UHShaftDoors_Trim, 1, 1, false #EG881

		#Patient room levels
		Load data/Carpet0006_S.jpg, WardFloor, 0.2, 0.2 #Elevator lobbies + hallways to patient rooms
		Load data/EG881/Drywall/wall5c_bluegray.jpg, WardWall, 0.2, 0.2 #EG881 #Hallway walls
		Load data/black_granite.jpg, WardWall_Deco, 0.1, 0.1
		Load data/EG881/ceilingtile3x3.jpg, WardCeiling, 0.125, 0.125 #EG881 #Entire floor

			#Inside patient rooms
			Load data/Carpet0012_S.jpg, WardFloor_Int, 0.2, 0.2
			Load data/stucco22b.jpg, WardWall_Int, 0.125, 0.125

			#Shaft doors
			Load data/dark muntz door.jpg, WardShaftDoors, 1, 1, false #EG881
			Load data/dark muntz door.jpg, WardShaftDoors2, 1, 1, false #EG881
			Load data/EG881/stainlesssteel2.jpg, WardShaftDoors_Trim, 1, 1, false #EG881

		#Drop ceilings
		Load data/EG881/Ceilings/ceilingtile4single.jpg, DropCeiling, 0.5, 0.5 #EG881 #Restrooms
		#Load data/EG881/Ceilings/ceilingtile4single.jpg, SLDropCeiling, 0.5, 0.5 #EG881 #Service elevator lobby

		#Service level
		Load data/EG881/cem_tr2b.jpg, ServiceFloor, 0.075225, 0.0625 #EG881
		Load data/EG881/cem_tr3.jpg, ServiceFloor2, 0.075225, 0.0625 #EG881
		Load data/stucco4.jpg, ServiceWall, 0.25, 0.25
		Load data/stucco1.jpg, ServiceWall2, 0.125, 0.125
		Load data/EG881/cem_tr2b.jpg, ServiceCeiling, 0.075225, 0.0625 #EG881
		Load data/EG881/cem_tr2c.jpg, ServiceCeiling2, 0.075225, 0.0625 #EG881

		#Roof
		Load data/EG881/cem_tr2b.jpg, RoofFloor, 0.075225, 0.0625 #EG881
		Load data/EG881/Concrete/smoothconcrete1.jpg, RoofFloor2, 0.0625, 0.0625 #EG881 #4th floor setback

	########################################

	#Doors
		#Main doors
		Load data/miscdoor1.jpg, Door1, 1, 1, false
		LoadCropped data/miscdoor1.jpg, Door1Side, 118, 0, 10, 256, 1, 1, false
		Load data/miscdoor6.jpg, Door6, 1, 1, false
		LoadCropped data/miscdoor6.jpg, Door6Side, 236, 0, 20, 512, 1, 1, false

		Load data/miscdoor8.jpg, Door8, 1, 1, false
		LoadCropped data/miscdoor8.jpg, Door8Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor88.gif, Door88, 1, 1, false
		LoadCropped data/miscdoor88.gif, Door88Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor9b.jpg, Door9b, 1, 1, false
		Load data/EG881/Doors/miscdoor9bside.jpg, Door9bSide, 1, 1, false #EG881

		#Service doors
		Load data/servicedoor1.jpg, ServiceDoor1, -1, 1, false
		LoadCropped data/servicedoor1.jpg, ServiceDoor1Side, 0, 0, 10, 256, 1, 1, false
		Load data/EG881/Doors/servicedoor2_graynovents.jpg, ServiceDoor2, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2back_graynovents.jpg, ServiceDoor2B, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2_graynovents_side.jpg, ServiceDoor2Side, 1, 1, false #EG881
		Load data/servicedoor3.jpg, ServiceDoor3, 1, 1, false
		LoadCropped data/servicedoor3.jpg, ServiceDoor3Side, 236, 0, 20, 512, 1, 1, false

		#Black entrance doors
		Load data/EG881/miscdoor8_blacktrans.png, BLKEntDoorInt, 1, 1, false #EG881
		LoadAlphaBlend data/EG881/miscdoor8_blacktranstinted.jpg, data/EG881/miscdoor8_blacktranspec.png, data/sky3c.jpg, BLKEntDoorExt, true, 1, 1, false #EG881
		LoadCropped data/EG881/miscdoor8_blacktrans.png, BLKEntDoorSide, 236, 0, 20, 512, 1, 1, false #EG881

	########################################

	#Stairwells
	Load data/EG881/cem_tr2b.jpg, Stairs, 0.075225, 0.0625 #EG881
	Load data/EG881/cem_tr2b.jpg, StairsFloor, 0.075225, 0.0625 #EG881
	Load data/stucco1.jpg, StairsWall, 0.25, 0.25
	Load data/stucco3.jpg, StairsWall2, 0.25, 0.25
	Load data/EG881/cem_tr2b.jpg, StairsCeiling, 0.075225, 0.0625 #EG881

	########################################

	#Shafts
	Load data/EG881/cem_tr2b.jpg, ShaftIntWall, 0.075225, 0.0625 #EG881

	#Elevators
		#Main components
		Load data/metal1.jpg, ElevExt, 0.25, 0.25
		Load data/counterweight.png, CWeight, 1, 1, false
		Load data/counterweight_frame.png, CWeight_Frame, 1, 1, false
		Load data/ropes.png, Ropes, 1, 1, false
		Load data/metal080.jpg, ElevRail, 0.5, 0.5
		Load data/metal1.jpg, ElevRailSide, 0.25, 0.25
		Load data/EG881/elevatordoortrackx.jpg, ElevDoorTrackX, 1, 1, false #EG881
		Load data/EG881/elevatordoortrackz.jpg, ElevDoorTrackZ, 1, 1, false #EG881
		Load data/panel-back3.jpg, ElevPanel, 1, 1, false
		#Load data/MT/panel-back2.gif, ElevPanel2, 1, 1, false
		Load data/panel-back3.jpg, ElevExtPanels, 1, 1, false
		Load data/Melvin/SDT_Signs/SDTCallPanel.png, ElevExtPanels-Alt, 1, 1, false

		#Main elevators
		Load data/Carpet0013b_S.jpg, MainElevFloor, 0.2, 0.2
		Load data/EG881/stainlesssteel2.jpg, MainElevCeiling, 1, 1, false #EG881
		Load data/EG881/light1x.jpg, MainElevCeilingLight, 2, 1, false #EG881
		Load data/EG881/stainlesssteel4.jpg, MainElevCeilingTrim, 1, 1, false #EG881
		Load data/EG881/blackdoortrim.jpg, MainElevWall, 1, 1, false #EG881
		Load data/checker23.jpg, MainElevWallPanel, 0.5, 0.5
		Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, MainElevPanelMain, 1, 1, false
		Load data/Melvin/OTIS67_Darkaluminium_cropped.jpg, MainElevPanelTop, 1, 0.125, false
		Load data/Melvin/OTIS67_Darkaluminium_door.jpg, MainElevDoors, 1, 1, false
		Load data/Melvin/OTIS67_Darkaluminium_door.jpg, MainElevDoors2, -1, 1, false

		#Service elevators
		Load data/metal072.jpg, ServiceElevFloor, 0.2, 0.2
		Load data/metal080.jpg, ServiceElevCeiling, 0.5, 0.5
		Load data/EG881/Lights/squarelight1_white.jpg, ServiceElevCeilingLight, 1, 1, false #EG881
		Load data/stucco22d.jpg, ServiceElevWall, 0.25, 0.25
		Load data/EG881/stainlesssteel2.jpg, ServiceElevWallTrim, 1, 1, false #EG881
		Load data/panel-back3.jpg, ServiceElevPanelMain, 1, 1, false #EG881
		Load data/panel-back3.jpg, ServiceElevPanelTop, 1, 0.125, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, ServiceElevDoors, -1, 1, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, ServiceElevDoors2, 1, 1, false #EG881

	####################

	#Mitsubishi MTS fixtures
	#Hall buttons
	Load data/Mitsubishi_MTS/CallButtons/ButtonUp.png, ButtonUp, 1, 1
	Load data/Mitsubishi_MTS/CallButtons/ButtonUpLit.png, ButtonUpLit, 1, 1
	Load data/Mitsubishi_MTS/CallButtons/ButtonDown.png, ButtonDown, 1, 1
	Load data/Mitsubishi_MTS/CallButtons/ButtonDownLit.png, ButtonDownLit, 1, 1

	#Car buttons
	Load data/Mitsubishi_MTS/256x256/ButtonBlank.jpg, Button, 1, 1
	Load data/Mitsubishi_MTS/256x256/ButtonBlank.jpg, ButtonLit, 1, 1
	AddTextRange 1, 18, Button, Button%number%, nimbus_sans.ttf, 110, %number%, -1, -1, -1, -1, center, center, 81, 79, 80
	AddTextRange 1, 18, ButtonLit, ButtonLit%number%, nimbus_sans.ttf, 110, %number%, -1, -1, -1, -1, center, center, 254, 249, 183

	AddText Button, ButtonBTemp, nimbus_sans.ttf, 110, B, 0, 0, 156, 256, center, center, 81, 79, 80
	AddText ButtonLit, ButtonLitBTemp, nimbus_sans.ttf, 110, B, 0, 0, 156, 256, center, center, 254, 249, 183
	AddTextRange 1, 3, ButtonBTemp, ButtonB%number%, nimbus_sans.ttf, 85, %number%, 156, 0, 256, 256, center, center, 81, 79, 80
	AddTextRange 1, 3, ButtonLitBTemp, ButtonLitB%number%, nimbus_sans.ttf, 85, %number%, 156, 0, 256, 256, center, center, 254, 249, 183

	Load data/Mitsubishi_MTS/256x256/ButtonOpen.jpg, ButtonOpen, 1, 1
	Load data/Mitsubishi_MTS/256x256/ButtonClose.jpg, ButtonClose, 1, 1
	Load data/Mitsubishi_MTS/256x256/ButtonStop-Running.jpg, ButtonStop-Run, 1, 1
	Load data/Mitsubishi_MTS/256x256/ButtonStop-Stopped.jpg, ButtonStop-Stop, 1, 1
	Load data/Mitsubishi_MTS/256x256/ButtonAlarm.jpg, ButtonAlarm, 1, 1

	#Car indicators
	Load data/black.jpg, IndBack, 1, 1, false
	Load data/black.jpg, Ind_Background, 1, 1, false
	AddTextRange 1, 18, IndBack, Ind_Button%number%, Mitsubishi-Modern-V2.ttf, 60, %number%, -1, -1, -1, -1, center, center, 255, 97, 0, false #Test Tower
	AddTextRange 1, 3, IndBack, Ind_ButtonB%number%, Mitsubishi-Modern-V2.ttf, 60, B%number%, -1, -1, -1, -1, center, center, 255, 97, 0, false #Test Tower
	Load data/Mitsubishi_Modern/Indicator_Set/FloorArrow2.jpg, ArrowUnlit, 1, 1, false #Test Tower
	Load data/Mitsubishi_Modern/Indicator_Set/FloorArrowUp2.jpg, ArrowUpLit, 1, 1, false #Test Tower
	Load data/Mitsubishi_Modern/Indicator_Set/FloorArrowDown2.jpg, ArrowDownLit, 1, 1, false #Test Tower

	#Hall indicators
	Load data/Mitsubishi_Modern/Indicator_Set/LanternBulb.gif, LanternUp, 1, 1, false #Test Tower
	LoadAnimated data/Mitsubishi_Modern/Indicator_Set/LanternBulbLit.gif, data/Mitsubishi_Modern/Indicator_Set/LanternBulb.gif, data/Mitsubishi_Modern/Indicator_Set/LanternBulbLit.gif, LanternUpLit, 0.5, 1, 1, false #Test Tower
	Load data/Mitsubishi_Modern/Indicator_Set/LanternBulb.gif, LanternDown, 1, -1, false #Test Tower
	LoadAnimated data/Mitsubishi_Modern/Indicator_Set/LanternBulbLit.gif, data/Mitsubishi_Modern/Indicator_Set/LanternBulb.gif, data/Mitsubishi_Modern/Indicator_Set/LanternBulbLit.gif, LanternDownLit, 0.5, 1, -1, false #Test Tower
	AddTextRange 1, 18, Black2, OutsideInd_Button%number%, Mitsubishi-Modern-V2.ttf, 110, %number%, -1, -1, -1, -1, center, center, 255, 97, 0, false #Test Tower
	AddTextRange 1, 3, Black2, OutsideInd_ButtonB%number%, Mitsubishi-Modern-V2.ttf, 110, B%number%, -1, -1, -1, -1, center, center, 255, 97, 0, false #Test Tower

	########################################

	#Miscellaneous
		#1. Restrooms
		Load data/text16.jpg, WoodSign2, 1, 1, false
		Load data/signs_mod/Restroom1A.png, Sign_Restroom_Brown, 1, 1, false
		Load data/signs_mod/Restroom1.png, Sign_Restroom_Gray, 1, 1, false
		Load data/signs_mod/Sign_Restroom_1A.png, Sign_RestroomW_Brown, 1, 1, false
		Load data/signs_mod/Sign_Restroom_3A.png, Sign_RestroomM_Brown, 1, 1, false
		Load data/signs_mod/Sign_Restroom_1.png, Sign_RestroomW_Gray, 1, 1, false
		Load data/signs_mod/Sign_Restroom_3.png, Sign_RestroomM_Gray, 1, 1, false

		#2. Exit signs
		Load data/panel-back3.jpg, StainlessSteel, 1, 1, false
		Load data/Melvin/ExitSignDown.png, ExitSign, 1, 1, false
		Load data/Melvin/ExitSignUp.png, ExitSignUp, 1, 1, false
		Load data/Melvin/ExitSignLeft.png, ExitSignLeft, 1, 1, false
		Load data/Melvin/ExitSignRight.png, ExitSignRight, 1, 1, false
		#Load data/Melvin/ExitSignBoth.png, ExitSignBoth, 1, 1, false

		#4. Floor signs (Transparent or blank)
		AddTextRange 1, 3, Blank, BlankSignB%number%, nimbus_sans_bold.ttf, 72, B%number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddTextRange 1, 18, Blank, BlankSign%number%, nimbus_sans_bold.ttf, 72, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddText Blank, BlankSignR, nimbus_sans_bold.ttf, 72, R, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#5. Signs at elevator lobbies
		Load data/signs_mod/Elev3.png, Sign_Elev, 1, 1, false
		Load data/signs_mod/Elev4A.png, Sign_ServiceElevA, 1, 1, false
		Load data/signs_mod/Elev4B.png, Sign_ServiceElevB, 1, 1, false

			#Floor signs
			Load data/graygradient.jpg, ELFlrSignBack, 1, 1, false
			AddTextRange 1, 3, ELFlrSignBack, ELFlrSignB%number%, nimbus_sans_bold_condensed.ttf, 130, B%number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
			AddTextRange 1, 17, ELFlrSignBack, ELFlrSign%number%, nimbus_sans_bold_condensed.ttf, 130, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#6. Elevator number signs (Inside)
		Load data/Melvin/transparent128.png, IN_ElevNoBack, 1, 1, false
		AddTextRange 1, 6, IN_ElevNoBack, IN_ElevNo%number%, nimbus_sans_bold.ttf, 55, %number%, -1, -1, -1, -1, center, center, 0, 255, 255, false
		AddTextRange 7, 10, IN_ElevNoBack, IN_ElevNo%number%, nimbus_sans_bold.ttf, 55, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#7. Access control
		#Load data/EG881/proxscan1.jpg, ProxScan, 1, 1, false #EG881
		#Load data/EG881/Miscellaneous/HIDbadgescannergreen.jpg, HIDScanGreen, 1, 1, false #EG881
		#Load data/EG881/Miscellaneous/HIDbadgescannerred.jpg, HIDScanRed, 1, 1, false #EG881

		#8. Other signs
		Load data/Melvin/DRH/09-2024/Dir_SR_W.png, WestDir, 1, 1, false #Floor directory
		Load data/Melvin/DRH/09-2024/Dir_SR_W_3F.png, WestDir_3F, 1, 1, false
		Load data/Melvin/DRH/09-2024/Dir_SR_E.png, EastDir, 1, 1, false
		Load data/signs_mod/Parking_Blue.png, ParkingSign, 1, 1, false
<EndTextures>

############################################################

#Floor Assignments
<Floor -1>
	ID = B1
	Name = Basement Level 1
	Type = Hospital
	Height = 10.5
	InterfloorHeight = 1.5
	Altitude = -13.5
<EndFloor>

<Floors -2 to -3>
	ID = B(0 - %floor%)
	Name = Basement Level (0 - %floor%)
	Type = Hospital
	Height = 10.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor -4>
	ID = X
	Name = Shaft Pit
	Type = Basement
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 0>
	ID = 1
	Name = Level %floorid%
	Type = Lobby
	Height = 22
	InterfloorHeight = 1.5
	Altitude = -1.5
<EndFloor>

<Floor 1>
	ID = %floor% + 1
	Name = Level %floorid%
	Type = Hospital
	Height = 10.5
	InterfloorHeight = 2
<EndFloor>

<Floors 2 to 8>
	ID = %floor% + 1
	Name = Level %floorid%
	Type = Hospital
	Height = 10.5
	InterfloorHeight = 1.5
<EndFloor>

<Floors 9 to 16>
	ID = %floor% + 1
	Name = Ward Level %floorid%
	Type = Hospital
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 17>
	ID = %floor% + 1
	Name = Mechanical
	Type = Service
	Height = 18.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 18>
	ID = R
	Name = Roof
	Type = Roof
	Height = 9.5
	InterfloorHeight = 1.5
<EndFloor>


##--------------------------------------------------------------------------------##

#Common values
Set 1 = 100 #Building Dimension X
Set 2 = 45 #Building Dimension Z
Set ExtraSouth = 40 #Extra south dimensions for levels B3 to 3
if[%floor% > 2] Set ExtraSouth = 0 #Extra south dimensions for levels B3 to 3

#Landscape
	#Perimeter floors
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, FrontFloor, Sidewalk, 0, -%1% - 6, -%2% - %ExtraSouth% - 5, %1% + 6, -%2% - %ExtraSouth%, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, LeftFloor, Sidewalk, 0, -%1% - 6, -%2% - %ExtraSouth%, -%1%, %2%, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, RightFloor, Sidewalk, 0, %1%, -%2% - %ExtraSouth% - 5, %1% + 6, %2%, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, BackFloor, Grass, 0, -%1% - 6, %2%, %1% + 6, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	

##--------------------------------------------------------------------------------##

#Set texture prefixes/suffixes
<Function TexPrefix>
	#Main prefixes
	Set 5 = LH
	if[%floor% > 2 & %floor% < 9] Set 5 = UH
	if[%floor% > 8 & %floor% < 17] Set 5 = Ward
	if[%floor% = 17] Set 5 = Service

		#Add "ExtraSouth" value for floor section.
		Set ExtraSouth = 40 #Extra south dimensions for levels B3 to 3
		if[%floor% > 2] Set ExtraSouth = 0 #Extra south dimensions for levels B3 to 3
		
		#Suffixes for walls inside patient rooms
		Set PTSuffix = Wall
		if[%floor% > 8 & %floor% < 17] Set PTSuffix = Wall_Int
<EndFunction>

##--------------------------------------------------------------------------------##

#Exterior & interior
#All floors
<Floors -3 to 17>
	TexPrefix() #Set texture prefix/suffix

	#Floors
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor %floorid%F_Floor, %5%Floor, 0.5, -%1%, -%2% - %ExtraSouth%, %1%, %2%, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Ceilings
	FloorOrientation = bottom
	DrawWalls = true, false, false, false, false, false
	AddFloor %floorid%F_Ceiling, %5%Ceiling, 0.5, -%1%, -%2% - %ExtraSouth%, %1%, %2%, %fullheight%, %fullheight%, 0, 0, true
<EndFloors>

<Floors -3 to -1>
	#Railings/walls
	TexPrefix() #Set texture prefix/suffix
	Set WallHeight = %height%
	Set WallOffset = 0

		#Set DrawWalls for Front/Left sides
		DrawWalls = false, true, false, false, false, false

		WallOrientation = left
		AddWall FrontWall, %5%Wall, 0.5, -%1%, -%2% - %ExtraSouth%, %1%, -%2% - %ExtraSouth%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, false
		AddWall LeftWall, %5%Wall, 0.5, -%1%, -%2% - %ExtraSouth%, -%1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, false

		#Set DrawWalls for Right/Back sides
		DrawWalls = true, false, false, false, false, false

		WallOrientation = right
		AddWall RightWall, %5%Wall, 0.5, %1%, -%2% - %ExtraSouth%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, false
		AddWall BackWall, %5%Wall, 0.5, -%1%, %2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, false
<EndFloors>

<Floors 0 to 16>
	TexPrefix() #Set texture prefix/suffix

	#Windows
	Set MyIFloorType = 2
	if[%floor% = 0] Set MyIFloorType = 3
	Set WinPrefix = Main

	SetAutoSize = true, false
	AddCustomWindows(FrontWin, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, -%2% - %ExtraSouth%, %1%, -%2% - %ExtraSouth%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	AddCustomWindows(LeftWin, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, -%2% - %ExtraSouth%, -%1%, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	AddCustomWindows(RightWin, %WinPrefix%WindowsInt, %WinPrefix%Windows, %1%, -%2% - %ExtraSouth%, %1%, %2%, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)
	AddCustomWindows(BackWin, %WinPrefix%WindowsInt, %WinPrefix%Windows, -%1%, %2%, %1%, %2%, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)
	SetAutoSize = true, true
<EndFloors>

#Cut windows for "Lobby windows"
	#South side
	Cut external, -20, Floor(0).Base, -%2% - 40 - 0.1, 0, Floor(1).Altitude, -%2% - 40 + 0.1, true, false
	
<Floor 0>
	TexPrefix() #Set wall textures

	#Lobby windows
	Set WinPrefix = Lobby
	Set winheight = %height%

		#Front
		SetAutoSize = true, false
		AddCustomWindows(FrontLobbyWin1, %WinPrefix%Windows, %WinPrefix%WindowsInt, -20, -%2% - %ExtraSouth%, 20, -%2% - %ExtraSouth%, %winheight%, 0, 0, 3, ExtFacade, %5%Wall)
		SetAutoSize = true, true

	#Entrance doors
	Set DTex = BLKEntDoorInt
	Set DTex_Ext = BLKEntDoorExt
	#Set DSTex = %DTex%Side
	Set DSTex = BLKEntDoorSide
	
		#SOUTH DOORS
		#Door set 1A
		Set Door_MainX = -15
		Set Door_MainZ = -%2% - %ExtraSouth%

		#WallOrientation = left
		#TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		#AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		#TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		#AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0

		#Door set 1B
		Set Door_MainX = -5
		Set Door_MainZ = -%2% - %ExtraSouth%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0

		#Door set 2A
		Set Door_MainX = 5
		Set Door_MainZ = -%2% - %ExtraSouth%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0

		#Door set 2B
		Set Door_MainX = 15
		Set Door_MainZ = -%2% - %ExtraSouth%

		#WallOrientation = left
		#TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		#AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		#TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		#AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
<EndFloor>

<Floor 3>
	#4th floor setback
	Set ExtraSouth = 40 #Override for setback

	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Setback, RoofFloor2, %interfloorheight%, -%1%, -%2% - %ExtraSouth%, %1%, -%2%, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Railings
	Set WallHeight = %interfloorheight% + 4.5
	Set WallOffset = 0

	WallOrientation = left
	DrawWalls = true, true, false, false, true, false
	TextureOverride ExtFacade, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner
	AddWall FrontRailing, ExtFacade, 0.5, -%1% + 0.5, -%2% - %ExtraSouth%, %1% - 0.5, -%2% - %ExtraSouth%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	DrawWalls = true, true, true, false, true, false
	TextureOverride ExtFacade, ExtFacade_Inner, ExtFacade, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner
	AddWall LeftRailing, ExtFacade, 0.5, -%1%, -%2% - %ExtraSouth%, -%1%, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	WallOrientation = right
	TextureOverride ExtFacade_Inner, ExtFacade, ExtFacade, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner
	AddWall RightRailing, ExtFacade, 0.5, %1%, -%2% - %ExtraSouth%, %1%, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloor>

<Floor 17>
	#Perimeter walls
	TexPrefix() #Set texture prefix/suffix
	Set WallHeight = %fullheight%
	Set WallOffset = 0

		#Set DrawWalls for all sides
		DrawWalls = true, true, false, false, false, false

		WallOrientation = left
		TextureOverride ExtFacade, %5%Wall2, ExtFacade, ExtFacade, ExtFacade, ExtFacade
		AddWall FrontWall, %5%Wall2, 0.5, -%1%, -%2%, %1%, -%2% - %ExtraSouth%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
		TextureOverride ExtFacade, %5%Wall2, ExtFacade, ExtFacade, ExtFacade, ExtFacade
		AddWall LeftWall, %5%Wall2, 0.5, -%1%, -%2%, -%1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		WallOrientation = right
		TextureOverride %5%Wall2, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade
		AddWall RightWall, %5%Wall2, 0.5, %1%, -%2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
		TextureOverride %5%Wall2, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade
		AddWall BackWall, %5%Wall2, 0.5, -%1%, %2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloor>

#Support columns
<Floors -3 to 17>
	#Set Values
	TexPrefix() #Set wall texture
	Set DistanceX = 40 #Distance between supports X
	Set DistanceZ = 30 #Distance between supports Z
	Set SupportHeight = %fullheight%
	Set SupportOffset = -%interfloorheight%

	#Corner Supports
	CreateWallBox2 External, SupportNW, %5%%PTSuffix%, -%1% + 1.25, %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportNE, %5%%PTSuffix%, %1% - 1.25, %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSW, %5%%PTSuffix%, -%1% + 1.25, -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSE, %5%%PTSuffix%, %1% - 1.25, -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#North Supports
	CreateWallBox2 External, SupportN1, %5%%PTSuffix%, -%1% + (%DistanceX% * 1), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN2, %5%%PTSuffix%, -%1% + (%DistanceX% * 2), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN3, %5%%PTSuffix%, -%1% + (%DistanceX% * 3), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN4, %5%%PTSuffix%, -%1% + (%DistanceX% * 4), %2% - 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#South Supports
	CreateWallBox2 External, SupportS1, %5%%PTSuffix%, -%1% + (%DistanceX% * 1), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS2, %5%%PTSuffix%, -%1% + (%DistanceX% * 2), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS3, %5%%PTSuffix%, -%1% + (%DistanceX% * 3), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS4, %5%%PTSuffix%, -%1% + (%DistanceX% * 4), -%2% + 1.25, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#West Supports
	CreateWallBox2 External, SupportW1, %5%%PTSuffix%, -%1% + 1.25, -%2% + (%DistanceZ% * 1), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportW2, %5%%PTSuffix%, -%1% + 1.25, -%2% + (%DistanceZ% * 2), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false

	#East Supports
	CreateWallBox2 External, SupportE1, %5%%PTSuffix%, %1% - 1.25, -%2% + (%DistanceZ% * 1), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportE2, %5%%PTSuffix%, %1% - 1.25, -%2% + (%DistanceZ% * 2), 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
<EndFloors>

#Extra suppports (Levels B3-4)
<Floors -3 to 3>
	#Set Values
	TexPrefix() #Set wall texture
	Set DistanceX = 40 #Distance between supports X
	Set DistanceZ = 30 #Distance between supports Z
	Set SupportHeight = %fullheight%
	Set SupportOffset = -%interfloorheight%


	#South Supports
	Set SupportZ = -%2% - %ExtraSouth% + 1.25

	CreateWallBox2 External, SupportS1, %5%Wall, -%1% + 1.25, %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS1, %5%Wall, -%1% + (%DistanceX% * 1), %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS2, %5%Wall, -%1% + (%DistanceX% * 2), %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS3, %5%Wall, -%1% + (%DistanceX% * 3), %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS4, %5%Wall, -%1% + (%DistanceX% * 4), %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS4, %5%Wall, %1% - 1.25, %SupportZ%, 2, 2, %SupportHeight%, %SupportOffset%, 0, 0, false, true, false, false
<EndFloors>

#Roof
<Floor 18>
	#Roof floor
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Roof, RoofFloor, 0.5, -%1%, -%2%, %1%, %2%, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Roof railings
	Set WallHeight = %interfloorheight% + 4.5
	Set WallOffset = 0

	WallOrientation = Left
	DrawWalls = true, true, true, true, true, false
	TextureOverride ExtFacade, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner
	AddWall FrontRail, ExtFacade, 0.5, -%1% + 0.5, -%2%, %1% - 0.5, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	TextureOverride ExtFacade, ExtFacade_Inner, ExtFacade, ExtFacade, ExtFacade_Inner, ExtFacade_Inner
	AddWall LeftRail, ExtFacade, 0.5, -%1%, -%2%, -%1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

	WallOrientation = Right
	TextureOverride ExtFacade_Inner, ExtFacade, ExtFacade, ExtFacade, ExtFacade_Inner, ExtFacade_Inner
	AddWall RightRail, ExtFacade, 0.5, %1%, -%2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	TextureOverride ExtFacade_Inner, ExtFacade, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner, ExtFacade_Inner
	AddWall BackRail, ExtFacade, 0.5, -%1% + 0.5, %2%, %1% - 0.5, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloor>

##--------------------------------------------------------------------------------##

#EXTERNAL SHAFT & STAIRWELL WALLS
<Floors -3 to 17>
	TexPrefix() #Set texture prefixes
	
	#Stairwells
		#West stairwell
		CreateShaftWall(%5%%PTSuffix%, %5%%PTSuffix%, %5%%PTSuffix%, %5%Wall, ExtFacade, ExtFacade, 0.5, -65, -17.5, 5, 12.5, WestST, %height%, 0, true, true, true, true, false, false, false)

		#East stairwell
		CreateShaftWall(%5%%PTSuffix%, %5%%PTSuffix%, %5%%PTSuffix%, %5%Wall, ExtFacade, ExtFacade, 0.5, 65, -17.5, 5, 12.5, WestST, %height%, 0, true, true, true, true, false, false, false)
<EndFloors>

##--------------------------------------------------------------------------------##

#STAIRWELLS
<Function StairwellSets>
	#Set Values
	Set NumSteps = 12 #Total: 24 steps
	if[%floor% > 8] Set NumSteps = 10 #Total: 20 steps

	#Extra sets at transition points
	Set NumSteps2 = %NumSteps%
	if[%floor% = 9] Set NumSteps2 = 12

	#Floors with double stairs (40/48 steps instead of the usual 20/24)
	Set DblStairs = false
	if[%floor% = 0 | %floor% = 17] Set DblStairs = true
	if[%floor% = 9 | %floor% = 10 | %floor% = 11 | %floor% = 26] Set DblStairs = true
<EndFunction>

#West Stairwell
CreateStairwell 1, -65, -17.5, -3, 18
CutStairwell 1, -4.5, -12, 4.5, 12, 0, 5

<Floors -3 to 18>
	StairwellSets() #Determine stairs' size

	Set 21 = -65 #Center X
	Set 22 = -17.5 #Center Z
	Set 23 = 4.5 #Cut X
	Set 24 = 12 #Cut Z
	Set 25 = 1 #Stairwell Number
	Set 26 = 0.5 #Riser Size

	#Walls
	WallOrientation = right
	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, Stair%25%Front, StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Left, StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	WallOrientation = left
	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, Stair%25%Right, StairsWall, 0.5, %23%, -%24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Back, StairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	WallOrientation = center
	DrawWalls = true, true, true, true, false, true
	AddStairsWall %25%, Stair%25%Center, StairsWall2, 1, 0, -%24% + 5, 0, -%24% + 5 + %NumSteps%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	#Railing
	WallOrientation = right
	DrawWalls = true, true, true, true, true, true
	if[%floor% = 18] AddStairsWall %25%, Stair%25%Rail, StairsWall2, 0.5, -%23%, -%24% + 5 + %NumSteps%, -%23% + 4, -%24% + 5 + %NumSteps%, 0.5 + 3.5, 0.5 + 3.5, -0.5, -0.5, 0, 0

	#Ceiling
	FloorOrientation = top
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 18] AddStairsFloor %25%, Stairs%25%Ceiling, StairsCeiling, 0.5, -%23%, -%24%, %23%, %24%, %height%, %height%, false, false, 0, 0

	#Stairs
	Set NumStepsU2 = %NumSteps% * 2
	Set NumStepsU3 = %NumSteps% * 3

	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 18] OP_OrthoStair(%25%, ST%25%_LowerStairs, Stairs, -%23%, -%23% + 4, back, -%24% + 5 + %NumSteps%, %NumSteps%, 1, %26%, 0, 0.5)
	if[%floor% < 18] OP_OrthoStair(%25%, ST%25%_UpperStairs, Stairs, %23% - 4, %23%, front, -%24% + 5, %NumSteps%, 1, %26%, %26% * %NumSteps%, 0.5)
	if[%DblStairs% = true] OP_OrthoStair(%25%, ST%25%_LowerStairsEX, Stairs, -%23%, -%23% + 4, back, -%24% + 5 + %NumSteps%, %NumSteps%, 1, %26%, %26% * %NumStepsU2%, 0.5)
	if[%DblStairs% = true] OP_OrthoStair(%25%, ST%25%_UpperStairsEX, Stairs, %23% - 4, %23%, front, -%24% + 5, %NumSteps%, 1, %26%, %26% * %NumStepsU3%, 0.5)

	#Landings
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	if[%floor% = -3] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23%, %24%, 0, 0, false, false, 0, 0
	if[%floor% > -3] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24% + 5 + %NumSteps%, %23%, %24%, 0, 0, false, false, 0, 0
	#if[%floor% = 9] AddStairsFloor %25%, Stairs%25%Transition, StairsFloor, 0.5, %23% - 4, -%24% + 5 + %NumSteps%, %23%, -%24% + 5 + %NumSteps2%, 0, 0, false, false, 0, 0
	if[%floor% < 18] AddStairsFloor %25%, Stairs%25%LandingUpper, StairsFloor, 0.5, -%23%, -%24%, %23%, -%24% + 5, %26% * %NumSteps%, %26% * %NumSteps%, false, false, 0, 0
	if[%DblStairs% = true] AddStairsFloor %25%, Stairs%25%LandingLowerEX, StairsFloor, 0.5, -%23%, -%24% + 5 + %NumSteps%, %23%, %24%, %26% * %NumStepsU2%, %26% * %NumStepsU2%, false, false, 0, 0
	if[%DblStairs% = true] AddStairsFloor %25%, Stairs%25%LandingUpperEX, StairsFloor, 0.5, -%23%, -%24%, %23%, -%24% + 5, %26% * %NumStepsU3%, %26% * %NumStepsU3%, false, false, 0, 0

	#Floor Signs
	Set Suffix = %floorid%
	Set SignCenterPoint = 0

	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, ST%25%FloorSign, BlankSign%Suffix%, 0, %SignCenterPoint% - 0.75, %24% - 0.001, %SignCenterPoint% + 0.75, %24% - 0.001, 1.5, 1.5, 3.75, 3.75, 0, 0

	#Doors
	WallOrientation = left
	TextureOverride ServiceDoor2, ServiceDoor2B, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	if[%floor% ! 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 5, 75, %23% - 2, %24%, 3, 7, 0, 0, 0
	WallOrientation = right
	TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	if[%floor% = 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 7, 75, %23% - 2, %24% + 0.5, 3, 7, 0, 0, 0

		#Manual filler walls for some floors
		WallOrientation = left
		if[%floor% = 18] AddFillerWalls ServiceDoor2Side, 0.5, %21% + %23% - 2, %22% + %24%, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true

	#Exit Signs
	Set SignTex = ExitSign
	if[%floor% < 0] Set SignTex = ExitSignUp

	if[%floor% ! 0] AddBoxSign(Stair%25%_SignMain, StainlessSteel, %SignTex%, StainlessSteel, 0.1, true, true, true, %21% + %23% - 2, %22% + %24% + 0.55, 1.8, 0.9, %interfloorheight% + 7.2, true)
	if[%floor% = 0] AddBoxSign(Stair%25%_SignGround, ExitSign, StainlessSteel, StainlessSteel, 0.1, true, true, true, %21% + %23% - 2, %22% + %24% - 0.05, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloors>