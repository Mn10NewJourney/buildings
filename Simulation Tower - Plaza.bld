#Simulation Tower - Plaza
#Copyright (c) 2024-Present - Mn10

#Start date: 28 September 2024
#Major detail finish date: 29 September 2024

#SCRIPTS TO BE INCLUDED
	#1. My new scripts
	<Include buildings/Mn10_Scripts/Deco2023_R1.txt>
	<Include buildings/Mn10_Scripts/Shafts_and_Elevators.txt>
	<Include buildings/Mn10_Scripts/Windows.txt>
	
	#2. ObjectPlus and SROC (credit to EG881)
	<Include ObjectPlus/Scripts/OP_Script2.txt> #EG881
	<Include SROC/Scripts/r1_script.txt> #EG881

	#3. My previous scripts
	<Include CustomObjects2018/Scripts/r1_script.txt>
	<Include Shaft Wall Creator/Scripts/r1_script.txt>
	<Include data/ACME Floor Indicator/Scripts/Indicators.txt>
	
############################################################

#Globals
<Globals>
	Name = Simulation Tower - Plaza
	Designer = Mn10 (Fixtures originally created by Vector)
	Location = 808 4th Street, Tranquility City
	CameraFloor = 0
	CameraPosition = -5, 0
	CameraRotation = 0, 270, 0
<EndGlobals>

#Textures
<Textures>
	#Commons
	Load data/black.jpg, Black, 1, 1, false
	Load data/Melvin/transparent128.png, Blank, 1, 1, false
	Load data/EG881/blackdoortrim.jpg, BlackTrim, 1, 1, false #EG881
	Load data/signs_mod/elevhallsign1a.png, FireSign, 1, 1, false #EG881

	#Landscapes
	Load data/downtown.jpg, Downtown, 1, 1
	#Load data/grndroad1mid.jpg, Road, 0.04, 0.04
	Load data/walkway.jpg, Sidewalk, 0.03125, 0.03125
	#Load data/Melvin/ABH_Tex/walkway-darker.jpg, SidewalkDarker, 0.03125, 0.03125
	Load data/ogrebullet/textures/grass.jpg, Grass, 0.01, 0.01

	########################################

	#Windows
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky1.jpg, MainWindows, true, 0.05, 1 #EG881
	Load data/windows11h_inside.gif, MainWindowsInt, 0.05, 1

	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky1.jpg, MallWindows, true, 0.025, 1 #EG881
	Load data/windows11h_inside.gif, MallWindowsInt, 0.025, 1

	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky1.jpg, MainWindowsB, true, 1, 1, false #EG881
	Load data/windows11h_inside.gif, MainWindowsIntB, 1, 1, false

	#Skylights
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3j.jpg, Skylight, true, 1, 1, false #EG881
	Load data/windows11h_inside.gif, SkylightInt, 1, 1, false

	########################################

	#External facades
	Load data/EG881/granite_0007e_black.jpg, ExtFacade, 0.075, 0.075, true #EG881
	#Load data/EG881/cem_tr2b.jpg, RoofFloor, 0.075225, 0.0625 #EG881
	Load data/EG881/Concrete/smoothconcrete1.jpg, RoofFloor, 0.0625, 0.0625 #EG881

	#Main textures
	Load data/marb047.jpg, ShopFloor, 0.25, 0.25
	Load data/EG881/ceilingtile2.jpg, ShopCeiling, 0.125, 0.125 #EG881
	Load data/marble5c.jpg, ShopWall, 0.15, 0.15, true
	Load data/marb148.jpg, ShopWall2, 0.15, 0.15, true #EG881 #Railings
	Load data/Carpet0021b_S.jpg, ABCarpet, 0.2, 0.2 #Atrium base (AB)

		#Shaft doors
		Load data/EG881/lightaluminumdoor2.jpg, ShopShaftDoors, 1, 1, false #EG881
		Load data/EG881/lightaluminumdoor2.jpg, ShopShaftDoors2, -1, 1, false #EG881
		Load data/panel-back3.jpg, ShopShaftDoors_Trim, 1, 1, false

	#Drop ceilings
	Load data/EG881/Ceilings/ceilingtile4single.jpg, DropCeiling, 0.5, 0.5 #EG881 #Restrooms

	########################################

	#Doors
		#Main doors
		Load data/miscdoor6.jpg, Door6, 1, 1, false
		LoadCropped data/miscdoor6.jpg, Door6Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor8.jpg, Door8, 1, 1, false
		LoadCropped data/miscdoor8.jpg, Door8Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor88.gif, Door88, 1, 1, false
		LoadCropped data/miscdoor88.gif, Door88Side, 236, 0, 20, 512, 1, 1, false
		Load data/miscdoor9b.jpg, Door9b, 1, 1, false
		Load data/EG881/Doors/miscdoor9bside.jpg, Door9bSide, 1, 1, false #EG881

		#Service doors
		Load data/servicedoor1.jpg, ServiceDoor1, -1, 1, false
		LoadCropped data/servicedoor1.jpg, ServiceDoor1Side, 0, 0, 10, 256, 1, 1, false
		Load data/EG881/Doors/servicedoor2_graynovents.jpg, ServiceDoor2, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2back_graynovents.jpg, ServiceDoor2B, 1, 1, false #EG881
		Load data/EG881/Doors/servicedoor2_graynovents_side.jpg, ServiceDoor2Side, 1, 1, false #EG881
		Load data/servicedoor3.jpg, ServiceDoor3, 1, 1, false
		LoadCropped data/servicedoor3.jpg, ServiceDoor3Side, 236, 0, 20, 512, 1, 1, false

		#Black entrance doors
		Load data/EG881/miscdoor8_blacktrans.png, BLKEntDoorInt, 1, 1, false #EG881
		LoadAlphaBlend data/EG881/miscdoor8_blacktranstinted.jpg, data/EG881/miscdoor8_blacktranspec.png, data/sky1.jpg, BLKEntDoorExt, true, 1, 1, false #EG881
		LoadCropped data/EG881/miscdoor8_blacktrans.png, BLKEntDoorSide, 236, 0, 20, 512, 1, 1, false #EG881

	########################################

	#Stairwells
	Load data/EG881/cem_tr2b.jpg, Stairs, 0.075225, 0.0625 #EG881
	Load data/EG881/cem_tr2b.jpg, StairsFloor, 0.075225, 0.0625 #EG881
	Load data/stucco1.jpg, StairsWall, 0.25, 0.25
	Load data/stucco3.jpg, StairsWall2, 0.25, 0.25
	Load data/EG881/cem_tr2b.jpg, StairsCeiling, 0.075225, 0.0625 #EG881

	#Escalators
	Load data/panel-back3.jpg, SilverBalustrade, 1, 1, false
	#Load data/EG881_Otis/Walls_and_Panels/otisstainlesssteel1.jpg, SilverBalustrade2, 1, 1, false #EG881
	Load data/MT/blank.gif, GlassBalustrade, 1, 1, false

	########################################

	#Shafts
	Load data/EG881/cem_tr2b.jpg, ShaftIntWall, 0.075225, 0.0625 #EG881

		#Glass Elevator Shaft
		Load data/EG881/blackdoortrim.jpg, GLEShaftWall, 0.05, 0.05 #EG881
		#Load data/stucco5.jpg, GLEShaftWall, 0.145, 0.145
		Load data/stucco1b.jpg, GLETrack, 0.1, 0.1
		Load data/EG881/windows11h_insideblack.png, GLEPanel, 1, 1, false #EG881

	#Elevators
	#Main components
	Load data/metal1.jpg, ElevExt, 0.25, 0.25
	Load data/panel-back3.jpg, ElevExt2, 1, 1, false
	Load data/counterweight.png, CWeight, 1, 1, false
	Load data/counterweight_frame.png, CWeight_Frame, 1, 1, false
	Load data/ropes.png, Ropes, 1, 1, false
	Load data/metal080.jpg, ElevRail, 0.5, 0.5
	Load data/metal1.jpg, ElevRailSide, 0.25, 0.25
	Load data/EG881/elevatordoortrackx.jpg, ElevDoorTrackX, 1, 1, false #EG881
	Load data/EG881/elevatordoortrackz.jpg, ElevDoorTrackZ, 1, 1, false #EG881
	Load data/panel-back3.jpg, ElevPanel, 1, 1, false
	Load data/panel-back3.jpg, ElevExtPanels, 1, 1, false

		#Passenger elevators
		Load data/Carpet0013b_S.jpg, MainElevFloor, 0.2, 0.2
		Load data/EG881/stainlesssteel2.jpg, MainElevCeiling, 1, 1, false #EG881
		Load data/otis4_weavefloor.jpg, MainElevCeilingDeco, 0.5, 0.5, true
		Load data/EG881/Lights/squarelight1_white.jpg, MainElevCeilingLight, 1, 1, false #EG881
		Load data/otis67/OTIS67_EU_Button_Lighted.gif, MainElevCeilingDL, 1, 1, false
		Load data/EG881/stainlesssteel4.jpg, MainElevCeilingTrim, 1, 1, false #EG881 #Left/right
		Rotate MainElevCeilingTrim, 90
		Load data/EG881/stainlesssteel4.jpg, MainElevCeilingTrim2, 1, 1, false #EG881 #Front/back
		Load data/marble5.jpg, MainElevWall, 0.2, 0.2
		Load data/marb148.jpg, MainElevWallPanel, 0.2, 0.2
		Load data/glasselevator2b.gif, MainElevWallGlass, 0.5, 1, false
		Load data/panel-back3.jpg, MainElevPanelMain, 1, 1, false
		Load data/panel-back3.jpg, MainElevPanelTop, 1, 0.125, false
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, MainElevDoors, -1, 1, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, MainElevDoors2, 1, 1, false #EG881

		#Service elevator
		Load data/metal072.jpg, ServiceElevFloor, 0.25, 0.25
		Load data/panel-back3.jpg, ServiceElevCeiling, 1, -1, false
		Load data/EG881/Lights/squarelight1_white.jpg, ServiceElevCeilingLight, 1, 1, false #EG881
		Load data/EG881/blackdoortrim.jpg, ServiceElevWall, 1, 1, false #EG881
		Load data/metal080.jpg, ServiceElevWallPanel, 0.5, 0.5
		Load data/panel-back3.jpg, ServiceElevPanelMain, 1, 1, false
		Load data/panel-back3.jpg, ServiceElevPanelTop, 1, 0.125, false
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, ServiceElevDoors, -1, 1, false #EG881
		Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, ServiceElevDoors2, 1, 1, false #EG881

			#Shaft doors (Service)
			Load data/EG881_Otis/Doors/otisstnlsteeldoorleft.jpg, ServiceAShaftDoors, 1, 1, false #EG881
			Load data/EG881_Otis/Doors/otisstnlsteeldoorright.jpg, ServiceAShaftDoors2, 1, 1, false #EG881
			Load data/panel-back3.jpg, ServiceAShaftDoors_Trim, 1, 1, false

	####################

	#EPL Kone fixtures
		#Hall buttons
		Load data/Thailand_EPL_Kone/UpUnlit.png, ButtonUp, 1, 1, false
		Load data/Thailand_EPL_Kone/UpLit.png, ButtonUpLit, 1, 1, false
		Load data/Thailand_EPL_Kone/DownUnlit.png, ButtonDown, 1, 1, false
		Load data/Thailand_EPL_Kone/DownLit.png, ButtonDownLit, 1, 1, false

		#Car buttons
		Load data/Thailand_EPL_Kone/FloorUnlit.png, Button, 1, 1, false
		Load data/Thailand_EPL_Kone/FloorLit.png, ButtonLit, 1, 1, false
		Load data/Thailand_EPL_Kone/OpenUnlit.png, ButtonOpen, 1, 1, false
		Load data/Thailand_EPL_Kone/CloseUnlit.png, ButtonClose, 1, 1, false
		Load data/Thailand_EPL_Kone/Alarm.png, ButtonAlarm, 1, 1, false
		Load data/Thailand_EPL_Kone/Phone.png, ButtonPhone, 1, 1, false
		AddTextRange 1, 3, Button, Button%number%, nimbus_sans.ttf, 45, %number%, -1, -1, -1, -1, center, center, 42, 255, 0, false
		AddTextRange 1, 3, ButtonLit, ButtonLit%number%, nimbus_sans.ttf, 45, %number%, -1, -1, -1, -1, center, center, 42, 255, 0, false
	
		#External indicators
		Load data/Thailand_EPL_Kone/LanternUnlit.gif, LanternUp, 1, 1, false
		Load data/Thailand_EPL_Kone/LanternLitGreen.gif, LanternUpLit, 1, 1, false
		Load data/Thailand_EPL_Kone/LanternUnlit.gif, LanternDown, 1, -1, false
		Load data/Thailand_EPL_Kone/LanternLitRed.gif, LanternDownLit, 1, -1, false

		Load data/ACME Floor Indicator/Resized/ACME Floor Indicator.jpg, OutsideIndBack, 1, 1, false
		AddTextRange 1, 3, OutsideIndBack, OutsideInd_Button%number%, acme_led.ttf, 240, _%number%, -1, -1, -1, -1, center, center, 42, 255, 0

		#Internal indicators
		AddTextRange 1, 3, Black, Ind_Button%number%, fontgomery.ttf, 47, %number%, 20, 0, 128, 128, center, center, 42, 255, 0, false

	########################################

	#Signs
		#1. Floor signs (On door jambs)
		AddTextRange 1, 3, BlackTrim, Sign%number%, nimbus_sans_bold.ttf, 96, %number%, -1, -1, -1, -1, center, center, 255, 255, 255, false 

		#2. Floor signs (Blank)
		AddTextRange 1, 3, Blank, BlankSign%number%, nimbus_sans_bold.ttf, 72, %number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
		AddText Blank, BlankSignR, nimbus_sans_bold.ttf, 72, R, -1, -1, -1, -1, center, center, 0, 0, 0, false

		#3. Restrooms
		Load data/text16.jpg, WoodSign2, 1, 1, false
		Load data/signs_mod/Restroom1A.png, Sign_Restroom_Brown, 1, 1, false
		#Load data/signs_mod/Restroom1.png, Sign_Restroom_Gray, 1, 1, false
		Load data/signs_mod/Sign_Restroom_1A.png, Sign_RestroomW_Brown, 1, 1, false
		Load data/signs_mod/Sign_Restroom_3A.png, Sign_RestroomM_Brown, 1, 1, false
		#Load data/signs_mod/Sign_Restroom_1.png, Sign_RestroomW_Gray, 1, 1, false
		#Load data/signs_mod/Sign_Restroom_3.png, Sign_RestroomM_Gray, 1, 1, false

		#4. Exit signs
		Load data/panel-back3.jpg, StainlessSteel, 1, 1, false
		Load data/Melvin/ExitSignDown.png, ExitSign, 1, 1, false
		Load data/Melvin/ExitSignUp.png, ExitSignUp, 1, 1, false
		Load data/Melvin/ExitSignLeft.png, ExitSignLeft, 1, 1, false
		Load data/Melvin/ExitSignRight.png, ExitSignRight, 1, 1, false
		#Load data/Melvin/ExitSignBoth.png, ExitSignBoth, 1, 1, false

		#5. Elevator number signs (Inside)
		Load data/Melvin/transparent128.png, IN_ElevNoBack, 1, 1, false
		AddTextRange 1, 3, IN_ElevNoBack, IN_ElevNoPL%number%, nimbus_sans_bold.ttf, 50, PL%number%, -1, -1, -1, -1, center, center, 0, 0, 0, false
<EndTextures>

############################################################

#Floor Assignments
<Floor -1>
	ID = %floor%
	Name = Shaft Pit
	Type = Basement
	Height = 8.5
	InterfloorHeight = 1.5
	Altitude = -11.5
<EndFloor>

<Floor 0>
	ID = %floor% + 1
	Name = Plaza Level %floorid%
	Type = Recreation
	Height = 12
	InterfloorHeight = 1.5
	Altitude = -1.5

	#Merge floors
	Group = 1, 2
<EndFloor>

<Floors 1 to 2>
	ID = %floor% + 1
	Name = Plaza Level %floorid%
	Type = Recreation
	Height = 12
	if[%floor% = 2] Height = 12.5
	InterfloorHeight = 2

		#Merge floors
		if[%floor% = 1] Group = 0, 2
		if[%floor% = 2] Group = 0 - 1
<EndFloors>

<Floor 3>
	ID = R
	Name = Roof
	Type = Roof
	Height = 9.5
	InterfloorHeight = 1.5
<EndFloor>

##--------------------------------------------------------------------------------##

#Landscape
Set 1 = 100 #Building Dimension X
Set 2 = 40 #Building Dimension Z

	#Downtown ground
	AddGround Ground, Downtown, -158400, -158400, 158400, 158400, 0, 7920, 7920
	Cut Landscape, -%1% - 15, -1, -%2% - 5, %1% + 5, 1, %2% + 5, false, true

	#Perimeter floors
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, FrontSidewalk, Sidewalk, 0, -25, -%2% - 5, 55, -%2%, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, FrontGrass1, Grass, 0, -%1%, -%2% - 5, -25, -%2%, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, FrontGrass2, Grass, 0, 55, -%2% - 5, %1%, -%2%, Floor(0).Base, Floor(0).Base, 0, 0	

	AddFloor landscape, LeftSidewalk, Sidewalk, 0, -%1% - 15, -10, -%1%, 10, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, LeftGrass1, Grass, 0, -%1% - 15, -%2% - 5, -%1%, -10, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, LeftGrass2, Grass, 0, -%1% - 15, 10, -%1%, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, RightGrass, Grass, 0, %1%, -%2% - 5, %1% + 5, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	

	AddFloor landscape, BackSidewalk, Sidewalk, 0, -55, %2%, 0, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, BackGrass1, Grass, 0, -%1%, %2%, -55, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	
	AddFloor landscape, BackGrass2, Grass, 0, 0, %2%, %1%, %2% + 5, Floor(0).Base, Floor(0).Base, 0, 0	

	#Cut Landscape, -%1%, Floor(0).Base - 1, -%2%, %1%, Floor(0).Base + 1, %2%, false, true	
	
##--------------------------------------------------------------------------------##

#Set texture prefixes/suffixes
<Function TexPrefix>
	#Main prefixes
	Set 5 = Shop
<EndFunction>

##--------------------------------------------------------------------------------##

#Exterior & interior
#1st-3rd floors
<Floors 0 to 2>
	Set 1 = 100 #Building Dimension X
	Set 2 = 40 #Building Dimension Z
	TexPrefix() #Set texture prefix/suffix

	#Floors
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Floor, %5%Floor, 0.5, -%1%, -%2%, %1%, %2%, %interfloorheight%, %interfloorheight%, 0, 0, true

		#Atrium base (1st floor)
		if[%floor% = 0] CutAll -30, %interfloorheight% - 1, -15, 35, %interfloorheight% + 1, 15, false, true
		if[%floor% = 0] AddFloor AtriumBase, ABCarpet, 0.5, -30, -15, 35, 15, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Ceilings
	FloorOrientation = bottom
	DrawWalls = true, false, false, false, false, false
	AddFloor Ceiling, %5%Ceiling, 0.5, -%1%, -%2%, %1%, %2%, %fullheight%, %fullheight%, 0, 0, true

	#Atrium cuts
		#Main atrium
		if[%floor% > 0] CutAll -40, %interfloorheight% - 1, -15, 35, %interfloorheight% + 1, 15, false, true
		if[%floor% < 2] CutAll -40, %fullheight% - 1, -15, 35, %fullheight% + 1, 15, false, true

	#Windows
	Set MyIFloorType = 2
	if[%floor% = 0] Set MyIFloorType = 3
	Set WinPrefix = Main

	if[%floor% ! 0] Set WinPrefix2 = Main
	if[%floor% = 0] Set WinPrefix2 = Mall

	SetAutoSize = true, false
	AddCustomWindows(FrontWin1, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1%, -%2%, -%1% + 25, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% ! 0] AddCustomWindows(FrontWin2, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1% + 50, -%2%, %1% - 50, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(FrontWin2A, %WinPrefix%Windows, %WinPrefix%WindowsInt, -%1% + 50, -%2%, -25, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(FrontWin2B, %WinPrefix%Windows, %WinPrefix%WindowsInt, 0, -%2%, %1% - 50, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	AddCustomWindows(FrontWin3, %WinPrefix%Windows, %WinPrefix%WindowsInt, %1% - 25, -%2%, %1%, -%2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)

	AddCustomWindows(LeftWin, %WinPrefix2%Windows, %WinPrefix2%WindowsInt, -%1%, -%2%, -%1%, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	AddCustomWindows(RightWin, %WinPrefix%WindowsInt, %WinPrefix%Windows, %1%, -%2%, %1%, %2%, %height%, 0, 0, %MyIFloorType%, %5%Wall, ExtFacade)

	AddCustomWindows(BackWin1, %WinPrefix%WindowsInt, %WinPrefix%Windows, -%1%, %2%, -%1% + 25, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% ! 0] AddCustomWindows(BackWin2, %WinPrefix%WindowsInt, %WinPrefix%Windows, -%1% + 50, %2%, %1% - 50, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(BackWin2A, %WinPrefix%WindowsInt, %WinPrefix%Windows, -%1% + 50, %2%, -25, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	if[%floor% = 0] AddCustomWindows(BackWin2B, %WinPrefix%WindowsInt, %WinPrefix%Windows, 0, %2%, %1% - 50, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	AddCustomWindows(BackWin3, %WinPrefix%WindowsInt, %WinPrefix%Windows, %1% - 15, %2%, %1%, %2%, %height%, 0, 0, %MyIFloorType%, ExtFacade, %5%Wall)
	SetAutoSize = true, true
	
	#Perimeter walls
	TexPrefix() #Set texture prefix/suffix
	Set WallHeight = %fullheight%
	Set WallOffset = 0
	if[%floor% = 0] Set WallHeight = %height%
	if[%floor% = 0] Set WallOffset = %interfloorheight%

		#Front walls
		WallOrientation = left
		DrawWalls = true, true, true, true, false, false
		TextureOverride ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		AddWall FrontWall_A, ExtFacade, 0.5, -%1% + 25, -%2%, -%1% + 50, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
		TextureOverride ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		if[%floor% = 0] AddWall FrontWall_AA, ExtFacade, 0.5, -25, -%2%, 0, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		DrawWalls = true, false, false, false, false, false
		#TextureOverride ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		AddWall FrontWall_B, ExtFacade, 0.5, %1% - 50, -%2%, %1% - 25, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

		#Back walls
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		#TextureOverride %5%Wall, ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		AddWall BackWall_A, ExtFacade, 0.5, -%1% + 25, %2%, -%1% + 50, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
		DrawWalls = true, true, true, true, false, false
		TextureOverride %5%Wall, ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		if[%floor% = 0] AddWall BackWall_AA, ExtFacade, 0.5, -25, %2%, 0, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
		DrawWalls = true, true, true, false, false, false
		TextureOverride %5%Wall, ExtFacade, %5%Wall, %5%Wall, %5%Wall, %5%Wall
		AddWall BackWall_B, ExtFacade, 0.5, %1% - 50, %2%, %1% - 15, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloors>

<Floors 1 to 2>
	#Atrium railings
	TexPrefix() #Set texture prefixes
	Set WH = %interfloorheight% + 3.5
	Set WO = -%interfloorheight%
	
	WallOrientation = right
	DrawWalls = true, true, true, true, true, false
	if[%floor% > 1] AddWall Atrium_FrontRail, %5%Wall2, 0.5, -40 - 0.5, -15, 35 + 0.5, -15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_FrontRailA, %5%Wall2, 0.5, -40 - 0.5, -15, -40 + 21 + 2, -15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_FrontRailB, %5%Wall2, 0.5, -40 + 21, -15 + 4.5, -40 + 21 + 20, -15 + 4.5, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_FrontRailC, %5%Wall2, 0.5, -40 + 21 + 20, -15, 35 + 0.5, -15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_FrontRailD, %5%Wall2, 0.5, -40 + 21 + 20, -15 - 0.5, -40 + 21 + 20, -15 + 4, %WH%, %WH%, %WO%, %WO%, 0, 0, false

	if[%floor% = 1] AddWall Atrium_LeftRailA, %5%Wall2, 0.5, -40, -15, -40, -10, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% > 1] AddWall Atrium_LeftRailA, %5%Wall2, 0.5, -40, -15 + 4, -40, -10, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_LeftRailB, %5%Wall2, 0.5, -40, 10, -40, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% > 1] AddWall Atrium_LeftRailB, %5%Wall2, 0.5, -40, 10, -40, 15 - 4, %WH%, %WH%, %WO%, %WO%, 0, 0, false

	WallOrientation = left
	if[%floor% = 1] AddWall Atrium_RightRail, %5%Wall2, 0.5, 35, -15 + 8, 35, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% > 1] AddWall Atrium_RightRail, %5%Wall2, 0.5, 35, -15, 35, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false

	if[%floor% > 1] AddWall Atrium_BackRail, %5%Wall2, 0.5, -40 - 0.5, 15, 35 + 0.5, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_BackRailA, %5%Wall2, 0.5, -40 - 0.5, 15, -40 + 21 + 2, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_BackRailB, %5%Wall2, 0.5, -40 + 21, 15 - 4.5, -40 + 21 + 20, 15 - 4.5, %WH%, %WH%, %WO%, %WO%, 0, 0, false
	if[%floor% = 1] AddWall Atrium_BackRailC, %5%Wall2, 0.5, -40 + 21 + 20, 15, 35 + 0.5, 15, %WH%, %WH%, %WO%, %WO%, 0, 0, false

	WallOrientation = right
	if[%floor% = 1] AddWall Atrium_BackRailD, %5%Wall2, 0.5, -40 + 21 + 20, 15 - 4, -40 + 21 + 20, 15 + 0.5, %WH%, %WH%, %WO%, %WO%, 0, 0, false
<EndFloors>

<Floors 0 to 1>
	#Extra floorings around escalators from 2 to 3
		#Floors
		FloorOrientation = top
		DrawWalls = false, true, false, false, false, false
		if[%floor% = 1] AddFloor SouthEscFloor, %5%Floor, 0.5, -40 + 21, -15, -40 + 21 + 20, -15 + 4.5, %interfloorheight%, %interfloorheight%, 0, 0, true
		if[%floor% = 1] AddFloor NorthEscFloor, %5%Floor, 0.5, -40 + 21, 15 - 4.5, -40 + 21 + 20, 15, %interfloorheight%, %interfloorheight%, 0, 0, true

		#Ceilings
		FloorOrientation = bottom
		DrawWalls = true, false, false, false, false, false
		if[%floor% = 0] AddFloor SouthEscCeiling, %5%Ceiling, 0.5, -40 + 21, -15, -40 + 21 + 20, -15 + 4.5, %fullheight%, %fullheight%, 0, 0, true
		if[%floor% = 0] AddFloor NorthEscCeiling, %5%Ceiling, 0.5, -40 + 21, 15 - 4.5, -40 + 21 + 20, 15, %fullheight%, %fullheight%, 0, 0, true
<EndFloors>

<Floor 0>
	#Entrance doors
		#1. South doors
		Set DTex = BLKEntDoorInt
		Set DTex_Ext = BLKEntDoorExt
		#Set DSTex = %DTex%Side
		Set DSTex = BLKEntDoorSide

		#Access point S1
		Set EntHeight = %height%
		Set Door_MainX = -16.25
		Set Door_MainZ = -%2%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 7, 8, %interfloorheight%, true, 0, 0, true

		#Add3DWindows(S1A_WinTop, %Door_MainX%, %Door_MainZ%, front, 7, %EntHeight% - 8.5, 8.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(S1A_WinSide1, %Door_MainX% - 3.5 - 2.125, %Door_MainZ%, front, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(S1A_WinSide2, %Door_MainX% + 3.5 + 2.125, %Door_MainZ%, front, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		Set Door_MainX = -8.75
		Set Door_MainZ = -%2%

		WallOrientation = left
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 8, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex_Ext%, %DTex%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 5, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 7, 8, %interfloorheight%, true, 0, 0, true

		#Add3DWindows(S1B_WinTop, %Door_MainX%, %Door_MainZ%, front, 7, %EntHeight% - 8.5, 8.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(S1B_WinSide1, %Door_MainX% - 3.5 - 2.125, %Door_MainZ%, front, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(S1B_WinSide2, %Door_MainX% + 3.5 + 2.125, %Door_MainZ%, front, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 1/4, 1)

		Set Door_MainX = -(8.75 + 16.25) / 2
		Add3DWindows(S1A_WinTop_Mid, %Door_MainX%, %Door_MainZ%, front, 7 * 2 + 0.5, %EntHeight% - 8.5, 8.25, MainWindowsB, MainWindowsIntB, %5%Wall, 0.5, 2/4, 1)

		#2. North doors
		#Access point N1
		Set EntHeight = %height%
		Set Door_MainX = -16.25
		Set Door_MainZ = %2%

		WallOrientation = right
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 7, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 6, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 7, 8, %interfloorheight%, true, 0, 0, true

		#Add3DWindows(N1A_WinTop, %Door_MainX%, %Door_MainZ%, back, 7, %EntHeight% - 8.5, 8.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(N1A_WinSide1, %Door_MainX% - 3.5 - 2.125, %Door_MainZ%, back, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(N1A_WinSide2, %Door_MainX% + 3.5 + 2.125, %Door_MainZ%, back, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)

		Set Door_MainX = -8.75
		Set Door_MainZ = %2%

		WallOrientation = right
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 7, 75, %Door_MainX% - 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		TextureOverride %DTex%, %DTex_Ext%, %DSTex%, %DSTex%, %DSTex%, %DSTex%
		AddExternalDoor , , false, %DTex%, 0.2, 6, 75, %Door_MainX% + 1.75, %Door_MainZ%, 3.5, 8, %interfloorheight%, 0, 0
		AddFillerWalls %5%Wall, 0.5, %Door_MainX%, %Door_MainZ%, 7, 8, %interfloorheight%, true, 0, 0, true

		#Add3DWindows(N1B_WinTop, %Door_MainX%, %Door_MainZ%, back, 7, %EntHeight% - 8.5, 8.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		#Add3DWindows(N1B_WinSide1, %Door_MainX% - 3.5 - 2.125, %Door_MainZ%, back, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)
		Add3DWindows(N1B_WinSide2, %Door_MainX% + 3.5 + 2.125, %Door_MainZ%, back, 3.75, %EntHeight% - 0.5, 0.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 1/4, 1)

		Set Door_MainX = -(8.75 + 16.25) / 2
		Add3DWindows(S1A_WinTop_Mid, %Door_MainX%, %Door_MainZ%, back, 7 * 2 + 0.5, %EntHeight% - 8.5, 8.25, MainWindowsIntB, MainWindowsB, %5%Wall, 0.5, 2/4, 1)

		#3. West doors
		Set Texture = BLKEntDoor
		Set MainZ = 5
		WallOrientation = left
		TextureOverride %Texture%Ext, %Texture%Int, %Texture%Side, %Texture%Side, %Texture%Side, %Texture%Side
		AddExternalDoor , , false, %Texture%Int, 0.2, 4, 75, -%1%, %MainZ% + 1.75, 3.5, 8.5, %interfloorheight%, 0, 0
		TextureOverride %Texture%Ext, %Texture%Int, %Texture%Side, %Texture%Side, %Texture%Side, %Texture%Side
		AddExternalDoor , , false, %Texture%Int, 0.2, 1, 75, -%1%, %MainZ% - 1.75, 3.5, 8.5, %interfloorheight%, 0, 0

		Set MainZ = -5
		WallOrientation = left
		TextureOverride %Texture%Ext, %Texture%Int, %Texture%Side, %Texture%Side, %Texture%Side, %Texture%Side
		AddExternalDoor , , false, %Texture%Int, 0.2, 4, 75, -%1%, %MainZ% + 1.75, 3.5, 8.5, %interfloorheight%, 0, 0
		TextureOverride %Texture%Ext, %Texture%Int, %Texture%Side, %Texture%Side, %Texture%Side, %Texture%Side
		AddExternalDoor , , false, %Texture%Int, 0.2, 1, 75, -%1%, %MainZ% - 1.75, 3.5, 8.5, %interfloorheight%, 0, 0
<EndFloor>

#Set door textures for restrooms
<Function RR_DTex>
	Set DTex = Door8
	Set DSTex = %DTex%Side
<EndFunction>

<Floors 0 to 2>
	#Restrooms
	TexPrefix() #Set texture prefixes
	RR_DTex() #Set door textures
	Set RRHeight = %height%
	Set RRCeiHeight = 8.5

		#Restrooms' dropped ceiling
		FloorOrientation = bottom
		DrawWalls = true, false, false, false, false, false
		AddFloor RR_DropCeiling, DropCeiling, 0.5, 50, 40 - 18, 75, 40, %RRCeiHeight%, %RRCeiHeight%, 0, 0, false

		#Restroom walls
		WallOrientation = left
		DrawWalls = true, true, false, false, false, false
		AddWall RR_Left, %5%Wall, 0.5, 50, 40 - 18, 50, 40 - 0.5, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false
		AddWall RR_Front, %5%Wall, 0.5, 50, 40 - 18, 75, 40 - 18, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false
		WallOrientation = right
		AddWall RR_Right, %5%Wall, 0.5, 75, 40 - 18, 75, 40, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		WallOrientation = center
		AddWall RR_Middle, %5%Wall, 1, 62.5, 40 - 18, 62.5, 40, %RRHeight%, %RRHeight%, 0, 0, 0, 0, false

		#Restroom doors
		WallOrientation = right
		AddStdDoor Floor %floor%, RR1Door_%floorid%, , , false, %DTex%, %DSTex%, 0.2, back, back, 75, 75, 62.5 + 2.5, 22.5, 3, 7, 0, 0, 0, 0, 0
		AddStdDoor Floor %floor%, RR2Door_%floorid%, , , false, %DTex%, %DSTex%, 0.2, front, back, 75, 75, 62.5 - 2.5, 22.5, 3, 7, 0, 0, 0, 0, 0
		AddFillerWalls ConnectionWall, 0.5, 62.5 + 2.5, 22.5, 3, 7, 0, true, 0, 0, false
		AddFillerWalls ConnectionWall, 0.5, 62.5 - 2.5, 22.5, 3, 7, 0, true, 0, 0, false
	
		#Restroom signs
		#Set BoxTex = BlackTrim
		#Set SignSuffix = _Gray
		Set BoxTex = WoodSign2
		Set SignSuffix = _Brown

			#Signs above doors
			AddBoxSign(RR1_Sign, Sign_RestroomM%SignSuffix%, Sign_RestroomM%SignSuffix%, %BoxTex%, 0.05, true, true, true, 62.5 + 2.5, 22 - 0.025, 1.25, 1.25, 7.5, false)
			AddBoxSign(RR2_Sign, Sign_RestroomW%SignSuffix%, Sign_RestroomW%SignSuffix%, %BoxTex%, 0.05, true, true, true, 62.5 - 2.5, 22 - 0.025, 1.25, 1.25, 7.5, false)
<EndFloors>

#Supports
<Floors 0 to 2>
	TexPrefix() #Set texture prefix/suffix

	#Set values
	Set 0 = 0 #Building Center X
	Set 1 = 0 #Building Center Z
	Set 3 = 100 #Building Dimension X
	Set 4 = 40 #Building Dimension Z
	Set SupportTexture = %5%Wall #Wall Texture=
	Set DistanceX = 25 #Distance between supports X
	Set DistanceZ = 40 #Distance between supports Z

	#Corner Supports
	CreateWallBox2 External, SupportNW, %SupportTexture%, (%0% - %3%) + 1.25, (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportNE, %SupportTexture%, (%0% + %3%) - 1.25, (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSW, %SupportTexture%, (%0% - %3%) + 1.25, (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportSE, %SupportTexture%, (%0% + %3%) - 1.25, (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false

	#North Supports
	#CreateWallBox2 External, SupportN1, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 1), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	#CreateWallBox2 External, SupportN2, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 2), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN3, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 3), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN4, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 4), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN5, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 5), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportN6, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 6), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	#CreateWallBox2 External, SupportN7, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 7), (%1% + %4%) - 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false

	#South Supports
	CreateWallBox2 External, SupportS1, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 1), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS2, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 2), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS3, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 3), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS4, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 4), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	CreateWallBox2 External, SupportS5, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 5), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	#CreateWallBox2 External, SupportS6, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 6), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
	#CreateWallBox2 External, SupportS7, %SupportTexture%, (%0% - %3%) + (%DistanceX% * 7), (%1% - %4%) + 1.25, 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false

	#West Supports
	CreateWallBox2 External, SupportW1, %SupportTexture%, (%0% - %3%) + 1.25, (%1% - %4%) + (%DistanceZ% * 1), 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false

	#East Supports
	CreateWallBox2 External, SupportE1, %SupportTexture%, (%0% + %3%) - 1.25, (%1% - %4%) + (%DistanceZ% * 1), 2, 2, %fullheight%, -%interfloorheight%, 0, 0, false, true, false, false
<EndFloors>

#Roof
<Floor 3>
	Set 1 = 100 #Building Dimension X
	Set 2 = 40 #Building Dimension Z

	#Roof floor
	FloorOrientation = top
	DrawWalls = false, true, false, false, false, false
	AddFloor Roof, RoofFloor, 0.5, -%1%, -%2%, %1%, %2%, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Roof railings
	Set WallHeight = %interfloorheight% + 4.5
	Set WallOffset = 0

	WallOrientation = Left
	DrawWalls = true, true, true, true, true, false
	AddWall FrontRailA, ExtFacade, 0.5, -%1% + 0.5, -%2%, 50, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall FrontRailB, ExtFacade, 0.5, 75, -%2%, %1% - 0.5, -%2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall LeftRail, ExtFacade, 0.5, -%1%, -%2%, -%1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	WallOrientation = Right
	AddWall RightRail, ExtFacade, 0.5, %1%, -%2%, %1%, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall BackRailA, ExtFacade, 0.5, -%1% + 0.5, %2%, -75, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall BackRailB1, ExtFacade, 0.5, -50, %2%, %1% - 25, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
	AddWall BackRailB2, ExtFacade, 0.5, %1% - 15, %2%, %1% - 0.5, %2%, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloor>

<Floors 2 to 3>
	#Atrium skylight cuts
		#Main atrium
		if[%floor% > 2] CutAll -30, %interfloorheight% - 1, -15, 30, %interfloorheight% + 1, 15, false, true
		if[%floor% < 3] CutAll -30, %fullheight% - 1, -15, 30, %fullheight% + 1, 15, false, true

	#Skylight
	DrawWalls = true, true, false, false, false, false
	TextureOverride SkylightInt, Skylight, Blank, Blank, Blank, Blank
	if[%floor% = 3] AddFloor SkylightFloor, SkylightInt, 0, -30, -15, 30, 15, %interfloorheight%, %interfloorheight%, 1.5, 3, true
	
	#Skylight atrium cover
	if[%floor% = 2] CreateWallBox2 floor, SkylightAtriumCover, ShopWall2, 0, 0, 60, 30, Floor(3).InterfloorHeight, %height%, 0, 0, true, false, false, false
<EndFloor>

##--------------------------------------------------------------------------------##

#STAIRWELLS
<Function StairwellSets>
	#Set Values
	Set NumSteps = 14 #Total: 28 steps
<EndFunction>

#West Stairwell
CreateStairwell 1, -62.5, 35, 0, 3
CutStairwell 1, -12, -4.5, 12, 4.5 + 0.5, 0, 5
ShowFullStairs 1 = always

<Floors 0 to 3>
	StairwellSets() #Determine stairs' size

	Set 21 = -62.5 #Center X
	Set 22 = 35 #Center Z
	Set 23 = 12 #Cut X
	Set 24 = 4.5 #Cut Z
	Set 25 = 1 #Stairwell Number
	Set 26 = 0.5 #Riser Size

	#Walls
	WallOrientation = right
	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, Stair%25%Front, StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Left, StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	WallOrientation = left
	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, Stair%25%Right, StairsWall, 0.5, %23%, -%24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Back, StairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	WallOrientation = center
	DrawWalls = true, true, true, true, false, true
	AddStairsWall %25%, Stair%25%Center, StairsWall2, 1, -%23% + 4, 0, -%23% + 4 + %NumSteps%, 0, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	#Railing
	WallOrientation = right
	DrawWalls = true, true, true, false, true, true
	if[%floor% = 3] AddStairsWall %25%, Stair%25%Rail, StairsWall2, 0.5, -%23% + 4 + %NumSteps%, %24% - 4, -%23% + 4 + %NumSteps%, %24%, 0.5 + 3.5, 0.5 + 3.5, -0.5, -0.5, 0, 0

	#Ceiling
	FloorOrientation = top
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 3] AddStairsFloor %25%, Stairs%25%Ceiling, StairsCeiling, 0.5, -%23%, -%24%, %23%, %24%, %height%, %height%, false, false, 0, 0

	#Stairs
	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 3] OP_OrthoStair(%25%, ST%25%_LowerStairs, Stairs, %24% - 4, %24%, right, -%23% + 4 + %NumSteps%, %NumSteps%, 1, %26%, 0, 0.5)
	if[%floor% < 3] OP_OrthoStair(%25%, ST%25%_UpperStairs, Stairs, -%24%, -%24% + 4, left, -%23% + 4, %NumSteps%, 1, %26%, %26% * %NumSteps%, 0.5)

	#Landings
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	if[%floor% = 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23%, %24% + 0.5, 0, 0, false, false, 0, 0
	if[%floor% > 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23% + 4 + %NumSteps%, -%24%, %23%, %24%, 0, 0, false, false, 0, 0
	if[%floor% < 3] AddStairsFloor %25%, Stairs%25%LandingUpper, StairsFloor, 0.5, -%23%, -%24%, -%23% + 4, %24%, %26% * %NumSteps%, %26% * %NumSteps%, false, false, 0, 0

	#Floor Signs
	Set Suffix = %floorid%
	Set SignCenterPoint = 0

	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, ST%25%FloorSign, BlankSign%Suffix%, 0, %23% - 0.001, %SignCenterPoint% - 1, %23% - 0.001, %SignCenterPoint% + 1, 2, 2, 4 - 0.5, 4 - 0.5, 0, 0

	#External walls
	TexPrefix()
	if[%floor% < 3] CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.5, %21%, %22%, %23% + 0.5, %24% + 0.5, ST%25%, %height%, 0, true, true, true, false, false, false, false)
	if[%floor% = 3] CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.5, %21%, %22%, %23% + 0.5, %24% + 0.5, ST%25%, %fullheight%, 0, true, true, true, true, true, false, true)

	#Doors
	WallOrientation = right
	TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 6, 75, %23% - 2, -%24%, 3, 7, 0, 0, 0

		#Emergency exit door
		SetLock 2, 0
		WallOrientation = right
		TextureOverride ServiceDoor2B, ServiceDoor2, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
		if[%floor% = 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 7, 75, %23% - 2, %24% + 0.5, 3, 7, 0, 0, 0
		SetLock 0, 0

		#Manual fillers (external mesh)
		WallOrientation = right
		if[%floor% = 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% + %23% - 2, %22% + %24% + 0.5, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true
		if[%floor% = 3] AddFillerWalls ServiceDoor2Side, 0.5, %21% + %23% - 2, %22% - %24%, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true

	#Exit Signs
	Set SignTex = ExitSign
	AddBoxSign(Stair%25%_SignMain, %SignTex%, StainlessSteel, StainlessSteel, 0.1, true, true, true, %21% + %23% - 2, %22% - %24% - 0.55, 1.8, 0.9, %interfloorheight% + 7.2, true)
	if[%floor% = 0] AddBoxSign(Stair%25%_SignStreetLevel, %SignTex%, StainlessSteel, StainlessSteel, 0.1, true, true, true, %21% + %23% - 2, %22% + %24% - 0.05, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloors>

#East Stairwell
CreateStairwell 2, 62.5, -35, 0, 3
CutStairwell 2, -12, -4.5 - 0.5, 12, 4.5, 0, 5
ShowFullStairs 2 = always

<Floors 0 to 3>
	StairwellSets() #Determine stairs' size

	Set 21 = 62.5 #Center X
	Set 22 = -35 #Center Z
	Set 23 = 12 #Cut X
	Set 24 = 4.5 #Cut Z
	Set 25 = 2 #Stairwell Number
	Set 26 = 0.5 #Riser Size

	#Walls
	WallOrientation = right
	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, Stair%25%Front, StairsWall, 0.5, -%23%, -%24%, %23%, -%24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Left, StairsWall, 0.5, -%23%, -%24%, -%23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	WallOrientation = left
	DrawWalls = true, false, false, false, false, false
	AddStairsWall %25%, Stair%25%Right, StairsWall, 0.5, %23%, -%24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0
	AddStairsWall %25%, Stair%25%Back, StairsWall, 0.5, -%23%, %24%, %23%, %24%, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	WallOrientation = center
	DrawWalls = true, true, true, true, false, true
	AddStairsWall %25%, Stair%25%Center, StairsWall2, 1, %23% - 4 - %NumSteps%, 0, %23% - 4, 0, %fullheight%, %fullheight%, -%interfloorheight%, -%interfloorheight%, 0, 0

	#Railing
	WallOrientation = left
	DrawWalls = true, true, true, false, true, true
	if[%floor% = 3] AddStairsWall %25%, Stair%25%Rail, StairsWall2, 0.5, %23% - 4 - %NumSteps%, -%24%, %23% - 4 - %NumSteps%, -%24% + 4, 0.5 + 3.5, 0.5 + 3.5, -0.5, -0.5, 0, 0

	#Ceiling
	FloorOrientation = top
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 3] AddStairsFloor %25%, Stairs%25%Ceiling, StairsCeiling, 0.5, -%23%, -%24%, %23%, %24%, %height%, %height%, false, false, 0, 0

	#Stairs
	OP_Draw(true, true, true, true, true, true)
	if[%floor% < 3] OP_OrthoStair(%25%, ST%25%_LowerStairs, Stairs, -%24%, -%24% + 4, left, %23% - 4 - %NumSteps%, %NumSteps%, 1, %26%, 0, 0.5)
	if[%floor% < 3] OP_OrthoStair(%25%, ST%25%_UpperStairs, Stairs, %24% - 4, %24%, right, %23% - 4, %NumSteps%, 1, %26%, %26% * %NumSteps%, 0.5)

	#Landings
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	if[%floor% = 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24% - 0.5, %23%, %24%, 0, 0, false, false, 0, 0
	if[%floor% > 0] AddStairsFloor %25%, Stairs%25%LandingLower, StairsFloor, 0.5, -%23%, -%24%, %23% - 4 - %NumSteps%, %24%, 0, 0, false, false, 0, 0
	if[%floor% < 3] AddStairsFloor %25%, Stairs%25%LandingUpper, StairsFloor, 0.5, %23% - 4, -%24%, %23%, %24%, %26% * %NumSteps%, %26% * %NumSteps%, false, false, 0, 0

	#Floor Signs
	Set Suffix = %floorid%
	Set SignCenterPoint = 0

	DrawWalls = false, true, false, false, false, false
	AddStairsWall %25%, ST%25%FloorSign, BlankSign%Suffix%, 0, -%23% + 0.001, %SignCenterPoint% - 1, -%23% + 0.001, %SignCenterPoint% + 1, 2, 2, 4 - 0.5, 4 - 0.5, 0, 0

	#External walls
	TexPrefix()
	if[%floor% < 3] CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.5, %21%, %22%, %23% + 0.5, %24% + 0.5, ST%25%, %height%, 0, false, true, true, true, false, false, false)
	if[%floor% = 3] CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.5, %21%, %22%, %23% + 0.5, %24% + 0.5, ST%25%, %fullheight%, 0, true, true, true, true, true, false, true)

	#Doors
	WallOrientation = left
	TextureOverride ServiceDoor2, ServiceDoor2B, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
	AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 8, 75, -%23% + 2, %24%, 3, 7, 0, 0, 0

		#Emergency exit door
		SetLock 1, 0
		WallOrientation = left
		TextureOverride ServiceDoor2, ServiceDoor2B, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side, ServiceDoor2Side
		if[%floor% = 0] AddStairsDoor %25%, , , false, ServiceDoor2, 0.2, 5, 75, -%23% + 2, -%24% - 0.5, 3, 7, 0, 0, 0
		SetLock 0, 0

		#Manual fillers (external mesh)
		WallOrientation = left
		if[%floor% = 0] AddFillerWalls ServiceDoor2Side, 0.5, %21% - %23% + 2, %22% - %24% - 0.5, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true
		if[%floor% = 3] AddFillerWalls ServiceDoor2Side, 0.5, %21% - %23% + 2, %22% + %24%, 3, 7 - 0.001, %interfloorheight%, true, 0, 0, true

	#Exit Signs
	Set SignTex = ExitSign
	AddBoxSign(Stair%25%_SignMain, StainlessSteel, %SignTex%, StainlessSteel, 0.1, true, true, true, %21% - %23% + 2, %22% + %24% + 0.55, 1.8, 0.9, %interfloorheight% + 7.2, true)
	if[%floor% = 0] AddBoxSign(Stair%25%_SignStreetLevel, StainlessSteel, %SignTex%, StainlessSteel, 0.1, true, true, true, %21% - %23% + 2, %22% - %24% + 0.05, 1.8, 0.9, %interfloorheight% + 7.2, true)
<EndFloors>

##--------------------------------------------------------------------------------##
#ESCALATORS
#--------------------#

#Set escalator properties
	OP_EscBalustrades(GlassBalustrade, 0, 3.5, 0, 0)
	OP_EscLanding(EscalatorLandingX, 5.5, 0, 0, EscalatorLandingX, 5.5, 0, 0)
	OP_EscHandrails(Handrail1, 0.22, 0.125, 0, 0)
	OP_EscTruss(SilverBalustrade, ShopWall, 0.5, 3.5, 0, 0, 0, 0)

#Escalators creation
#1. Escalators from 1 to 2
<Floor 0>
	OP_Escalator(PlazaEsc1-%floorid%, up, %floor% + 1, 1.1, EG881Sounds2/Escalators/otisescalator5.wav, Escalator3Z_Unmarked, Escalator3XB, left, 35, -13, 3, 2/3, 1, 21, 0, 0, 0, 0) #EG881
	OP_Escalator(PlazaEsc2-%floorid%, down, %floor% + 1, 1.1, EG881Sounds2/Escalators/otisescalator5.wav, Escalator3Z_Unmarked, Escalator3XB, left, 35, -13 + 4, 3, 2/3, 1, 21, 0, 0, 0, 0) #EG881
<EndFloor>

#2. Escalators from 2 to 3
<Floor 1>
	OP_Escalator(PlazaEsc1-%floorid%, up, %floor% + 1, 1.1, EG881Sounds2/Escalators/otisescalator5.wav, Escalator3Z_Unmarked, Escalator3X, right, -40, -13, 3, 2/3, 1, 21, 0, 0, 0, 0) #EG881
	OP_Escalator(PlazaEsc2-%floorid%, down, %floor% + 1, 1.1, EG881Sounds2/Escalators/otisescalator5.wav, Escalator3Z_Unmarked, Escalator3X, right, -40, 13, 3, 2/3, 1, 21, 0, 0, 0, 0) #EG881
<EndFloor>

##--------------------------------------------------------------------------------##

#SHAFTS
#Passenger Elevators
AddShaft 1, -40, 0, -1, 3
ShaftCut 1, -4.67 + 0.33, -9.67 + 0.33, 4.67 - 0.33, 9.67 - 0.33, 0, 5
ShowFullShaft 1 = true
ShaftShowOutside 1 = 0 - 2
ShaftShowFloors 1 = 0 - 2

#Shaft pit
<Floor -1>
	Set 8 = -40 #Center X
	Set 9 = 0 #Center Z
	Set 10 = 4.67 #Cut X
	Set 11 = 9.67 #Cut Z
	Set 12 = 1 #Shaft Number
	Set FloorThick = 0.5
	Set WallThick = 0.33
	Set SHWallTex = GLEShaftWall #Wall Texture

	#Shaft Floor
	FloorOrientation = bottom
	DrawWalls = false, true, false, false, false, false
	if[%floor% = -1] AddShaftFloor %12%, Shaft%12%-PitBase, %SHWallTex%, %FloorThick%, -%10%, -%11%, %10%, %11%, 0, 0, 0, 0

	WallOrientation = left
	DrawWalls = true, true, false, false, false, false
	AddShaftWall %12%, Shaft%12%Front, %SHWallTex%, %WallThick%, -%10%, -%11%, %10%, -%11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Left, %SHWallTex%, %WallThick%, -%10%, -%11%, -%10%, %11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	WallOrientation = right
	AddShaftWall %12%, Shaft%12%Right, %SHWallTex%, %WallThick%, %10%, -%11%, %10%, %11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Back, %SHWallTex%, %WallThick%, -%10%, %11%, %10%, %11%, %fullheight%, %fullheight%, 0, 0, 0, 0
<EndFloor>

#Main walls
<Floors 0 to 2>
	Set 8 = -40 #Center X
	Set 9 = 0 #Center Z
	Set 10 = 4.67 #Cut X
	Set 11 = 9.67 #Cut Z
	Set 12 = 1 #Shaft Number
	Set FloorThick = 0.5
	Set WallThick = 0.33
	Set SHWallTex = GLEShaftWall #Wall Texture

	#Shaft Floor
	WallOrientation = left
	DrawWalls = true, true, false, false, false, false
	AddShaftWall %12%, Shaft%12%Front, %SHWallTex%, %WallThick%, -%10%, -%11%, 0, -%11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	if[%floor% = 0] AddShaftWall %12%, Shaft%12%FrontEX, %SHWallTex%, %WallThick%, 0, -%11%, %10%, -%11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Left, %SHWallTex%, %WallThick%, -%10%, -%11%, -%10%, %11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	WallOrientation = right
	if[%floor% = 0] AddShaftWall %12%, Shaft%12%Right, %SHWallTex%, %WallThick%, %10%, -%11%, %10%, %11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Back, %SHWallTex%, %WallThick%, -%10%, %11%, 0, %11%, %fullheight%, %fullheight%, 0, 0, 0, 0
	if[%floor% = 0] AddShaftWall %12%, Shaft%12%BackEX, %SHWallTex%, %WallThick%, 0, %11%, %10%, %11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
<EndFloors>

<Floors -1 to 3>
	Set WallHeight = %fullheight%
	if[%floor% = 3] Set WallHeight = %interfloorheight%

	#Divider walls
	WallOrientation = center
	DrawWalls = true, true, false, false, false, false
	AddShaftWall %12%, Shaft%12%Divider1, GLEShaftWall, 0.66, -%10%, 0, 0, 0, %WallHeight%, %WallHeight%, 0, 0, 0, 0

	#Glass elevator tracks
	Set EachElevWidth = 6.5 #Estimated width for each glass elevator's area
	Set TrackEndpoint = %EachElevWidth% / 2

	WallOrientation = right
	DrawWalls = true, true, false, true, false, false
	AddShaftWall %12%, Shaft%12%Track1, GLETrack, 1, 0, -%11% - 0.33, 0, -5 - %TrackEndpoint%, %WallHeight%, %WallHeight%, 0, 0, 0, 0
	DrawWalls = true, true, true, true, false, false
	AddShaftWall %12%, Shaft%12%Track2, GLETrack, 1, 0, -5 + %TrackEndpoint%, 0, 5 - %TrackEndpoint%, %WallHeight%, %WallHeight%, 0, 0, 0, 0
	DrawWalls = true, true, true, false, false, false
	AddShaftWall %12%, Shaft%12%Track3, GLETrack, 1, 0, 5 + %TrackEndpoint%, 0, %11% + 0.33, %WallHeight%, %WallHeight%, 0, 0, 0, 0
<EndFloors>

#Glass panels
<Floor 0>
	Set WallHeight = 8
	Set TileZ = 1

	Set GLEPanelTex = GLEPanel

	#Shaft 1
	Set 12 = 1 #Shaft Number
	DrawWalls = true, true, false, false, false, false
	if[%floor% ! -1] AddWall Shaft%12%PanelFront, %GLEPanelTex%, 0, -40, -9.34, -35.66, -9.34, %WallHeight%, %WallHeight%, 0, 0, 1/4, %TileZ%, false
	if[%floor% ! -1] AddWall Shaft%12%PanelRight, %GLEPanelTex%, 0, -35.66, -9.34, -35.66, 9.34, %WallHeight%, %WallHeight%, 0, 0, 4/4, %TileZ%, false
	if[%floor% ! -1] AddWall Shaft%12%PanelBack, %GLEPanelTex%, 0, -40, 9.34, -35.66, 9.34, %WallHeight%, %WallHeight%, 0, 0, 1/4, %TileZ%, false
<EndFloor>

#Machine room
<Floor 3>
	Set 8 = -40 #Center X
	Set 9 = 0 #Center Z
	Set 10 = 4.67 #Cut X
	Set 11 = 9.67 #Cut Z
	Set 12 = 1 #Shaft Number
	Set FloorThick = 0.5
	Set WallThick = 0.33
	Set SHWallTex = GLEShaftWall #Wall Texture
	Set SHWallTex2 = ShaftIntWall #Wall Texture (Machine Room)

	#Shaft Floors
	FloorOrientation = top
	DrawWalls = true, true, false, false, false, false
	TextureOverride %SHWallTex%, %SHWallTex2%, Blank, Blank, Blank, Blank
	if[%floor% = 3] AddShaftFloor %12%, Shaft%12%-MRBase, %SHWallTex%, %FloorThick%, -%10% + %WallThick%, -%11% + %WallThick%, %10% - %WallThick%, %11% - %WallThick%, %interfloorheight%, %interfloorheight%, 0, 0
	DrawWalls = true, false, false, false, false, false
	if[%floor% = 3] AddShaftFloor %12%, Shaft%12%-MRTop, %SHWallTex2%, %FloorThick%, -%10%, -%11%, %10%, %11%, %fullheight%, %fullheight%, 0, 0

	#Interfloor walls, to match with the shaft's main part
	WallOrientation = left
	DrawWalls = true, true, false, false, false, false
	AddShaftWall %12%, Shaft%12%Front, %SHWallTex%, %WallThick%, -%10%, -%11%, %10%, -%11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Left, %SHWallTex%, %WallThick%, -%10%, -%11%, -%10%, %11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
	WallOrientation = right
	AddShaftWall %12%, Shaft%12%Right, %SHWallTex%, %WallThick%, %10%, -%11%, %10%, %11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0
	AddShaftWall %12%, Shaft%12%Back, %SHWallTex%, %WallThick%, -%10%, %11%, %10%, %11%, %interfloorheight%, %interfloorheight%, 0, 0, 0, 0

	#Machine room walls
	WallOrientation = left
	DrawWalls = true, true, false, false, false, false
	AddShaftWall %12%, Shaft%12%MRFront, %SHWallTex2%, %WallThick%, -%10%, -%11%, %10%, -%11%, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0
	AddShaftWall %12%, Shaft%12%MRLeft, %SHWallTex2%, %WallThick%, -%10%, -%11%, -%10%, %11%, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0
	WallOrientation = right
	AddShaftWall %12%, Shaft%12%MRRight, %SHWallTex2%, %WallThick%, %10%, -%11%, %10%, %11%, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0
	AddShaftWall %12%, Shaft%12%MRBack, %SHWallTex2%, %WallThick%, -%10%, %11%, %10%, %11%, %height%, %height%, %interfloorheight%, %interfloorheight%, 0, 0
<EndFloor>

#External shaft walls
<Floors 0 to 2>
	TexPrefix()

	#Shaft 1
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, -40 - 2.5, 0, 2.5, 10, Shaft1, %fullheight%, -%interfloorheight%, true, true, false, true, false, false, false)
<EndFloor>

<Floor 3>
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, -40, 0, 5, 10, Shaft1_Top, %height%, %interfloorheight%, true, true, true, true, true, false, true)
<EndFloor>

#Machine room doors
<Floor 3>
	Set DTex = ServiceDoor2
	Set DSTex = %DTex%Side

	Set Door_MainX = -40 - 5
	Set Door_MainZ = 0

	WallOrientation = left
	AddStdDoor DoorManager, %floorid%_Shaft1MR_L, , , false, %DTex%, %DSTex%, 0.2, right, left, 75, 75, %Door_MainX%, %Door_MainZ% + 1.5, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddStdDoor DoorManager, %floorid%_Shaft1MR_R, , , false, %DTex%, %DSTex%, 0.2, left, left, 75, 75, %Door_MainX%, %Door_MainZ% - 1.5, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddFillerWalls ConnectionWall, 0.66 + 0.01, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, false, 0, 0, true
<EndFloor>

########################################

#Service Elevator
AddStdShaft(2, 1, 80, 34, 4.67, 5.67, -1, 3, ShaftIntWall, 0.5, 0.33, 0, 5, %interfloorheight%)

#External walls for shaft 2
<Floors 0 to 2>
	TexPrefix()
	CreateShaftWall(%5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, %5%Wall, 0.33, 80, 34, 5, 6, SH2, %height%, 0, true, false, true, false, false, false, false)
<EndFloors>

<Floor 3>
	CreateShaftWall(ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, ExtFacade, 0.33, 80, 34, 5, 6, SH2, %fullheight%, 0, true, true, true, true, true, false, true)
<EndFloor>

#Machine room doors
<Floor 3>
	Set DTex = ServiceDoor2
	Set DSTex = %DTex%Side

	Set Door_MainX = 80
	Set Door_MainZ = 34 - 6

	WallOrientation = left
	AddStdDoor DoorManager, %floorid%_Shaft1MR_L, , , false, %DTex%, %DSTex%, 0.2, front, front, 75, 75, %Door_MainX% + 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddStdDoor DoorManager, %floorid%_Shaft1MR_R, , , false, %DTex%, %DSTex%, 0.2, back, front, 75, 75, %Door_MainX% - 1.5, %Door_MainZ%, 3, 7, Floor(%floor%).Base, 0, 0, 0, 0
	AddFillerWalls ConnectionWall, 0.66 + 0.01, %Door_MainX%, %Door_MainZ%, 6, 7, %interfloorheight%, true, 0, 0, true
<EndFloor>

##--------------------------------------------------------------------------------##

#ELEVATORS
#Passenger elevators
<Elevator 1>
	Name = Elevator 1
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 2
	AssignedShaft = 1
	CreateElevator true, -1.92, 5, 0
<EndElevator>

<Elevator 2>
	Name = Elevator 2
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 2
	AssignedShaft = 1
	CreateElevator true, -1.92, -5, 0
<EndElevator>

#Button creation function (Elevators 1-3)
<Function BtnPanelE1-3>
	Set PanelNumber = %param1%
	Set BtnPrefix = Button

	if[%PanelNumber% = 1] AddButton %PanelNumber%, , %BtnPrefix%Alarm, %BtnPrefix%Alarm, 1, 1, alarm, 1, 1
	if[%PanelNumber% = 1] AddButton %PanelNumber%, , %BtnPrefix%Phone, %BtnPrefix%Phone, 1, 2, off, 1, 1

	AddButton %PanelNumber%, , %BtnPrefix%3, %BtnPrefix%Lit3, 3, 1, 2, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%2, %BtnPrefix%Lit2, 4, 1, 1, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%1, %BtnPrefix%Lit1, 5, 1, 0, 1, 1

	AddButton %PanelNumber%, , %BtnPrefix%Open, %BtnPrefix%Open, 7, 1, open, 1, 1
	AddButton %PanelNumber%, , %BtnPrefix%Close, %BtnPrefix%Close, 7, 2, close, 1, 1
<EndFunction>

<Elevators 1 to 2>
	Set Prefix = Main
	AutoEnable = false

	#Set textures and dimensions
	M10_ElevCabTex(ElevExt2, %Prefix%ElevFloor, %Prefix%ElevCeiling, %Prefix%ElevWall, %Prefix%ElevWall)
	M10_ElevDimensions(3.84, 6, 8, 1.5, 0.2, true, false, false, true)
	M10_ElevPanels(%Prefix%ElevPanelMain, %Prefix%ElevPanelTop, 1, 1, 1, 0.2, false, true, false, false)

	#Finish elevator assembly
	M10_ElevInterior()
	#--------------------
	#Interior walls detailing
		#1. Panoramic cab
		Floors and ceiling
		DrawWalls = true, false, false, false, false, false
		AddCustomFloor elevator, FloorInt, %Prefix%ElevFloor, 1.92, -3, 1.92 + 2, -3, 1.92 + 3.66, -1.5, 1.92 + 3.66, 1.5, 1.92 + 2, 3, 1.92, 3, 0, 0, 0
		DrawWalls = false, true, false, false, false, false
		AddCustomFloor elevator, FloorExt, ElevExt2, 1.92, -3, 1.92 + 2, -3, 1.92 + 3.66, -1.5, 1.92 + 3.66, 1.5, 1.92 + 2, 3, 1.92, 3, -1.5, 0, 0
		DrawWalls = true, true, false, false, false, false
		AddCustomFloor elevator, Ceiling, Blank, 1.92, -3, 1.92 + 2, -3, 1.92 + 3.66, -1.5, 1.92 + 3.66, 1.5, 1.92 + 2, 3, 1.92, 3, 8 + 1.5, 0, 0

		#Floor covering walls
		DrawWalls = true, false, false, false, false, false
		AddWall GlassWall1_Ext, ElevExt2, 0, 1.92, -3, 1.92 + 2, -3, 1.5, 1.5, -1.5, -1.5, 0, 0
		AddWall GlassWall2_Ext, ElevExt2, 0, 1.92 + 2, -3, 1.92 + 3.66, -1.5, 1.5, 1.5, -1.5, -1.5, 0, 0
		DrawWalls = false, true, false, false, false, false
		AddWall GlassWall3_Ext, ElevExt2, 0, 1.92 + 3.66, -1.5, 1.92 + 3.66, 1.5, 1.5, 1.5, -1.5, -1.5, 0, 0
		DrawWalls = false, true, false, false, false, false
		AddWall GlassWall4_Ext, ElevExt2, 0, 1.92 + 3.66, 1.5, 1.92 + 2, 3, 1.5, 1.5, -1.5, -1.5, 0, 0
		AddWall GlassWall5_Ext, ElevExt2, 0, 1.92 + 2, 3, 1.92, 3, 1.5, 1.5, -1.5, -1.5, 0, 0

		#Glass walls
		DrawWalls = true, true, false, false, false, false
		AddWall GlassWall1, %Prefix%ElevWallGlass, 0, 1.92, -3, 1.92 + 2, -3, 8 + 1.5, 8 + 1.5, 0, 0, 0, 0
		AddWall GlassWall2, %Prefix%ElevWallGlass, 0, 1.92 + 2, -3, 1.92 + 3.66, -1.5, 8 + 1.5, 8 + 1.5, 0, 0, 0, 0
		AddWall GlassWall3, %Prefix%ElevWallGlass, 0, 1.92 + 3.66, -1.5, 1.92 + 3.66, 1.5, 8 + 1.5, 8 + 1.5, 0, 0, 0, 0
		AddWall GlassWall4, %Prefix%ElevWallGlass, 0, 1.92 + 3.66, 1.5, 1.92 + 2, 3, 8 + 1.5, 8 + 1.5, 0, 0, 0, 0
		AddWall GlassWall5, %Prefix%ElevWallGlass, 0, 1.92 + 2, 3, 1.92, 3, 8 + 1.5, 8 + 1.5, 0, 0, 0, 0

		#2. Walls
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall LeftInt-Link, %Prefix%ElevWall, 0, 1.92, -3, 1.92, -2.8, 8, 8, 0, 0, 0, 0
		WallOrientation = right
		DrawWalls = false, true, false, false, false, false
		AddWall RightInt-Link, %Prefix%ElevWall, 0, 1.92, 2.8, 1.92, 3, 8, 8, 0, 0, 0, 0
		WallOrientation = left
		DrawWalls = false, true, true, true, true, true
		AddWall CeilingDeco, %Prefix%ElevCeilingDeco, 0.01, 1.92, -3, 1.92, 3, 1.5, 1.5, 8, 8, 0, 0

		#3. Lights
		Set LightWidth = 2
		Set LightLength = 3
		Set TrimWidth = 0.2
	
		CreateWallBox2 elevator, CL_Main, %Prefix%ElevCeilingLight, 0.1, 0, %LightWidth%, %LightLength%, 0.05, 8 - 0.05, 0, 0, false, true, true, true
	
		#3a. Trims around lights
		CreateWallBox2 elevator, CL_Trim_Front, %Prefix%ElevCeilingTrim2, 0.1, 0 - (%LightLength% / 2) - (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 8 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Back, %Prefix%ElevCeilingTrim2, 0.1, 0 + (%LightLength% / 2) + (%TrimWidth% / 2), %LightWidth%, %TrimWidth%, 0.05, 8 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Left, %Prefix%ElevCeilingTrim, 0.1 - (%LightWidth% / 2) - (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 8 - 0.05, 0, 0, false, true, true, true
		CreateWallBox2 elevator, CL_Trim_Right, %Prefix%ElevCeilingTrim, 0.1 + (%LightWidth% / 2) + (%TrimWidth% / 2), 0, %TrimWidth%, %LightLength% + (%TrimWidth% * 2), 0.05, 8 - 0.05, 0, 0, false, true, true, true

		#4. Downlights
		Set DLOffset = 8 - 0.001
		FloorOrientation = top
		DrawWalls = true, false, false, false, true, true
		AddFloor Downlight1A, %Prefix%ElevCeilingDL, 0, -1.45 - 0.15, -2.55 - 0.15, -1.45 + 0.15, -2.55 + 0.15, %DLOffset%, %DLOffset%, 0, 0
		AddFloor Downlight1B, %Prefix%ElevCeilingDL, 0, 1.65 - 0.15, -2.55 - 0.15, 1.65 + 0.15, -2.55 + 0.15, %DLOffset%, %DLOffset%, 0, 0
		AddFloor Downlight2A, %Prefix%ElevCeilingDL, 0, -1.45 - 0.15, 2.55 - 0.15, -1.45 + 0.15, 2.55 + 0.15, %DLOffset%, %DLOffset%, 0, 0
		AddFloor Downlight2A, %Prefix%ElevCeilingDL, 0, 1.65 - 0.15, 2.55 - 0.15, 1.65 + 0.15, 2.55 + 0.15, %DLOffset%, %DLOffset%, 0, 0

		#5. Panels on walls
		Set WPTex = %Prefix%ElevWallPanel
		CreateWallBox2 elevator, Panel_Front, %WPTex%, 0.1, -2.8 + 0.01, 2, 0.02, 8 - 1.5 - 0.08, 0.75, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_Back, %WPTex%, 0.1, 2.8 - 0.01, 2, 0.02, 8 - 1.5 - 0.08, 0.75, 0, 0, false, true, true, true
	#--------------------
	#Car Doors
	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -1.92

	WallOrientation = right
	AddDoorComponent 1, Left, %ElevDoorTex%, %ElevDoorTex%, 0.2, left, 0.8, 0.6, %IDP%, -2, %IDP%, 0, 7, 0, 0, 0, 0, 0
	AddDoorComponent 1, Right, %ElevDoorTex%2, %ElevDoorTex%2, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 2, 7, 0, 0, 0, 0, 0
	FinishDoors 1, false, false

		#Door Track
		FloorOrientation = top
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevDoorTrackX, ElevExt, ElevExt, ElevExt, ElevExt
		AddFloor DoorTrack, ElevDoorTrackX, 0.05, -2.12, -(2 * 2), -1.92, 2 * 2, 0, 0, 0, 0
	#--------------------
	#Button Panels
	Set Position = 2.4 #Right panel
	Set Position2 = -2.4 #Left panel

		#Panel
		CreatePanel , 7, 2, right, -1.72 + 0.001, %Position%, 0.15, 0.15, 0.95, 0.2, 3.5, 0, 0 #Increased horizontal distance.
		#CreatePanel , 7, 2, right, -1.72 + 0.001, %Position2%, 0.15, 0.15, 0.95, 0.2, 3.5, 0, 0

			#Buttons
			BtnPanelE1-3(1)
			#BtnPanelE1-3(2)

	#Elevator number sign
	DrawWalls = false, true, false, false, false, false
	AddWall ElevNo%elevator%_Inside, IN_ElevNoPL%elevator%, 0, -1.72 + 0.001, 0 - 0.25, -1.72 + 0.001, 0 + 0.25, 0.5, 0.5, 7.05, 7.05, 0, 0

	#Indicators
	AddFloorIndicator Ind_Button, right, -1.72 + 0.001, %Position%, 0.45, 0.45, 5.75
	#AddFloorIndicator Ind_Button, right, -1.72 + 0.001, %Position2%, 0.45, 0.45, 5.75
	#--------------------
	#Shaft components
	DrawWalls = true, true, true, true, true, true
		#Ropes
		RopePosition = 0, 9.5, 0
		RopeTexture = Ropes
	
		#Counterweight
		if[%elevator% < 2] Set Position = 3 + 0.75
		if[%elevator% = 2] Set Position = -3 - 0.75
		CreateCounterweight CWeight_Frame, CWeight, -0.5, %Position%, 2, 6, 0.5, 0.5
	
		#Rails
		AddRails ElevRail, ElevRailSide, 0, 0, true, 3 + 0.06, 0.1
		AddRails ElevRail, ElevRailSide, -0.5, %Position%, false, 1 + 0.06, 0.1
<EndElevators>

#Service elevator
<Elevator 3>
	Name = Elevator 3
	Type = Service
	Speed = 5
	Acceleration = 0.6
	Deceleration = 0.6
	ServicedFloors = 0 - 2
	AssignedShaft = 2
	CreateElevator true, 0.5, 0, 0
	#--------------------
	RecallFloor = 0
	Set Prefix = Service

	#Set textures and dimensions
	M10_ElevCabTex(ElevExt, %Prefix%ElevFloor, %Prefix%ElevCeiling, %Prefix%ElevWall, %Prefix%ElevWall)
	M10_ElevDimensions(6, 9.28, 8, 0.2, 0.2, false, true, true, true)
	M10_ElevPanels(%Prefix%ElevPanelMain, %Prefix%ElevPanelTop, 1.25, 0.25, 1, 0.2, true, false, false, false)

	#Finish elevator assembly
	M10_ElevInterior()
	#--------------------
	#MORE DETAILED INTERIORS
	#1. Panels on walls
	Set WPTex = %Prefix%ElevWallPanel
	Set WPanelHeight = 8 - 0.1 - 0.5

		#Back panels
		CreateWallBox2 elevator, Panel_B1, %WPTex%, 0 - (5.6 * 1/4), 4.44 - 0.01, (5.6 * 1/2) - 0.3, 0.02, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_B2, %WPTex%, 0 + (5.6 * 1/4), 4.44 - 0.01, (5.6 * 1/2) - 0.3, 0.02, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

		#Left panels
		CreateWallBox2 elevator, Panel_L1, %WPTex%, -2.8 + 0.01, 0 - (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_L2, %WPTex%, -2.8 + 0.01, 0, 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_L3, %WPTex%, -2.8 + 0.01, 0 + (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

		#Right panels
		CreateWallBox2 elevator, Panel_R1, %WPTex%, 2.8 - 0.01, 0 - (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_R2, %WPTex%, 2.8 - 0.01, 0, 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true
		CreateWallBox2 elevator, Panel_R3, %WPTex%, 2.8 - 0.01, 0 + (8.88 * 1/3), 0.02, (8.88 * 1/3) - 0.3, %WPanelHeight%, 0.25, 0, 0, false, true, true, true

	#2. Ceiling lights
	CreateWallBox2 elevator, CL_Left1, %Prefix%ElevCeilingLight, -2.8 + (1.5 / 2), 0 - 2.95, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Left2, %Prefix%ElevCeilingLight, -2.8 + (1.5 / 2), 0, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Left3, %Prefix%ElevCeilingLight, -2.8 + (1.5 / 2), 0 + 2.95, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true

	CreateWallBox2 elevator, CL_Right1, %Prefix%ElevCeilingLight, 2.8 - (1.5 / 2), 0 - 2.95, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Right2, %Prefix%ElevCeilingLight, 2.8 - (1.5 / 2), 0, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true
	CreateWallBox2 elevator, CL_Right3, %Prefix%ElevCeilingLight, 2.8 - (1.5 / 2), 0 + 2.95, 1.5, 2.7, 0.1, 8 - 0.1, 0, 0, false, true, true, true
	#--------------------
	#Car Doors
	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -4.64

	WallOrientation = right
	AddDoorComponent 1, 2Speed_Fast, %ElevDoorTex%2, %ElevDoorTex%2, 0.15, left, 0.8, 0.6, 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 2.75, %IDP% - (0.15 * 1), 7, 0, 0, 0, 0, 0
	AddDoorComponent 1, 2Speed_Slow, %ElevDoorTex%2, %ElevDoorTex%2, 0.15, left, 0.4, 0.3, 2.75 - (2.25 * 2), %IDP%, 2.75 - (2.25 * 1), %IDP%, 7, 0, 0, 0, 0, 0
	FinishDoors 1, false, false

		#Door track
		FloorOrientation = top
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevDoorTrackZ, ElevExt, ElevExt, ElevExt, ElevExt
		AddFloor DoorTrack, ElevDoorTrackZ, 0.05, 2.75 - (2.25 * 3), %IDP% - (0.15 * 2), 2.75, %IDP%, 0, 0, 0, 0

		#Top filler
		FloorOrientation = bottom
		DrawWalls = true, true, true, true, true, true
		TextureOverride %Prefix%ElevPanelTop, ElevExt, ElevExt, ElevExt, ElevExt, ElevExt
		AddFloor TopFiller, %Prefix%ElevPanelTop, 0.05, 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 2.75, %IDP%, 7, 7, 0, 0

	#Gap filler
	WallOrientation = right
	DrawWalls = true, true, true, true, true, true
	TextureOverride ElevExt, ElevExt, %Prefix%ElevPanelMain, ElevExt, ElevExt, ElevExt
	AddWall GapFiller, %Prefix%ElevPanelMain, 0.15 * 2, 2.75, %IDP%, 3, %IDP%, 7, 7, 0, 0, 0, 0
	#--------------------
	#Button Panels
	Set Position = -2.275

		#Panel
		CreatePanel , 7, 2, back, %Position%, -4.44 + 0.001, 0.15, 0.15, 0.95, 0.2, 3.5, 0, 0 #Increased horizontal distance.

			#Buttons
			BtnPanelE1-3(1)

	#Elevator number sign
	DrawWalls = false, true, false, false, false, false
	AddWall ElevNo%elevator%_Inside, IN_ElevNoPL%elevator%, 0, 0.5 - 0.25, -4.44 + 0.001, 0.5 + 0.25, -4.44 + 0.001, 0.5, 0.5, 7.05, 7.05, 0, 0

	#Indicator sets
	AddFloorIndicator Ind_Button, back, %Position%, -4.44 + 0.001, 0.5, 0.5, 5.75
	#--------------------
	#Shaft components
	DrawWalls = true, true, true, true, true, true
		#Ropes
		RopePosition = 0, 8 + 0.2, 0
		RopeTexture = Ropes
	
		#Counterweight
		Set Position = -3 - 0.8
		CreateCounterweight CWeight_Frame, CWeight, %Position%, 0, 0.5, 6, 2, 0.5
	
		#Rails
		AddRails ElevRail, ElevRailSide, 0, 0, false, 3 + 0.06, 0.1
		AddRails ElevRail, ElevRailSide, %Position%, 0, true, 1 + 0.06, 0.1
<EndElevator>

#HALL STATIONS
#Set textures (Passenger)
<Function ElevHallTex>
	#Shaft doors
	Set MDoorTexture = ShopShaftDoors

	#Use automatic filler walls or not
	Set AutoFiller = false

	#Door trims
		#Sides
		Set DoorTrimTexture = %MDoorTexture%_Trim

		#Top
		Set DoorTrimTexture_Top = %DoorTrimTexture%

	#Deco dimensions
		#Set SideDecoH = 7.2
		#Set TopDecoH = 0.2
		Set SideDecoH = 9.5
		Set TopDecoH = 2.5

	#Call button panels
	Set BPanelTexture = ElevExtPanels
	Set ButtonPrefix = Button

	#Lantern panels
	Set LPanelTexture = %BPanelTexture%
	Set LanternPrefix = Lantern

		#Lateral offset for lantern panels
		#Set ExtraLateralOffset = 0
		Set ExtraLateralOffset = 0.05
<EndFunction>

#Elevators 1-2
<Floors 0 to 2>
	#Shaft Doors
	ElevHallTex() #Set shaft door and panel textures
	Set StartNo = 1 #Number of the first elevator

	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -1.92 - 0.3

	WallOrientation = right
	AddShaftDoorComponent %StartNo%, 1, Left, %MDoorTexture%, %MDoorTexture%, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 2, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo%, 1, Right, %MDoorTexture%2, %MDoorTexture%, 0.2, left, 0.8, 0.6, %IDP%, -2, %IDP%, 0, 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo%, 1, %AutoFiller%, false

	WallOrientation = right
	AddShaftDoorComponent %StartNo% + 1, 1, Left, %MDoorTexture%, %MDoorTexture%, 0.2, right, 0.8, 0.6, %IDP%, 0, %IDP%, 2, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo% + 1, 1, Right, %MDoorTexture%2, %MDoorTexture%, 0.2, left, 0.8, 0.6, %IDP%, -2, %IDP%, 0, 7, 0, 0, 0, 0, 0
	FinishShaftDoor %StartNo% + 1, 1, %AutoFiller%, false

	#Door tracks
	AddShaftTrack(1, 1, -4.34, -4.14, 5 - 4, 5 + 4, ElevDoorTrackX, ElevExt, 0.05, 1, 7)
	AddShaftTrack(2, 1, -4.34, -4.14, -5 - 4, -5 + 4, ElevDoorTrackX, ElevExt, 0.05, 1, 7)

	#****************************************#

	#Door trims/fillers
	WallTex() #Set filler textures to match with shaft walls
	Set Shaft1CX = -40 #1st shaft for elevator bank
	Set Shaft1CZ = 0

		#1st shaft
		AddDecoObject(Elev1_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, left, true, false, 0.05, 0.2, %Shaft1CX% - 5, %Shaft1CZ% + 5, 4, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)
		AddDecoObject(Elev2_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, left, true, false, 0.05, 0.2, %Shaft1CX% - 5, %Shaft1CZ% - 5, 4, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)

	#****************************************#

	#Call buttons
	CallButtonElevators = 1, 2
	CreateCallButtons %BPanelTexture%, %ButtonPrefix%Up, %ButtonPrefix%UpLit, %ButtonPrefix%Down, %ButtonPrefix%DownLit, %Shaft1CX% - 5 - 0.001, %Shaft1CZ%, 3.5, left, 0.5, 1, true, 0, 0

		#Signs
		DrawWalls = true, false, false, false, false, false
		AddWall E1-2HallSign, FireSign, 0, %Shaft1CX% - 5 - 0.001, %Shaft1CZ% - 0.25, %Shaft1CX% - 5 - 0.001, %Shaft1CZ% + 0.25, 0.75, 0.75, 2.6, 2.6, 0, 0, false
		AddWall E1-2FloorSign, BlankSign%floorid%, 0, %Shaft1CX% - 5 - 0.001, %Shaft1CZ% - 0.75, %Shaft1CX% - 5 - 0.001, %Shaft1CZ% + 0.75, 1.5, 1.5, 6 - 0.75, 6 - 0.75, 0, 0, false

	#****************************************#

	#Indicator sets
	Set Offset = 7.75

		#Set textures
		ACMEInd_LanternTex(%LanternPrefix%Up, %LanternPrefix%UpLit, %LanternPrefix%Down, %LanternPrefix%DownLit, %LPanelTexture%, OutsideInd_Button)

		#Indicator creation
		ACMELobbyInds(left, 1, %Shaft1CX% - 5 - %ExtraLateralOffset%, %Shaft1CZ% + 5, %Offset%, 0, 2)
		ACMELobbyInds(left, 2, %Shaft1CX% - 5 - %ExtraLateralOffset%, %Shaft1CZ% - 5, %Offset%, 0, 2)
<EndFloors>

#Floor signs
<Elevators 1 to 2>
	AddFloorSigns 1, true, Sign, back, -1.92 - 0.5 - 0.33, 0 - 2 + 0.001, 0.35, 0.35, 4.825
	AddFloorSigns 1, true, Sign, front, -1.92 - 0.5 - 0.33, 0 + 2 - 0.001, 0.35, 0.35, 4.825
<EndElevators>

#Set textures (Service)
<Function ElevHallTex_Service>
	#Shaft doors
	Set MDoorTexture = ServiceAShaftDoors

	#Use automatic filler walls or not
	Set AutoFiller = false

	#Door trims
		#Sides
		Set DoorTrimTexture = %MDoorTexture%_Trim

		#Top
		Set DoorTrimTexture_Top = %DoorTrimTexture%

	#Deco dimensions
		Set SideDecoH = 7.2
		Set TopDecoH = 0.2

	#Call button panels
	Set BPanelTexture = ElevExtPanels
	Set ButtonPrefix = Button

	#Lantern panels
	Set LPanelTexture = %BPanelTexture%
	Set LanternPrefix = Lantern

		#Lateral offset for lantern panels
		Set ExtraLateralOffset = 0
<EndFunction>

#Elevator 3
<Floors 0 to 2>
	#Shaft Doors
	ElevHallTex_Service() #Set shaft door and panel textures
	Set StartNo = 3 #Number of the first elevator

	Set ElevDoorTex = %Prefix%ElevDoors
	Set IDP = -4.64 - 0.4

	WallOrientation = right
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Fast, %MDoorTexture%, %MDoorTexture%, 0.15, left, 0.8, 0.6, 2.75 - (2.25 * 1), %IDP%, 2.75, %IDP%, 7, 0, 0, 0, 0, 0
	AddShaftDoorComponent %StartNo%, 1, 2Speed_Slow, %MDoorTexture%2, %MDoorTexture%2, 0.15, left, 0.4, 0.3, 2.75 - (2.25 * 2), %IDP% - (0.15 * 1), 2.75 - (2.25 * 1), %IDP% - (0.15 * 1), 7, 0, -1, 0, -1, 0
	FinishShaftDoor %StartNo%, 1, %AutoFiller%, false

	#Door tracks
	AddShaftTrack(3, 2, 1 - 4.5, 1 + 2.25, -5.34, -5.34 + 0.3, ElevDoorTrackZ, ElevExt, 0.05, 1, 7)

	#Top filler
	AddShaftTrack(3, 2, 1, 1 + 2.25, -5.34, -5.34 + 0.15, %DoorTrimTexture%, ElevExt, 0.05, 2, 7)

	#Gap filler
		WallOrientation = left
		DrawWalls = true, true, true, true, true, true
		TextureOverride ElevExt, ElevExt, %DoorTrimTexture%, %DoorTrimTexture%, ElevExt, ElevExt
		AddShaftWall 2, GapFiller_Elev3, ElevExt, 0.15 * 2, 1 + 2.25, -5.34, 1 + 2.25 + 0.25, -5.34, 7, 7, %interfloorheight%, %interfloorheight%, 0, 0	

	#****************************************#

	#Door trims/fillers
	WallTex() #Set filler textures to match with shaft walls
	Set Shaft1CX = 80 #1st shaft for elevator bank
	Set Shaft1CZ = 34

		#1st shaft
		WallOrientation = left
		AddFillerWalls %5%Wall, 0.66, %Shaft1CX% + 1, %Shaft1CZ% - 6, 4.5, 7, 0, true, 0, 0, false #Elevator 3
		#AddDecoObject(Elev3_Deco, %DoorTrimTexture%, %DoorTrimTexture_Top%, front, true, false, 0.05, 0.2, %Shaft1CX% + 1, %Shaft1CZ% - 6, 4.5, 7, %SideDecoH%, %TopDecoH%, 0, true, %DoorTrimTexture%, 0.66, floor)

	#****************************************#

	#Call buttons
	CallButtonElevators = 3
	CreateCallButtons %BPanelTexture%, %ButtonPrefix%Up, %ButtonPrefix%UpLit, %ButtonPrefix%Down, %ButtonPrefix%DownLit, %Shaft1CX% + 1 - 2.25 - 1.25, %Shaft1CZ% - 6.001, 3.5, front, 0.5, 1, true, 0, 0

		#Signs
		DrawWalls = true, false, false, false, false, false
		AddWall E3HallSign, FireSign, 0, %Shaft1CX% + 1 - 3.5 - 0.25, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 1 - 3.5 + 0.25, %Shaft1CZ% - 6 - 0.001, 0.75, 0.75, 2.6, 2.6, 0, 0, false
		AddWall E3FloorSign, BlankSign%floorid%, 0, %Shaft1CX% + 1 - 3.5 - 0.75, %Shaft1CZ% - 6 - 0.001, %Shaft1CX% + 1 - 3.5 + 0.75, %Shaft1CZ% - 6 - 0.001, 1.5, 1.5, 6 - 0.75, 6 - 0.75, 0, 0, false

	#****************************************#

	#Indicator sets
	Set Offset = 8

		#Indicator creation
		ACMELobbyInds(front, 3, %Shaft1CX% + 1, %Shaft1CZ% - 6 - %ExtraLateralOffset%, %Offset%, 0, 2)
<EndFloors>

#Floor signs
<Elevator 3>
	AddFloorSigns 1, true, Sign, right, 0.5 - 2.25 + 0.001, -4.64 - 0.7 - 0.33, 0.35, 0.35, 4.825
	AddFloorSigns 1, true, Sign, left, 0.5 + 2.25 - 0.001, -4.64 - 0.7 - 0.33, 0.35, 0.35, 4.825
<EndElevator>

#Global settings
<Elevators 1 to 3>
	#Notifications
	UpChimeSound 1 = chime2-up.wav
	DownChimeSound 1 = chime2-down.wav
	NotifyEarly = 1
	QueueResets = true
	
	#Motor sounds
	MotorStartSound = EG881Sounds/motor_start3.wav #EG881
	MotorRunSound = EG881Sounds/motor_running3.wav #EG881
	MotorStopSound = EG881Sounds/motor_stop3.wav #EG881

	#Elevator movement sounds
	CarStartSound = EG881Sounds/slowelevstart.wav #EG881
	CarMoveSound = EG881Sounds/slowelevmove.wav #EG881
	CarStopSound = EG881Sounds/slowelevstop.wav #EG881
<EndElevators>

<Elevators 1 to 2>
	#Door settings
	DoorTimer 1 = 12000
	QuickClose 1 = 8000
	OpenSound 1 = EG881Sounds/elevopenquick2.wav #EG881
	CloseSound 1 = EG881Sounds/elevclosequick2.wav #EG881
<EndElevators>

<Elevator 3>
	#Door settings
	DoorTimer 1 = 15000
	QuickClose 1 = 10000
	OpenSound 1 = EG881Sounds/freightelevatoropen1.wav #EG881
	CloseSound 1 = EG881Sounds/freightelevatorclose1.wav #EG881
<EndElevator>