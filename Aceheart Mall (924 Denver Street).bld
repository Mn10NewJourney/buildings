#Aceheart Mall - Entire Complex
#Copyright (c) 2023-Present - Mn10

#Originally started on: ~26 August 2023

########################################

#Updated 2-4 March 2025:
	#1. Reset the floor altitudes to make the 1st floor immediately the street level.
	#2. Removed the entrance stairs, railings, and entrance elevators. (I sincerely apologize for people who liked
	#those elevators, but I hope you will find the new design better whenever you see it "Clearly from Your Eyes.")
	
	#NEW BUILDING HEIGHTS:
	#South PG Roof (Level 5): 80 ft.
	#North PG Roof (Level 4A): 70 ft.
	#Mall Roofs:
		#80 ft. (5th floor Roof Garden)
		#100 ft. (6th floor service area)
	#Office Tower Roof (25th floor): 340 ft.

########################################

#PREVIOUS UPDATES:
#Updated 10 November 2024: Separated the mall, north PG, and office tower into its own file.

########################################

#SCRIPTS TO BE INCLUDED
	#1. My new scripts
	<Include buildings/Mn10_Scripts/Deco2023_R1.txt>
	<Include buildings/Mn10_Scripts/Shafts_and_Elevators.txt>
	<Include buildings/Mn10_Scripts/Windows.txt>
	
	#2. ObjectPlus and SROC (credit to EG881)
	<Include ObjectPlus/Scripts/OP_Script2.txt> #EG881
	<Include SROC/Scripts/r1_script.txt> #EG881

	#3. My previous scripts
	<Include CustomObjects2018/Scripts/r1_script.txt>
	<Include Shaft Wall Creator/Scripts/r1_script.txt>
	<Include data/ACME Floor Indicator/Scripts/Indicators.txt>

#Add buildings
<Buildings>
	Load Aceheart Mall - South Parking Garage.bld, 0, 0, -325, 0, -140 - 10, 0, -90 - 10, 140 + 10, 0, 90 + 8
	Load Aceheart Mall - Main Complex.bld, 0, 0, 0, 0, -140 - 10, 0, -195 - 8, 140 + 10, 0, 195 + 10
<EndBuildings>

##--------------------------------------------------------------------------------##

#Globals
<Globals>
	Name = Aceheart Mall
	Designer = Mn10 (Fixtures created by Windows446)
	Location = 924 Denver Street, Tranquility City
	Description = A mall frequently visited by people leaving Tranquility City to the west.
	Version = 1
	CameraFloor = 0
	CameraPosition = 100, 270
	CameraRotation = 0, 180, 0
<EndGlobals>

##--------------------------------------------------------------------------------##

#Textures
<Textures>
	#Commons
	Load data/Melvin/transparent128.png, Blank, 1, 1, false
	Load data/EG881/blackdoortrim.jpg, BlackTrim, 1, 1, false #EG881
	Load data/gray2-sm.jpg, ConnectionWall, 1, 1, true
	Load data/text16.jpg, WoodSign2, 1, 1, false
	#Load data/signs_mod/elevhallsign1.png, ElevHallSign, 1, 1, false

	#Landscapes
	Load data/downtown.jpg, Downtown, 1, 1
	Load data/walkway.jpg, Sidewalk, 0.03125, 0.03125

		#Plants
		#Load data/ogrebullet/textures/grass.jpg, Grass, 0.005, 0.005
		#Load data/ogrebullet/materials/textures/grass_1024.jpg, Grass_Light, 0.005, 0.005
		Load data/EG881/grass2.jpg, Grass_Dark, 0.01, 0.01 #EG881

		Load data/EG881/Trees_Summer/BradfordPear1.png, Tree1, 1, 1, false #EG881
		Load data/EG881/tree1.gif, Tree2, 1, 1, false #EG881
		Load data/EG881/cem_tr2b.jpg, FlowerBed, 0.075225, 0.0625 #EG881

		#Roads
		Load data/grndroad1mid.jpg, Road, 0.04, 0.04
		Load data/Melvin/Roads/3lane-oneway-90de.png, AccessRoad, 0.0278, 0.05 #36 feet wide
		Load data/Melvin/Roads/2lane-oneway.png, AccessRoad2, 0.05, 0.041667

		#Road markings
		#Load data/EG881/parkingspacex.png, ParkingSpaceX, 1, 1, false #EG881
		#Load data/EG881/parkingspacez.png, ParkingSpaceZ, 1, 1, false #EG881
		#Load data/EG881Mod/NoParkingX.png, NoParkingX, 1, 1, false #EG881 #Yellow islands
		Load data/EG881Mod/NoParkingZ.png, NoParkingZ, 1, 1, false #EG881
		#Load data/EG881Mod/WhiteIslandX.png, WhiteIslandX, 1, 1, false #EG881
		#Load data/EG881Mod/WhiteIslandZ.png, WhiteIslandZ, 1, 1, false #EG881

	#Concrete
	Load data/graniteTexture_0005b.jpg, ExtFacade, 0.0625, 0.0625, true
	Load data/granite_0008b.jpg, PGExtFacade, 0.075, 0.075, true #North PG external walls
	Load data/EG881/Concrete/smoothconcrete1.jpg, PGExtFacade2, 0.0625, 0.0625, true #EG881
	Load data/EG881/Concrete/smoothconcrete1_white.jpg, PGExtFacade3, 0.0625, 0.0625, true #EG881 #South PG external walls
	Load data/EG881/Concrete/smoothconcrete1_white.jpg, PGLinkUDS, 0.0625, 0.0625, true #EG881 #Link between both garages (UDS = Underside)

	#Windows
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky1.jpg, MainWindows, true, 0.05, 1 #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsInt, 0.05, 1 #EG881
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky1.jpg, MainWindowsB, true, 1, 1, false #EG881
	Load data/EG881/windows11h_insideblack.png, MainWindowsIntB, 1, 1, false #EG881
	LoadAlphaBlend data/EG881/windows11h_black2.jpg, data/windows11ispec.png, data/sky3j.jpg, Skylight, true, 1, 1, false #EG881
	Load data/EG881/windows11h_insideblack.png, SkylightInt, 1, 1, false #EG881

	#Main textures
		#Parking levels and ramps
		Load data/EG881/cem_tr3.jpg, ParkingFloor, 0.075225, 0.0625 #EG881
		Load data/EG881/cem_tr2b.jpg, ParkingFloor2, 0.075225, 0.0625 #EG881
		Load data/stucco4b.jpg, ParkingWall, 0.25, 0.25 #Perimeter walls
		Load data/EG881/cem_tr2b.jpg, ParkingCeiling, 0.075225, 0.0625 #EG881

		Load data/Carpet0021d_S.jpg, ParkingLobbyFloor, 0.2, 0.2
		Load data/marb148.jpg, ParkingLobbyWall_Lower, 0.25, 0.25
		Load data/EG881/stucco22b_white.jpg, ParkingLobbyWall, 0.125, 0.125 #EG881
		Load data/EG881/ceilingtile3x3.jpg, ParkingLobbyCeiling, 0.125, 0.125 #EG881
<EndTextures>

##--------------------------------------------------------------------------------##

#Floor assignments
<Floor 0>
	ID = 1
	Name = Level %floorid%
	Type = Lobby
	Height = 8.5
	InterfloorHeight = 1.5
	Altitude = -1.5
<EndFloor>

<Floor 1>
	ID = 1A
	Name = Level %floorid%
	Type = Mezzanine
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 2>
	ID = 2
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 3>
	ID = 2A
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 4>
	ID = 3
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 5>
	ID = 3A
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 6>
	ID = 4
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

<Floor 7>
	ID = 4A
	Name = Level %floorid%
	Type = Recreation
	Height = 8.5
	InterfloorHeight = 1.5
<EndFloor>

##--------------------------------------------------------------------------------##

#Grass creation function
<Function GrassCreator>
	Set GrassName = %param1%
	Set GrassTex = %param2%
	Set X1 = %param3%
	Set Z1 = %param4%
	Set X2 = %param5%
	Set Z2 = %param6%
	Set Offset = %param7%
	
	#Cut ground for grass
	Cut Landscape, %X1%, %Offset% - 1, %Z1%, %X2%, %Offset% + 1, %Z2%, false, true

	#Grass creation
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, %GrassName%, %GrassTex%, 0, %X1%, %Z1%, %X2%, %Z2%, %Offset%, %Offset%, 0, 0	
<EndFunction>

##--------------------------------------------------------------------------------##

#Landscape
#Coordinate Set
Set MainX = 4100
Set MainZ = -2680
Set InAceheart = true

#Tranquility City Landscape
<Include buildings/KW_Maps/Tranquility City.txt>

	#Downtown ground
	Set 1 = 140 #Main Complex Dimension X
	Set 2 = 195 #Main Complex Dimension Z

	#AddGround Ground, Downtown, -158400, -158400, 158400, 158400, 0, 7920, 7920
	Cut Landscape, -%1% - 10 - 36, -1, -%2% - 32, %1% + 10 + 36, 1, %2% + 100, false, true

	#Foundation for Main Complex (Will be cut when the complex is loaded)
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, MainComplexFoundation, ParkingCeiling, 0, -%1% - 10, -%2% - 8, %1% + 10, %2% + 10, Floor(0).Base, Floor(0).Base, false, true, 0, 0	

	#Sidewalk
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, NorthSidewalk, Sidewalk, 0, -%1% - 10, %2% + 10, %1% + 10, %2% + 100, Floor(0).Base, Floor(0).Base, false, true, 0, 0	

	#Grass
	GrassCreator(WestGrass1, Grass_Dark, -%1%, 195, -100, 295, 0)
	GrassCreator(WestGrass2A, Grass_Dark, -60, 195, 20, 245 - 5, 0)
	GrassCreator(WestGrass2B, Grass_Dark, -60, 245 + 5, 20, 295, 0)

	GrassCreator(EastGrass1A, Grass_Dark, 40, 195, 80, 245 - 5, 0)
	GrassCreator(EastGrass1B, Grass_Dark, 40, 245 + 5, 80, 295, 0)
	GrassCreator(EastGrass2, Grass_Dark, 120, 190, %1%, 295, 0)

	#Trees
		#East trees
		AddTree(Tree1, 130, 235, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, 130, 235 + 15, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, 130, 235 + 30, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, 130, 235 + 45, 13.5, 12, Floor(0).Base)

		#West trees
		AddTree(Tree1, -130, 235, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -130, 235 + 15, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -130, 235 + 30, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -130, 235 + 45, 13.5, 12, Floor(0).Base)

		AddTree(Tree1, -112.5, 235, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -112.5, 235 + 15, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -112.5, 235 + 30, 13.5, 12, Floor(0).Base)
		AddTree(Tree1, -112.5, 235 + 45, 13.5, 12, Floor(0).Base)

	#Roads
	AddFloor landscape, EntranceRoad, AccessRoad, 0, %1% + 10, -%2% + 60, %1% + 10 + 36, %2% + 100, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, ExitRoad, AccessRoad, 0, -%1% - 10 - 36, -%2% + 60, -%1% - 10, %2% + 100, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, SouthPGConnectorRoad, AccessRoad2, 0, -%1% - 10, -%2% - 32, %1% + 10, -%2% - 8, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, ExitRoadCorner, Road, 0, -%1% - 10 - 36, -%2% - 32, -%1% - 10, -%2%, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, EntrRoadCorner, Road, 0, %1% + 10, -%2% - 32, %1% + 10 + 36, -%2%, Floor(0).Base, Floor(0).Base, 0, 0

	#Driveways
	DrawWalls = false, true, false, false, false, false
	AddFloor landscape, EntrDrive, Road, 0, %1% + 10, -%2%, %1% + 10 + 36, -%2% + 60, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, ExitDrive, Road, 0, -%1% - 10 - 36, -%2%, -%1% - 10, -%2% + 60, Floor(0).Base, Floor(0).Base, 0, 0

	#Access driveways from Denver Street
	Cut Landscape, -%1% - 10 - 36, Floor(0).Base - 1, %2% + 100, -%1% - 10, Floor(0).Base + 1, %2% + 115, false, true
	Cut Landscape, %1% + 10, Floor(0).Base - 1, %2% + 100, %1% + 10 + 36, Floor(0).Base + 1, %2% + 115, false, true

	DrawWalls = true, true, false, false, false, false
	AddFloor landscape, EntrDrive, Road, 0, %1% + 10, %2% + 100, %1% + 10 + 36, %2% + 115, Floor(0).Base, Floor(0).Base, 0, 0
	AddFloor landscape, ExitDrive, Road, 0, -%1% - 10 - 36, %2% + 100, -%1% - 10, %2% + 115, Floor(0).Base, Floor(0).Base, 0, 0

#############################################
#New divider lines "For Clarity in My Eyes."
#############################################

<Floors 2 to 7>
	Set 1 = 140 #Building Dimension X
	Set 2 = 195 #Building Dimension Z

	#Floors
	FloorOrientation = top
	#DrawWalls = false, true, false, false, false, false

		#Link to South Garage
		Set UDTex = ParkingCeiling
		if[%floor% = 2] Set UDTex = PGExtFacade3
	
		DrawWalls = true, true, false, false, false, false	
		TextureOverride %UDTex%, Road, Blank, Blank, Blank, Blank
		AddFloor North-SouthPGLink, Road, 0.5, 100, -%2% - 32, %1%, -%2% - 10, %interfloorheight%, %interfloorheight%, 0, 0, true
		DrawWalls = true, false, false, false, false, false	
		if[%floor% = 2] AddFloor North-SouthPGLink, PGLinkUDS, %interfloorheight%, 100 + 0.5, -%2% - 32, %1% - 0.5, -%2% - 10, %interfloorheight%, %interfloorheight%, 0, 0, true

	#Road markings
	#Sets to determine odd/even floors
	Set Mod_Temp = mod(%floor%, 2)
	Set Mod = abs(%Mod_Temp%)

		Set Offset = %interfloorheight% + 0.01
		DrawWalls = false, true, false, false, false, false

		#Spacers
			AddFloor SouthPGLinkIsland, NoParkingZ, 0, 123.5 - 5.5, -%2% - 32, 123.5, -%2% - 10, %Offset%, %Offset%, 1, 1, true

	#Parking perimeter walls
	Set WallHeight = %interfloorheight% + 4.5
	if[%floor% = 8] Set WallHeight = %interfloorheight%
	Set WallOffset = 0
	Set PGSouthEnd = -%2%

	Set UDSTex = PGLinkUDS
	if[%floor% > 2] Set UDSTex = PGExtFacade2

	WallOrientation = left
	DrawWalls = true, true, true, true, true, true
	TextureOverride PGExtFacade3, PGExtFacade2, PGExtFacade, PGExtFacade2, PGExtFacade2, %UDSTex%
	AddWall PGLeft-LinktoSouthGarage, PGExtFacade, 0.5, 100, %PGSouthEnd% - 32, 100, %PGSouthEnd% - 10, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true

	WallOrientation = right
	DrawWalls = true, true, true, true, true, true
	TextureOverride PGExtFacade2, PGExtFacade3, PGExtFacade, PGExtFacade, PGExtFacade2, %UDSTex%
	AddWall PGRight-LinktoSouthGarage, PGExtFacade, 0.5, %1%, %PGSouthEnd% - 32, %1%, %PGSouthEnd% - 10, %WallHeight%, %WallHeight%, %WallOffset%, %WallOffset%, 0, 0, true
<EndFloors>

#############################################
#End of the "Clarity Divider Lines."
#############################################